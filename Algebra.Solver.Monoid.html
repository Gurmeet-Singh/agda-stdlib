<!DOCTYPE HTML>
<html><head><meta charset="utf-8"><title>Algebra.Solver.Monoid</title><link rel="stylesheet" href="Agda.css"></head><body><pre class="Agda"><a id="1" class="Comment">------------------------------------------------------------------------</a>
<a id="74" class="Comment">-- The Agda standard library</a>
<a id="103" class="Comment">--</a>
<a id="106" class="Comment">-- A solver for equations over monoids</a>
<a id="145" class="Comment">------------------------------------------------------------------------</a>

<a id="219" class="Symbol">{-#</a> <a id="223" class="Keyword">OPTIONS</a> <a id="231" class="Pragma">--cubical-compatible</a> <a id="252" class="Pragma">--safe</a> <a id="259" class="Symbol">#-}</a>

<a id="264" class="Keyword">open</a> <a id="269" class="Keyword">import</a> <a id="276" href="Algebra.html" class="Module">Algebra</a>

<a id="285" class="Keyword">module</a> <a id="292" href="Algebra.Solver.Monoid.html" class="Module">Algebra.Solver.Monoid</a> <a id="314" class="Symbol">{</a><a id="315" href="Algebra.Solver.Monoid.html#315" class="Bound">m₁</a> <a id="318" href="Algebra.Solver.Monoid.html#318" class="Bound">m₂</a><a id="320" class="Symbol">}</a> <a id="322" class="Symbol">(</a><a id="323" href="Algebra.Solver.Monoid.html#323" class="Bound">M</a> <a id="325" class="Symbol">:</a> <a id="327" href="Algebra.Bundles.html#6016" class="Record">Monoid</a> <a id="334" href="Algebra.Solver.Monoid.html#315" class="Bound">m₁</a> <a id="337" href="Algebra.Solver.Monoid.html#318" class="Bound">m₂</a><a id="339" class="Symbol">)</a> <a id="341" class="Keyword">where</a>

<a id="348" class="Keyword">open</a> <a id="353" class="Keyword">import</a> <a id="360" href="Data.Fin.Base.html" class="Module">Data.Fin.Base</a> <a id="374" class="Symbol">as</a> <a id="377" class="Module">Fin</a>
<a id="381" class="Keyword">import</a> <a id="388" href="Data.Fin.Properties.html" class="Module">Data.Fin.Properties</a> <a id="408" class="Symbol">as</a> <a id="411" class="Module">Fin</a>
<a id="415" class="Keyword">open</a> <a id="420" class="Keyword">import</a> <a id="427" href="Data.List.Base.html" class="Module">Data.List.Base</a> <a id="442" class="Keyword">hiding</a> <a id="449" class="Symbol">(</a><a id="450" href="Data.List.Base.html#6314" class="Function">lookup</a><a id="456" class="Symbol">)</a>
<a id="458" class="Keyword">import</a> <a id="465" href="Data.List.Relation.Binary.Equality.DecPropositional.html" class="Module">Data.List.Relation.Binary.Equality.DecPropositional</a> <a id="517" class="Symbol">as</a> <a id="520" class="Module">ListEq</a>
<a id="527" class="Keyword">open</a> <a id="532" class="Keyword">import</a> <a id="539" href="Data.Maybe.Base.html" class="Module">Data.Maybe.Base</a> <a id="555" class="Symbol">as</a> <a id="558" class="Module">Maybe</a>
  <a id="566" class="Keyword">using</a> <a id="572" class="Symbol">(</a><a id="573" href="Agda.Builtin.Maybe.html#118" class="Datatype">Maybe</a><a id="578" class="Symbol">;</a> <a id="580" href="Data.Maybe.Base.html#1241" class="Function">decToMaybe</a><a id="590" class="Symbol">;</a> <a id="592" href="Data.Maybe.Base.html#1847" class="Function">From-just</a><a id="601" class="Symbol">;</a> <a id="603" href="Data.Maybe.Base.html#1935" class="Function">from-just</a><a id="612" class="Symbol">)</a>
<a id="614" class="Keyword">open</a> <a id="619" class="Keyword">import</a> <a id="626" href="Data.Nat.Base.html" class="Module">Data.Nat.Base</a> <a id="640" class="Keyword">using</a> <a id="646" class="Symbol">(</a><a id="647" href="Agda.Builtin.Nat.html#186" class="Datatype">ℕ</a><a id="648" class="Symbol">)</a>
<a id="650" class="Keyword">open</a> <a id="655" class="Keyword">import</a> <a id="662" href="Data.Product.html" class="Module">Data.Product</a>
<a id="675" class="Keyword">open</a> <a id="680" class="Keyword">import</a> <a id="687" href="Data.Vec.Base.html" class="Module">Data.Vec.Base</a> <a id="701" class="Keyword">using</a> <a id="707" class="Symbol">(</a><a id="708" href="Data.Vec.Base.html#1086" class="Datatype">Vec</a><a id="711" class="Symbol">;</a> <a id="713" href="Data.Vec.Base.html#1643" class="Function">lookup</a><a id="719" class="Symbol">)</a>
<a id="721" class="Keyword">open</a> <a id="726" class="Keyword">import</a> <a id="733" href="Function.Base.html" class="Module">Function.Base</a> <a id="747" class="Keyword">using</a> <a id="753" class="Symbol">(</a><a id="754" href="Function.Base.html#1115" class="Function Operator">_∘_</a><a id="757" class="Symbol">;</a> <a id="759" href="Function.Base.html#2003" class="Function Operator">_$_</a><a id="762" class="Symbol">)</a>
<a id="764" class="Keyword">open</a> <a id="769" class="Keyword">import</a> <a id="776" href="Relation.Binary.html" class="Module">Relation.Binary</a> <a id="792" class="Keyword">using</a> <a id="798" class="Symbol">(</a><a id="799" href="Relation.Binary.Definitions.html#5914" class="Function">Decidable</a><a id="808" class="Symbol">)</a>

<a id="811" class="Keyword">open</a> <a id="816" class="Keyword">import</a> <a id="823" href="Relation.Binary.PropositionalEquality.Core.html" class="Module">Relation.Binary.PropositionalEquality.Core</a> <a id="866" class="Symbol">as</a> <a id="869" class="Module">P</a> <a id="871" class="Keyword">using</a> <a id="877" class="Symbol">(</a><a id="878" href="Agda.Builtin.Equality.html#133" class="Datatype Operator">_≡_</a><a id="881" class="Symbol">)</a>
<a id="883" class="Keyword">import</a> <a id="890" href="Relation.Binary.Reflection.html" class="Module">Relation.Binary.Reflection</a>
<a id="917" class="Keyword">open</a> <a id="922" class="Keyword">import</a> <a id="929" href="Relation.Nullary.html" class="Module">Relation.Nullary</a>
<a id="946" class="Keyword">import</a> <a id="953" href="Relation.Nullary.Decidable.html" class="Module">Relation.Nullary.Decidable</a> <a id="980" class="Symbol">as</a> <a id="983" class="Module">Dec</a>

<a id="988" class="Keyword">open</a> <a id="993" href="Algebra.Bundles.html#6016" class="Module">Monoid</a> <a id="1000" href="Algebra.Solver.Monoid.html#323" class="Bound">M</a>
<a id="1002" class="Keyword">open</a> <a id="1007" class="Keyword">import</a> <a id="1014" href="Relation.Binary.Reasoning.Setoid.html" class="Module">Relation.Binary.Reasoning.Setoid</a> <a id="1047" href="Algebra.Structures.html#1296" class="Function">setoid</a>

<a id="1055" class="Comment">------------------------------------------------------------------------</a>
<a id="1128" class="Comment">-- Monoid expressions</a>

<a id="1151" class="Comment">-- There is one constructor for every operation, plus one for</a>
<a id="1213" class="Comment">-- variables; there may be at most n variables.</a>

<a id="1262" class="Keyword">infixr</a> <a id="1269" class="Number">5</a> <a id="1271" href="Algebra.Solver.Monoid.html#1346" class="InductiveConstructor Operator">_⊕_</a>

<a id="1276" class="Keyword">data</a> <a id="Expr"></a><a id="1281" href="Algebra.Solver.Monoid.html#1281" class="Datatype">Expr</a> <a id="1286" class="Symbol">(</a><a id="1287" href="Algebra.Solver.Monoid.html#1287" class="Bound">n</a> <a id="1289" class="Symbol">:</a> <a id="1291" href="Agda.Builtin.Nat.html#186" class="Datatype">ℕ</a><a id="1292" class="Symbol">)</a> <a id="1294" class="Symbol">:</a> <a id="1296" href="Agda.Primitive.html#320" class="Primitive">Set</a> <a id="1300" class="Keyword">where</a>
  <a id="Expr.var"></a><a id="1308" href="Algebra.Solver.Monoid.html#1308" class="InductiveConstructor">var</a> <a id="1312" class="Symbol">:</a> <a id="1314" href="Data.Fin.Base.html#1312" class="Datatype">Fin</a> <a id="1318" href="Algebra.Solver.Monoid.html#1287" class="Bound">n</a> <a id="1320" class="Symbol">→</a> <a id="1322" href="Algebra.Solver.Monoid.html#1281" class="Datatype">Expr</a> <a id="1327" href="Algebra.Solver.Monoid.html#1287" class="Bound">n</a>
  <a id="Expr.id"></a><a id="1331" href="Algebra.Solver.Monoid.html#1331" class="InductiveConstructor">id</a>  <a id="1335" class="Symbol">:</a> <a id="1337" href="Algebra.Solver.Monoid.html#1281" class="Datatype">Expr</a> <a id="1342" href="Algebra.Solver.Monoid.html#1287" class="Bound">n</a>
  <a id="Expr._⊕_"></a><a id="1346" href="Algebra.Solver.Monoid.html#1346" class="InductiveConstructor Operator">_⊕_</a> <a id="1350" class="Symbol">:</a> <a id="1352" href="Algebra.Solver.Monoid.html#1281" class="Datatype">Expr</a> <a id="1357" href="Algebra.Solver.Monoid.html#1287" class="Bound">n</a> <a id="1359" class="Symbol">→</a> <a id="1361" href="Algebra.Solver.Monoid.html#1281" class="Datatype">Expr</a> <a id="1366" href="Algebra.Solver.Monoid.html#1287" class="Bound">n</a> <a id="1368" class="Symbol">→</a> <a id="1370" href="Algebra.Solver.Monoid.html#1281" class="Datatype">Expr</a> <a id="1375" href="Algebra.Solver.Monoid.html#1287" class="Bound">n</a>

<a id="1378" class="Comment">-- An environment contains one value for every variable.</a>

<a id="Env"></a><a id="1436" href="Algebra.Solver.Monoid.html#1436" class="Function">Env</a> <a id="1440" class="Symbol">:</a> <a id="1442" href="Agda.Builtin.Nat.html#186" class="Datatype">ℕ</a> <a id="1444" class="Symbol">→</a> <a id="1446" href="Agda.Primitive.html#320" class="Primitive">Set</a> <a id="1450" class="Symbol">_</a>
<a id="1452" href="Algebra.Solver.Monoid.html#1436" class="Function">Env</a> <a id="1456" href="Algebra.Solver.Monoid.html#1456" class="Bound">n</a> <a id="1458" class="Symbol">=</a> <a id="1460" href="Data.Vec.Base.html#1086" class="Datatype">Vec</a> <a id="1464" href="Algebra.Bundles.html#6095" class="Field">Carrier</a> <a id="1472" href="Algebra.Solver.Monoid.html#1456" class="Bound">n</a>

<a id="1475" class="Comment">-- The semantics of an expression is a function from an environment to</a>
<a id="1546" class="Comment">-- a value.</a>

<a id="⟦_⟧"></a><a id="1559" href="Algebra.Solver.Monoid.html#1559" class="Function Operator">⟦_⟧</a> <a id="1563" class="Symbol">:</a> <a id="1565" class="Symbol">∀</a> <a id="1567" class="Symbol">{</a><a id="1568" href="Algebra.Solver.Monoid.html#1568" class="Bound">n</a><a id="1569" class="Symbol">}</a> <a id="1571" class="Symbol">→</a> <a id="1573" href="Algebra.Solver.Monoid.html#1281" class="Datatype">Expr</a> <a id="1578" href="Algebra.Solver.Monoid.html#1568" class="Bound">n</a> <a id="1580" class="Symbol">→</a> <a id="1582" href="Algebra.Solver.Monoid.html#1436" class="Function">Env</a> <a id="1586" href="Algebra.Solver.Monoid.html#1568" class="Bound">n</a> <a id="1588" class="Symbol">→</a> <a id="1590" href="Algebra.Bundles.html#6095" class="Field">Carrier</a>
<a id="1598" href="Algebra.Solver.Monoid.html#1559" class="Function Operator">⟦</a> <a id="1600" href="Algebra.Solver.Monoid.html#1308" class="InductiveConstructor">var</a> <a id="1604" href="Algebra.Solver.Monoid.html#1604" class="Bound">x</a>   <a id="1608" href="Algebra.Solver.Monoid.html#1559" class="Function Operator">⟧</a> <a id="1610" href="Algebra.Solver.Monoid.html#1610" class="Bound">ρ</a> <a id="1612" class="Symbol">=</a> <a id="1614" href="Data.Vec.Base.html#1643" class="Function">lookup</a> <a id="1621" href="Algebra.Solver.Monoid.html#1610" class="Bound">ρ</a> <a id="1623" href="Algebra.Solver.Monoid.html#1604" class="Bound">x</a>
<a id="1625" href="Algebra.Solver.Monoid.html#1559" class="Function Operator">⟦</a> <a id="1627" href="Algebra.Solver.Monoid.html#1331" class="InductiveConstructor">id</a>      <a id="1635" href="Algebra.Solver.Monoid.html#1559" class="Function Operator">⟧</a> <a id="1637" href="Algebra.Solver.Monoid.html#1637" class="Bound">ρ</a> <a id="1639" class="Symbol">=</a> <a id="1641" href="Algebra.Bundles.html#6172" class="Field">ε</a>
<a id="1643" href="Algebra.Solver.Monoid.html#1559" class="Function Operator">⟦</a> <a id="1645" href="Algebra.Solver.Monoid.html#1645" class="Bound">e₁</a> <a id="1648" href="Algebra.Solver.Monoid.html#1346" class="InductiveConstructor Operator">⊕</a> <a id="1650" href="Algebra.Solver.Monoid.html#1650" class="Bound">e₂</a> <a id="1653" href="Algebra.Solver.Monoid.html#1559" class="Function Operator">⟧</a> <a id="1655" href="Algebra.Solver.Monoid.html#1655" class="Bound">ρ</a> <a id="1657" class="Symbol">=</a> <a id="1659" href="Algebra.Solver.Monoid.html#1559" class="Function Operator">⟦</a> <a id="1661" href="Algebra.Solver.Monoid.html#1645" class="Bound">e₁</a> <a id="1664" href="Algebra.Solver.Monoid.html#1559" class="Function Operator">⟧</a> <a id="1666" href="Algebra.Solver.Monoid.html#1655" class="Bound">ρ</a> <a id="1668" href="Algebra.Bundles.html#6145" class="Field Operator">∙</a> <a id="1670" href="Algebra.Solver.Monoid.html#1559" class="Function Operator">⟦</a> <a id="1672" href="Algebra.Solver.Monoid.html#1650" class="Bound">e₂</a> <a id="1675" href="Algebra.Solver.Monoid.html#1559" class="Function Operator">⟧</a> <a id="1677" href="Algebra.Solver.Monoid.html#1655" class="Bound">ρ</a>

<a id="1680" class="Comment">------------------------------------------------------------------------</a>
<a id="1753" class="Comment">-- Normal forms</a>

<a id="1770" class="Comment">-- A normal form is a list of variables.</a>

<a id="Normal"></a><a id="1812" href="Algebra.Solver.Monoid.html#1812" class="Function">Normal</a> <a id="1819" class="Symbol">:</a> <a id="1821" href="Agda.Builtin.Nat.html#186" class="Datatype">ℕ</a> <a id="1823" class="Symbol">→</a> <a id="1825" href="Agda.Primitive.html#320" class="Primitive">Set</a>
<a id="1829" href="Algebra.Solver.Monoid.html#1812" class="Function">Normal</a> <a id="1836" href="Algebra.Solver.Monoid.html#1836" class="Bound">n</a> <a id="1838" class="Symbol">=</a> <a id="1840" href="Agda.Builtin.List.html#130" class="Datatype">List</a> <a id="1845" class="Symbol">(</a><a id="1846" href="Data.Fin.Base.html#1312" class="Datatype">Fin</a> <a id="1850" href="Algebra.Solver.Monoid.html#1836" class="Bound">n</a><a id="1851" class="Symbol">)</a>

<a id="1854" class="Comment">-- The semantics of a normal form.</a>

<a id="⟦_⟧⇓"></a><a id="1890" href="Algebra.Solver.Monoid.html#1890" class="Function Operator">⟦_⟧⇓</a> <a id="1895" class="Symbol">:</a> <a id="1897" class="Symbol">∀</a> <a id="1899" class="Symbol">{</a><a id="1900" href="Algebra.Solver.Monoid.html#1900" class="Bound">n</a><a id="1901" class="Symbol">}</a> <a id="1903" class="Symbol">→</a> <a id="1905" href="Algebra.Solver.Monoid.html#1812" class="Function">Normal</a> <a id="1912" href="Algebra.Solver.Monoid.html#1900" class="Bound">n</a> <a id="1914" class="Symbol">→</a> <a id="1916" href="Algebra.Solver.Monoid.html#1436" class="Function">Env</a> <a id="1920" href="Algebra.Solver.Monoid.html#1900" class="Bound">n</a> <a id="1922" class="Symbol">→</a> <a id="1924" href="Algebra.Bundles.html#6095" class="Field">Carrier</a>
<a id="1932" href="Algebra.Solver.Monoid.html#1890" class="Function Operator">⟦</a> <a id="1934" href="Agda.Builtin.List.html#167" class="InductiveConstructor">[]</a>     <a id="1941" href="Algebra.Solver.Monoid.html#1890" class="Function Operator">⟧⇓</a> <a id="1944" href="Algebra.Solver.Monoid.html#1944" class="Bound">ρ</a> <a id="1946" class="Symbol">=</a> <a id="1948" href="Algebra.Bundles.html#6172" class="Field">ε</a>
<a id="1950" href="Algebra.Solver.Monoid.html#1890" class="Function Operator">⟦</a> <a id="1952" href="Algebra.Solver.Monoid.html#1952" class="Bound">x</a> <a id="1954" href="Agda.Builtin.List.html#182" class="InductiveConstructor Operator">∷</a> <a id="1956" href="Algebra.Solver.Monoid.html#1956" class="Bound">nf</a> <a id="1959" href="Algebra.Solver.Monoid.html#1890" class="Function Operator">⟧⇓</a> <a id="1962" href="Algebra.Solver.Monoid.html#1962" class="Bound">ρ</a> <a id="1964" class="Symbol">=</a> <a id="1966" href="Data.Vec.Base.html#1643" class="Function">lookup</a> <a id="1973" href="Algebra.Solver.Monoid.html#1962" class="Bound">ρ</a> <a id="1975" href="Algebra.Solver.Monoid.html#1952" class="Bound">x</a> <a id="1977" href="Algebra.Bundles.html#6145" class="Field Operator">∙</a> <a id="1979" href="Algebra.Solver.Monoid.html#1890" class="Function Operator">⟦</a> <a id="1981" href="Algebra.Solver.Monoid.html#1956" class="Bound">nf</a> <a id="1984" href="Algebra.Solver.Monoid.html#1890" class="Function Operator">⟧⇓</a> <a id="1987" href="Algebra.Solver.Monoid.html#1962" class="Bound">ρ</a>

<a id="1990" class="Comment">-- A normaliser.</a>

<a id="normalise"></a><a id="2008" href="Algebra.Solver.Monoid.html#2008" class="Function">normalise</a> <a id="2018" class="Symbol">:</a> <a id="2020" class="Symbol">∀</a> <a id="2022" class="Symbol">{</a><a id="2023" href="Algebra.Solver.Monoid.html#2023" class="Bound">n</a><a id="2024" class="Symbol">}</a> <a id="2026" class="Symbol">→</a> <a id="2028" href="Algebra.Solver.Monoid.html#1281" class="Datatype">Expr</a> <a id="2033" href="Algebra.Solver.Monoid.html#2023" class="Bound">n</a> <a id="2035" class="Symbol">→</a> <a id="2037" href="Algebra.Solver.Monoid.html#1812" class="Function">Normal</a> <a id="2044" href="Algebra.Solver.Monoid.html#2023" class="Bound">n</a>
<a id="2046" href="Algebra.Solver.Monoid.html#2008" class="Function">normalise</a> <a id="2056" class="Symbol">(</a><a id="2057" href="Algebra.Solver.Monoid.html#1308" class="InductiveConstructor">var</a> <a id="2061" href="Algebra.Solver.Monoid.html#2061" class="Bound">x</a><a id="2062" class="Symbol">)</a>   <a id="2066" class="Symbol">=</a> <a id="2068" href="Algebra.Solver.Monoid.html#2061" class="Bound">x</a> <a id="2070" href="Agda.Builtin.List.html#182" class="InductiveConstructor Operator">∷</a> <a id="2072" href="Agda.Builtin.List.html#167" class="InductiveConstructor">[]</a>
<a id="2075" href="Algebra.Solver.Monoid.html#2008" class="Function">normalise</a> <a id="2085" href="Algebra.Solver.Monoid.html#1331" class="InductiveConstructor">id</a>        <a id="2095" class="Symbol">=</a> <a id="2097" href="Agda.Builtin.List.html#167" class="InductiveConstructor">[]</a>
<a id="2100" href="Algebra.Solver.Monoid.html#2008" class="Function">normalise</a> <a id="2110" class="Symbol">(</a><a id="2111" href="Algebra.Solver.Monoid.html#2111" class="Bound">e₁</a> <a id="2114" href="Algebra.Solver.Monoid.html#1346" class="InductiveConstructor Operator">⊕</a> <a id="2116" href="Algebra.Solver.Monoid.html#2116" class="Bound">e₂</a><a id="2118" class="Symbol">)</a> <a id="2120" class="Symbol">=</a> <a id="2122" href="Algebra.Solver.Monoid.html#2008" class="Function">normalise</a> <a id="2132" href="Algebra.Solver.Monoid.html#2111" class="Bound">e₁</a> <a id="2135" href="Data.List.Base.html#1943" class="Function Operator">++</a> <a id="2138" href="Algebra.Solver.Monoid.html#2008" class="Function">normalise</a> <a id="2148" href="Algebra.Solver.Monoid.html#2116" class="Bound">e₂</a>

<a id="2152" class="Comment">-- The normaliser is homomorphic with respect to _++_/_∙_.</a>

<a id="homomorphic"></a><a id="2212" href="Algebra.Solver.Monoid.html#2212" class="Function">homomorphic</a> <a id="2224" class="Symbol">:</a> <a id="2226" class="Symbol">∀</a> <a id="2228" class="Symbol">{</a><a id="2229" href="Algebra.Solver.Monoid.html#2229" class="Bound">n</a><a id="2230" class="Symbol">}</a> <a id="2232" class="Symbol">(</a><a id="2233" href="Algebra.Solver.Monoid.html#2233" class="Bound">nf₁</a> <a id="2237" href="Algebra.Solver.Monoid.html#2237" class="Bound">nf₂</a> <a id="2241" class="Symbol">:</a> <a id="2243" href="Algebra.Solver.Monoid.html#1812" class="Function">Normal</a> <a id="2250" href="Algebra.Solver.Monoid.html#2229" class="Bound">n</a><a id="2251" class="Symbol">)</a> <a id="2253" class="Symbol">(</a><a id="2254" href="Algebra.Solver.Monoid.html#2254" class="Bound">ρ</a> <a id="2256" class="Symbol">:</a> <a id="2258" href="Algebra.Solver.Monoid.html#1436" class="Function">Env</a> <a id="2262" href="Algebra.Solver.Monoid.html#2229" class="Bound">n</a><a id="2263" class="Symbol">)</a> <a id="2265" class="Symbol">→</a>
              <a id="2281" href="Algebra.Solver.Monoid.html#1890" class="Function Operator">⟦</a> <a id="2283" href="Algebra.Solver.Monoid.html#2233" class="Bound">nf₁</a> <a id="2287" href="Data.List.Base.html#1943" class="Function Operator">++</a> <a id="2290" href="Algebra.Solver.Monoid.html#2237" class="Bound">nf₂</a> <a id="2294" href="Algebra.Solver.Monoid.html#1890" class="Function Operator">⟧⇓</a> <a id="2297" href="Algebra.Solver.Monoid.html#2254" class="Bound">ρ</a> <a id="2299" href="Algebra.Bundles.html#6116" class="Field Operator">≈</a> <a id="2301" class="Symbol">(</a><a id="2302" href="Algebra.Solver.Monoid.html#1890" class="Function Operator">⟦</a> <a id="2304" href="Algebra.Solver.Monoid.html#2233" class="Bound">nf₁</a> <a id="2308" href="Algebra.Solver.Monoid.html#1890" class="Function Operator">⟧⇓</a> <a id="2311" href="Algebra.Solver.Monoid.html#2254" class="Bound">ρ</a> <a id="2313" href="Algebra.Bundles.html#6145" class="Field Operator">∙</a> <a id="2315" href="Algebra.Solver.Monoid.html#1890" class="Function Operator">⟦</a> <a id="2317" href="Algebra.Solver.Monoid.html#2237" class="Bound">nf₂</a> <a id="2321" href="Algebra.Solver.Monoid.html#1890" class="Function Operator">⟧⇓</a> <a id="2324" href="Algebra.Solver.Monoid.html#2254" class="Bound">ρ</a><a id="2325" class="Symbol">)</a>
<a id="2327" href="Algebra.Solver.Monoid.html#2212" class="Function">homomorphic</a> <a id="2339" href="Agda.Builtin.List.html#167" class="InductiveConstructor">[]</a> <a id="2342" href="Algebra.Solver.Monoid.html#2342" class="Bound">nf₂</a> <a id="2346" href="Algebra.Solver.Monoid.html#2346" class="Bound">ρ</a> <a id="2348" class="Symbol">=</a> <a id="2350" href="Relation.Binary.Reasoning.Base.Single.html#1925" class="Function Operator">begin</a>
  <a id="2358" href="Algebra.Solver.Monoid.html#1890" class="Function Operator">⟦</a> <a id="2360" href="Algebra.Solver.Monoid.html#2342" class="Bound">nf₂</a> <a id="2364" href="Algebra.Solver.Monoid.html#1890" class="Function Operator">⟧⇓</a> <a id="2367" href="Algebra.Solver.Monoid.html#2346" class="Bound">ρ</a>      <a id="2374" href="Relation.Binary.Reasoning.Setoid.html#1061" class="Function">≈⟨</a> <a id="2377" href="Relation.Binary.Structures.html#1622" class="Function">sym</a> <a id="2381" href="Function.Base.html#2003" class="Function Operator">$</a> <a id="2383" href="Algebra.Structures.html#4042" class="Function">identityˡ</a> <a id="2393" class="Symbol">_</a> <a id="2395" href="Relation.Binary.Reasoning.Setoid.html#1061" class="Function">⟩</a>
  <a id="2399" href="Algebra.Bundles.html#6172" class="Field">ε</a> <a id="2401" href="Algebra.Bundles.html#6145" class="Field Operator">∙</a> <a id="2403" href="Algebra.Solver.Monoid.html#1890" class="Function Operator">⟦</a> <a id="2405" href="Algebra.Solver.Monoid.html#2342" class="Bound">nf₂</a> <a id="2409" href="Algebra.Solver.Monoid.html#1890" class="Function Operator">⟧⇓</a> <a id="2412" href="Algebra.Solver.Monoid.html#2346" class="Bound">ρ</a>  <a id="2415" href="Relation.Binary.Reasoning.Base.Single.html#2564" class="Function Operator">∎</a>
<a id="2417" href="Algebra.Solver.Monoid.html#2212" class="Function">homomorphic</a> <a id="2429" class="Symbol">(</a><a id="2430" href="Algebra.Solver.Monoid.html#2430" class="Bound">x</a> <a id="2432" href="Agda.Builtin.List.html#182" class="InductiveConstructor Operator">∷</a> <a id="2434" href="Algebra.Solver.Monoid.html#2434" class="Bound">nf₁</a><a id="2437" class="Symbol">)</a> <a id="2439" href="Algebra.Solver.Monoid.html#2439" class="Bound">nf₂</a> <a id="2443" href="Algebra.Solver.Monoid.html#2443" class="Bound">ρ</a> <a id="2445" class="Symbol">=</a> <a id="2447" href="Relation.Binary.Reasoning.Base.Single.html#1925" class="Function Operator">begin</a>
  <a id="2455" href="Data.Vec.Base.html#1643" class="Function">lookup</a> <a id="2462" href="Algebra.Solver.Monoid.html#2443" class="Bound">ρ</a> <a id="2464" href="Algebra.Solver.Monoid.html#2430" class="Bound">x</a> <a id="2466" href="Algebra.Bundles.html#6145" class="Field Operator">∙</a> <a id="2468" href="Algebra.Solver.Monoid.html#1890" class="Function Operator">⟦</a> <a id="2470" href="Algebra.Solver.Monoid.html#2434" class="Bound">nf₁</a> <a id="2474" href="Data.List.Base.html#1943" class="Function Operator">++</a> <a id="2477" href="Algebra.Solver.Monoid.html#2439" class="Bound">nf₂</a> <a id="2481" href="Algebra.Solver.Monoid.html#1890" class="Function Operator">⟧⇓</a> <a id="2484" href="Algebra.Solver.Monoid.html#2443" class="Bound">ρ</a>          <a id="2495" href="Relation.Binary.Reasoning.Setoid.html#1061" class="Function">≈⟨</a> <a id="2498" href="Algebra.Structures.html#1371" class="Function">∙-congˡ</a> <a id="2506" class="Symbol">(</a><a id="2507" href="Algebra.Solver.Monoid.html#2212" class="Function">homomorphic</a> <a id="2519" href="Algebra.Solver.Monoid.html#2434" class="Bound">nf₁</a> <a id="2523" href="Algebra.Solver.Monoid.html#2439" class="Bound">nf₂</a> <a id="2527" href="Algebra.Solver.Monoid.html#2443" class="Bound">ρ</a><a id="2528" class="Symbol">)</a> <a id="2530" href="Relation.Binary.Reasoning.Setoid.html#1061" class="Function">⟩</a>
  <a id="2534" href="Data.Vec.Base.html#1643" class="Function">lookup</a> <a id="2541" href="Algebra.Solver.Monoid.html#2443" class="Bound">ρ</a> <a id="2543" href="Algebra.Solver.Monoid.html#2430" class="Bound">x</a> <a id="2545" href="Algebra.Bundles.html#6145" class="Field Operator">∙</a> <a id="2547" class="Symbol">(</a><a id="2548" href="Algebra.Solver.Monoid.html#1890" class="Function Operator">⟦</a> <a id="2550" href="Algebra.Solver.Monoid.html#2434" class="Bound">nf₁</a> <a id="2554" href="Algebra.Solver.Monoid.html#1890" class="Function Operator">⟧⇓</a> <a id="2557" href="Algebra.Solver.Monoid.html#2443" class="Bound">ρ</a> <a id="2559" href="Algebra.Bundles.html#6145" class="Field Operator">∙</a> <a id="2561" href="Algebra.Solver.Monoid.html#1890" class="Function Operator">⟦</a> <a id="2563" href="Algebra.Solver.Monoid.html#2439" class="Bound">nf₂</a> <a id="2567" href="Algebra.Solver.Monoid.html#1890" class="Function Operator">⟧⇓</a> <a id="2570" href="Algebra.Solver.Monoid.html#2443" class="Bound">ρ</a><a id="2571" class="Symbol">)</a>  <a id="2574" href="Relation.Binary.Reasoning.Setoid.html#1061" class="Function">≈⟨</a> <a id="2577" href="Relation.Binary.Structures.html#1622" class="Function">sym</a> <a id="2581" href="Function.Base.html#2003" class="Function Operator">$</a> <a id="2583" href="Algebra.Structures.html#2883" class="Function">assoc</a> <a id="2589" class="Symbol">_</a> <a id="2591" class="Symbol">_</a> <a id="2593" class="Symbol">_</a> <a id="2595" href="Relation.Binary.Reasoning.Setoid.html#1061" class="Function">⟩</a>
  <a id="2599" href="Data.Vec.Base.html#1643" class="Function">lookup</a> <a id="2606" href="Algebra.Solver.Monoid.html#2443" class="Bound">ρ</a> <a id="2608" href="Algebra.Solver.Monoid.html#2430" class="Bound">x</a> <a id="2610" href="Algebra.Bundles.html#6145" class="Field Operator">∙</a> <a id="2612" href="Algebra.Solver.Monoid.html#1890" class="Function Operator">⟦</a> <a id="2614" href="Algebra.Solver.Monoid.html#2434" class="Bound">nf₁</a> <a id="2618" href="Algebra.Solver.Monoid.html#1890" class="Function Operator">⟧⇓</a> <a id="2621" href="Algebra.Solver.Monoid.html#2443" class="Bound">ρ</a> <a id="2623" href="Algebra.Bundles.html#6145" class="Field Operator">∙</a> <a id="2625" href="Algebra.Solver.Monoid.html#1890" class="Function Operator">⟦</a> <a id="2627" href="Algebra.Solver.Monoid.html#2439" class="Bound">nf₂</a> <a id="2631" href="Algebra.Solver.Monoid.html#1890" class="Function Operator">⟧⇓</a> <a id="2634" href="Algebra.Solver.Monoid.html#2443" class="Bound">ρ</a>    <a id="2639" href="Relation.Binary.Reasoning.Base.Single.html#2564" class="Function Operator">∎</a>

<a id="2642" class="Comment">-- The normaliser preserves the semantics of the expression.</a>

<a id="normalise-correct"></a><a id="2704" href="Algebra.Solver.Monoid.html#2704" class="Function">normalise-correct</a> <a id="2722" class="Symbol">:</a>
  <a id="2726" class="Symbol">∀</a> <a id="2728" class="Symbol">{</a><a id="2729" href="Algebra.Solver.Monoid.html#2729" class="Bound">n</a><a id="2730" class="Symbol">}</a> <a id="2732" class="Symbol">(</a><a id="2733" href="Algebra.Solver.Monoid.html#2733" class="Bound">e</a> <a id="2735" class="Symbol">:</a> <a id="2737" href="Algebra.Solver.Monoid.html#1281" class="Datatype">Expr</a> <a id="2742" href="Algebra.Solver.Monoid.html#2729" class="Bound">n</a><a id="2743" class="Symbol">)</a> <a id="2745" class="Symbol">(</a><a id="2746" href="Algebra.Solver.Monoid.html#2746" class="Bound">ρ</a> <a id="2748" class="Symbol">:</a> <a id="2750" href="Algebra.Solver.Monoid.html#1436" class="Function">Env</a> <a id="2754" href="Algebra.Solver.Monoid.html#2729" class="Bound">n</a><a id="2755" class="Symbol">)</a> <a id="2757" class="Symbol">→</a> <a id="2759" href="Algebra.Solver.Monoid.html#1890" class="Function Operator">⟦</a> <a id="2761" href="Algebra.Solver.Monoid.html#2008" class="Function">normalise</a> <a id="2771" href="Algebra.Solver.Monoid.html#2733" class="Bound">e</a> <a id="2773" href="Algebra.Solver.Monoid.html#1890" class="Function Operator">⟧⇓</a> <a id="2776" href="Algebra.Solver.Monoid.html#2746" class="Bound">ρ</a> <a id="2778" href="Algebra.Bundles.html#6116" class="Field Operator">≈</a> <a id="2780" href="Algebra.Solver.Monoid.html#1559" class="Function Operator">⟦</a> <a id="2782" href="Algebra.Solver.Monoid.html#2733" class="Bound">e</a> <a id="2784" href="Algebra.Solver.Monoid.html#1559" class="Function Operator">⟧</a> <a id="2786" href="Algebra.Solver.Monoid.html#2746" class="Bound">ρ</a>
<a id="2788" href="Algebra.Solver.Monoid.html#2704" class="Function">normalise-correct</a> <a id="2806" class="Symbol">(</a><a id="2807" href="Algebra.Solver.Monoid.html#1308" class="InductiveConstructor">var</a> <a id="2811" href="Algebra.Solver.Monoid.html#2811" class="Bound">x</a><a id="2812" class="Symbol">)</a> <a id="2814" href="Algebra.Solver.Monoid.html#2814" class="Bound">ρ</a> <a id="2816" class="Symbol">=</a> <a id="2818" href="Relation.Binary.Reasoning.Base.Single.html#1925" class="Function Operator">begin</a>
  <a id="2826" href="Data.Vec.Base.html#1643" class="Function">lookup</a> <a id="2833" href="Algebra.Solver.Monoid.html#2814" class="Bound">ρ</a> <a id="2835" href="Algebra.Solver.Monoid.html#2811" class="Bound">x</a> <a id="2837" href="Algebra.Bundles.html#6145" class="Field Operator">∙</a> <a id="2839" href="Algebra.Bundles.html#6172" class="Field">ε</a>  <a id="2842" href="Relation.Binary.Reasoning.Setoid.html#1061" class="Function">≈⟨</a> <a id="2845" href="Algebra.Structures.html#4103" class="Function">identityʳ</a> <a id="2855" class="Symbol">_</a> <a id="2857" href="Relation.Binary.Reasoning.Setoid.html#1061" class="Function">⟩</a>
  <a id="2861" href="Data.Vec.Base.html#1643" class="Function">lookup</a> <a id="2868" href="Algebra.Solver.Monoid.html#2814" class="Bound">ρ</a> <a id="2870" href="Algebra.Solver.Monoid.html#2811" class="Bound">x</a>      <a id="2877" href="Relation.Binary.Reasoning.Base.Single.html#2564" class="Function Operator">∎</a>
<a id="2879" href="Algebra.Solver.Monoid.html#2704" class="Function">normalise-correct</a> <a id="2897" href="Algebra.Solver.Monoid.html#1331" class="InductiveConstructor">id</a> <a id="2900" href="Algebra.Solver.Monoid.html#2900" class="Bound">ρ</a> <a id="2902" class="Symbol">=</a> <a id="2904" href="Relation.Binary.Reasoning.Base.Single.html#1925" class="Function Operator">begin</a>
  <a id="2912" href="Algebra.Bundles.html#6172" class="Field">ε</a>  <a id="2915" href="Relation.Binary.Reasoning.Base.Single.html#2564" class="Function Operator">∎</a>
<a id="2917" href="Algebra.Solver.Monoid.html#2704" class="Function">normalise-correct</a> <a id="2935" class="Symbol">(</a><a id="2936" href="Algebra.Solver.Monoid.html#2936" class="Bound">e₁</a> <a id="2939" href="Algebra.Solver.Monoid.html#1346" class="InductiveConstructor Operator">⊕</a> <a id="2941" href="Algebra.Solver.Monoid.html#2941" class="Bound">e₂</a><a id="2943" class="Symbol">)</a> <a id="2945" href="Algebra.Solver.Monoid.html#2945" class="Bound">ρ</a> <a id="2947" class="Symbol">=</a> <a id="2949" href="Relation.Binary.Reasoning.Base.Single.html#1925" class="Function Operator">begin</a>
  <a id="2957" href="Algebra.Solver.Monoid.html#1890" class="Function Operator">⟦</a> <a id="2959" href="Algebra.Solver.Monoid.html#2008" class="Function">normalise</a> <a id="2969" href="Algebra.Solver.Monoid.html#2936" class="Bound">e₁</a> <a id="2972" href="Data.List.Base.html#1943" class="Function Operator">++</a> <a id="2975" href="Algebra.Solver.Monoid.html#2008" class="Function">normalise</a> <a id="2985" href="Algebra.Solver.Monoid.html#2941" class="Bound">e₂</a> <a id="2988" href="Algebra.Solver.Monoid.html#1890" class="Function Operator">⟧⇓</a> <a id="2991" href="Algebra.Solver.Monoid.html#2945" class="Bound">ρ</a>        <a id="3000" href="Relation.Binary.Reasoning.Setoid.html#1061" class="Function">≈⟨</a> <a id="3003" href="Algebra.Solver.Monoid.html#2212" class="Function">homomorphic</a> <a id="3015" class="Symbol">(</a><a id="3016" href="Algebra.Solver.Monoid.html#2008" class="Function">normalise</a> <a id="3026" href="Algebra.Solver.Monoid.html#2936" class="Bound">e₁</a><a id="3028" class="Symbol">)</a> <a id="3030" class="Symbol">(</a><a id="3031" href="Algebra.Solver.Monoid.html#2008" class="Function">normalise</a> <a id="3041" href="Algebra.Solver.Monoid.html#2941" class="Bound">e₂</a><a id="3043" class="Symbol">)</a> <a id="3045" href="Algebra.Solver.Monoid.html#2945" class="Bound">ρ</a> <a id="3047" href="Relation.Binary.Reasoning.Setoid.html#1061" class="Function">⟩</a>
  <a id="3051" href="Algebra.Solver.Monoid.html#1890" class="Function Operator">⟦</a> <a id="3053" href="Algebra.Solver.Monoid.html#2008" class="Function">normalise</a> <a id="3063" href="Algebra.Solver.Monoid.html#2936" class="Bound">e₁</a> <a id="3066" href="Algebra.Solver.Monoid.html#1890" class="Function Operator">⟧⇓</a> <a id="3069" href="Algebra.Solver.Monoid.html#2945" class="Bound">ρ</a> <a id="3071" href="Algebra.Bundles.html#6145" class="Field Operator">∙</a> <a id="3073" href="Algebra.Solver.Monoid.html#1890" class="Function Operator">⟦</a> <a id="3075" href="Algebra.Solver.Monoid.html#2008" class="Function">normalise</a> <a id="3085" href="Algebra.Solver.Monoid.html#2941" class="Bound">e₂</a> <a id="3088" href="Algebra.Solver.Monoid.html#1890" class="Function Operator">⟧⇓</a> <a id="3091" href="Algebra.Solver.Monoid.html#2945" class="Bound">ρ</a>  <a id="3094" href="Relation.Binary.Reasoning.Setoid.html#1061" class="Function">≈⟨</a> <a id="3097" href="Algebra.Structures.html#1221" class="Function">∙-cong</a> <a id="3104" class="Symbol">(</a><a id="3105" href="Algebra.Solver.Monoid.html#2704" class="Function">normalise-correct</a> <a id="3123" href="Algebra.Solver.Monoid.html#2936" class="Bound">e₁</a> <a id="3126" href="Algebra.Solver.Monoid.html#2945" class="Bound">ρ</a><a id="3127" class="Symbol">)</a> <a id="3129" class="Symbol">(</a><a id="3130" href="Algebra.Solver.Monoid.html#2704" class="Function">normalise-correct</a> <a id="3148" href="Algebra.Solver.Monoid.html#2941" class="Bound">e₂</a> <a id="3151" href="Algebra.Solver.Monoid.html#2945" class="Bound">ρ</a><a id="3152" class="Symbol">)</a> <a id="3154" href="Relation.Binary.Reasoning.Setoid.html#1061" class="Function">⟩</a>
  <a id="3158" href="Algebra.Solver.Monoid.html#1559" class="Function Operator">⟦</a> <a id="3160" href="Algebra.Solver.Monoid.html#2936" class="Bound">e₁</a> <a id="3163" href="Algebra.Solver.Monoid.html#1559" class="Function Operator">⟧</a> <a id="3165" href="Algebra.Solver.Monoid.html#2945" class="Bound">ρ</a> <a id="3167" href="Algebra.Bundles.html#6145" class="Field Operator">∙</a> <a id="3169" href="Algebra.Solver.Monoid.html#1559" class="Function Operator">⟦</a> <a id="3171" href="Algebra.Solver.Monoid.html#2941" class="Bound">e₂</a> <a id="3174" href="Algebra.Solver.Monoid.html#1559" class="Function Operator">⟧</a> <a id="3176" href="Algebra.Solver.Monoid.html#2945" class="Bound">ρ</a>                        <a id="3201" href="Relation.Binary.Reasoning.Base.Single.html#2564" class="Function Operator">∎</a>

<a id="3204" class="Comment">------------------------------------------------------------------------</a>
<a id="3277" class="Comment">-- &quot;Tactic.</a>

<a id="3290" class="Keyword">open</a> <a id="3295" class="Keyword">module</a> <a id="R"></a><a id="3302" href="Algebra.Solver.Monoid.html#3302" class="Module">R</a> <a id="3304" class="Symbol">=</a> <a id="3306" href="Relation.Binary.Reflection.html" class="Module">Relation.Binary.Reflection</a>
                  <a id="3351" href="Algebra.Structures.html#1296" class="Function">setoid</a> <a id="3358" href="Algebra.Solver.Monoid.html#1308" class="InductiveConstructor">var</a> <a id="3362" href="Algebra.Solver.Monoid.html#1559" class="Function Operator">⟦_⟧</a> <a id="3366" class="Symbol">(</a><a id="3367" href="Algebra.Solver.Monoid.html#1890" class="Function Operator">⟦_⟧⇓</a> <a id="3372" href="Function.Base.html#1115" class="Function Operator">∘</a> <a id="3374" href="Algebra.Solver.Monoid.html#2008" class="Function">normalise</a><a id="3383" class="Symbol">)</a> <a id="3385" href="Algebra.Solver.Monoid.html#2704" class="Function">normalise-correct</a>
  <a id="3405" class="Keyword">public</a> <a id="3412" class="Keyword">using</a> <a id="3418" class="Symbol">(</a><a id="3419" href="Relation.Binary.Reflection.html#2519" class="Function">solve</a><a id="3424" class="Symbol">;</a> <a id="3426" href="Relation.Binary.Reflection.html#3624" class="Function Operator">_⊜_</a><a id="3429" class="Symbol">)</a>

<a id="3432" class="Comment">-- We can decide if two normal forms are /syntactically/ equal.</a>

<a id="3497" class="Keyword">infix</a> <a id="3503" class="Number">5</a> <a id="3505" href="Algebra.Solver.Monoid.html#3510" class="Function Operator">_≟_</a>

<a id="_≟_"></a><a id="3510" href="Algebra.Solver.Monoid.html#3510" class="Function Operator">_≟_</a> <a id="3514" class="Symbol">:</a> <a id="3516" class="Symbol">∀</a> <a id="3518" class="Symbol">{</a><a id="3519" href="Algebra.Solver.Monoid.html#3519" class="Bound">n</a><a id="3520" class="Symbol">}</a> <a id="3522" class="Symbol">→</a> <a id="3524" href="Relation.Binary.Definitions.html#5914" class="Function">Decidable</a> <a id="3534" class="Symbol">{</a><a id="3535" class="Argument">A</a> <a id="3537" class="Symbol">=</a> <a id="3539" href="Algebra.Solver.Monoid.html#1812" class="Function">Normal</a> <a id="3546" href="Algebra.Solver.Monoid.html#3519" class="Bound">n</a><a id="3547" class="Symbol">}</a> <a id="3549" href="Agda.Builtin.Equality.html#133" class="Datatype Operator">_≡_</a>
<a id="3553" href="Algebra.Solver.Monoid.html#3553" class="Bound">nf₁</a> <a id="3557" href="Algebra.Solver.Monoid.html#3510" class="Function Operator">≟</a> <a id="3559" href="Algebra.Solver.Monoid.html#3559" class="Bound">nf₂</a> <a id="3563" class="Symbol">=</a> <a id="3565" href="Relation.Nullary.Decidable.Core.html#4793" class="Function">Dec.map′</a> <a id="3574" href="Data.List.Relation.Binary.Equality.Propositional.html#1023" class="Function">≋⇒≡</a> <a id="3578" href="Data.List.Relation.Binary.Equality.Propositional.html#1119" class="Function">≡⇒≋</a> <a id="3582" class="Symbol">(</a><a id="3583" href="Algebra.Solver.Monoid.html#3553" class="Bound">nf₁</a> <a id="3587" href="Data.List.Relation.Binary.Equality.DecSetoid.html#887" class="Function Operator">≋?</a> <a id="3590" href="Algebra.Solver.Monoid.html#3559" class="Bound">nf₂</a><a id="3593" class="Symbol">)</a>
  <a id="3597" class="Keyword">where</a> <a id="3603" class="Keyword">open</a> <a id="3608" href="Data.List.Relation.Binary.Equality.DecPropositional.html" class="Module">ListEq</a> <a id="3615" href="Data.Fin.Properties.html#3234" class="Function Operator">Fin._≟_</a>

<a id="3624" class="Comment">-- We can also give a sound, but not necessarily complete, procedure</a>
<a id="3693" class="Comment">-- for determining if two expressions have the same semantics.</a>

<a id="prove′"></a><a id="3757" href="Algebra.Solver.Monoid.html#3757" class="Function">prove′</a> <a id="3764" class="Symbol">:</a> <a id="3766" class="Symbol">∀</a> <a id="3768" class="Symbol">{</a><a id="3769" href="Algebra.Solver.Monoid.html#3769" class="Bound">n</a><a id="3770" class="Symbol">}</a> <a id="3772" class="Symbol">(</a><a id="3773" href="Algebra.Solver.Monoid.html#3773" class="Bound">e₁</a> <a id="3776" href="Algebra.Solver.Monoid.html#3776" class="Bound">e₂</a> <a id="3779" class="Symbol">:</a> <a id="3781" href="Algebra.Solver.Monoid.html#1281" class="Datatype">Expr</a> <a id="3786" href="Algebra.Solver.Monoid.html#3769" class="Bound">n</a><a id="3787" class="Symbol">)</a> <a id="3789" class="Symbol">→</a> <a id="3791" href="Agda.Builtin.Maybe.html#118" class="Datatype">Maybe</a> <a id="3797" class="Symbol">(∀</a> <a id="3800" href="Algebra.Solver.Monoid.html#3800" class="Bound">ρ</a> <a id="3802" class="Symbol">→</a> <a id="3804" href="Algebra.Solver.Monoid.html#1559" class="Function Operator">⟦</a> <a id="3806" href="Algebra.Solver.Monoid.html#3773" class="Bound">e₁</a> <a id="3809" href="Algebra.Solver.Monoid.html#1559" class="Function Operator">⟧</a> <a id="3811" href="Algebra.Solver.Monoid.html#3800" class="Bound">ρ</a> <a id="3813" href="Algebra.Bundles.html#6116" class="Field Operator">≈</a> <a id="3815" href="Algebra.Solver.Monoid.html#1559" class="Function Operator">⟦</a> <a id="3817" href="Algebra.Solver.Monoid.html#3776" class="Bound">e₂</a> <a id="3820" href="Algebra.Solver.Monoid.html#1559" class="Function Operator">⟧</a> <a id="3822" href="Algebra.Solver.Monoid.html#3800" class="Bound">ρ</a><a id="3823" class="Symbol">)</a>
<a id="3825" href="Algebra.Solver.Monoid.html#3757" class="Function">prove′</a> <a id="3832" href="Algebra.Solver.Monoid.html#3832" class="Bound">e₁</a> <a id="3835" href="Algebra.Solver.Monoid.html#3835" class="Bound">e₂</a> <a id="3838" class="Symbol">=</a>
  <a id="3842" href="Data.Maybe.Base.html#2049" class="Function">Maybe.map</a> <a id="3852" href="Algebra.Solver.Monoid.html#3911" class="Function">lemma</a> <a id="3858" href="Function.Base.html#2003" class="Function Operator">$</a> <a id="3860" href="Data.Maybe.Base.html#1241" class="Function">decToMaybe</a> <a id="3871" class="Symbol">(</a><a id="3872" href="Algebra.Solver.Monoid.html#2008" class="Function">normalise</a> <a id="3882" href="Algebra.Solver.Monoid.html#3832" class="Bound">e₁</a> <a id="3885" href="Algebra.Solver.Monoid.html#3510" class="Function Operator">≟</a> <a id="3887" href="Algebra.Solver.Monoid.html#2008" class="Function">normalise</a> <a id="3897" href="Algebra.Solver.Monoid.html#3835" class="Bound">e₂</a><a id="3899" class="Symbol">)</a>
  <a id="3903" class="Keyword">where</a>
  <a id="3911" href="Algebra.Solver.Monoid.html#3911" class="Function">lemma</a> <a id="3917" class="Symbol">:</a> <a id="3919" href="Algebra.Solver.Monoid.html#2008" class="Function">normalise</a> <a id="3929" href="Algebra.Solver.Monoid.html#3832" class="Bound">e₁</a> <a id="3932" href="Agda.Builtin.Equality.html#133" class="Datatype Operator">≡</a> <a id="3934" href="Algebra.Solver.Monoid.html#2008" class="Function">normalise</a> <a id="3944" href="Algebra.Solver.Monoid.html#3835" class="Bound">e₂</a> <a id="3947" class="Symbol">→</a> <a id="3949" class="Symbol">∀</a> <a id="3951" href="Algebra.Solver.Monoid.html#3951" class="Bound">ρ</a> <a id="3953" class="Symbol">→</a> <a id="3955" href="Algebra.Solver.Monoid.html#1559" class="Function Operator">⟦</a> <a id="3957" href="Algebra.Solver.Monoid.html#3832" class="Bound">e₁</a> <a id="3960" href="Algebra.Solver.Monoid.html#1559" class="Function Operator">⟧</a> <a id="3962" href="Algebra.Solver.Monoid.html#3951" class="Bound">ρ</a> <a id="3964" href="Algebra.Bundles.html#6116" class="Field Operator">≈</a> <a id="3966" href="Algebra.Solver.Monoid.html#1559" class="Function Operator">⟦</a> <a id="3968" href="Algebra.Solver.Monoid.html#3835" class="Bound">e₂</a> <a id="3971" href="Algebra.Solver.Monoid.html#1559" class="Function Operator">⟧</a> <a id="3973" href="Algebra.Solver.Monoid.html#3951" class="Bound">ρ</a>
  <a id="3977" href="Algebra.Solver.Monoid.html#3911" class="Function">lemma</a> <a id="3983" href="Algebra.Solver.Monoid.html#3983" class="Bound">eq</a> <a id="3986" href="Algebra.Solver.Monoid.html#3986" class="Bound">ρ</a> <a id="3988" class="Symbol">=</a>
    <a id="3994" href="Relation.Binary.Reflection.html#1833" class="Function">R.prove</a> <a id="4002" href="Algebra.Solver.Monoid.html#3986" class="Bound">ρ</a> <a id="4004" href="Algebra.Solver.Monoid.html#3832" class="Bound">e₁</a> <a id="4007" href="Algebra.Solver.Monoid.html#3835" class="Bound">e₂</a> <a id="4010" class="Symbol">(</a><a id="4011" href="Relation.Binary.Reasoning.Base.Single.html#1925" class="Function Operator">begin</a>
      <a id="4023" href="Algebra.Solver.Monoid.html#1890" class="Function Operator">⟦</a> <a id="4025" href="Algebra.Solver.Monoid.html#2008" class="Function">normalise</a> <a id="4035" href="Algebra.Solver.Monoid.html#3832" class="Bound">e₁</a> <a id="4038" href="Algebra.Solver.Monoid.html#1890" class="Function Operator">⟧⇓</a> <a id="4041" href="Algebra.Solver.Monoid.html#3986" class="Bound">ρ</a>  <a id="4044" href="Relation.Binary.Reasoning.Base.Single.html#2191" class="Function">≡⟨</a> <a id="4047" href="Relation.Binary.PropositionalEquality.Core.html#1158" class="Function">P.cong</a> <a id="4054" class="Symbol">(λ</a> <a id="4057" href="Algebra.Solver.Monoid.html#4057" class="Bound">e</a> <a id="4059" class="Symbol">→</a> <a id="4061" href="Algebra.Solver.Monoid.html#1890" class="Function Operator">⟦</a> <a id="4063" href="Algebra.Solver.Monoid.html#4057" class="Bound">e</a> <a id="4065" href="Algebra.Solver.Monoid.html#1890" class="Function Operator">⟧⇓</a> <a id="4068" href="Algebra.Solver.Monoid.html#3986" class="Bound">ρ</a><a id="4069" class="Symbol">)</a> <a id="4071" href="Algebra.Solver.Monoid.html#3983" class="Bound">eq</a> <a id="4074" href="Relation.Binary.Reasoning.Base.Single.html#2191" class="Function">⟩</a>
      <a id="4082" href="Algebra.Solver.Monoid.html#1890" class="Function Operator">⟦</a> <a id="4084" href="Algebra.Solver.Monoid.html#2008" class="Function">normalise</a> <a id="4094" href="Algebra.Solver.Monoid.html#3835" class="Bound">e₂</a> <a id="4097" href="Algebra.Solver.Monoid.html#1890" class="Function Operator">⟧⇓</a> <a id="4100" href="Algebra.Solver.Monoid.html#3986" class="Bound">ρ</a>  <a id="4103" href="Relation.Binary.Reasoning.Base.Single.html#2564" class="Function Operator">∎</a><a id="4104" class="Symbol">)</a>

<a id="4107" class="Comment">-- This procedure can be combined with from-just.</a>

<a id="prove"></a><a id="4158" href="Algebra.Solver.Monoid.html#4158" class="Function">prove</a> <a id="4164" class="Symbol">:</a> <a id="4166" class="Symbol">∀</a> <a id="4168" href="Algebra.Solver.Monoid.html#4168" class="Bound">n</a> <a id="4170" class="Symbol">(</a><a id="4171" href="Algebra.Solver.Monoid.html#4171" class="Bound">es</a> <a id="4174" class="Symbol">:</a> <a id="4176" href="Algebra.Solver.Monoid.html#1281" class="Datatype">Expr</a> <a id="4181" href="Algebra.Solver.Monoid.html#4168" class="Bound">n</a> <a id="4183" href="Data.Product.Base.html#1618" class="Function Operator">×</a> <a id="4185" href="Algebra.Solver.Monoid.html#1281" class="Datatype">Expr</a> <a id="4190" href="Algebra.Solver.Monoid.html#4168" class="Bound">n</a><a id="4191" class="Symbol">)</a> <a id="4193" class="Symbol">→</a>
        <a id="4203" href="Data.Maybe.Base.html#1847" class="Function">From-just</a> <a id="4213" class="Symbol">(</a><a id="4214" href="Data.Product.Base.html#3109" class="Function">uncurry</a> <a id="4222" href="Algebra.Solver.Monoid.html#3757" class="Function">prove′</a> <a id="4229" href="Algebra.Solver.Monoid.html#4171" class="Bound">es</a><a id="4231" class="Symbol">)</a>
<a id="4233" href="Algebra.Solver.Monoid.html#4158" class="Function">prove</a> <a id="4239" class="Symbol">_</a> <a id="4241" class="Symbol">=</a> <a id="4243" href="Data.Maybe.Base.html#1935" class="Function">from-just</a> <a id="4253" href="Function.Base.html#1115" class="Function Operator">∘</a> <a id="4255" href="Data.Product.Base.html#3109" class="Function">uncurry</a> <a id="4263" href="Algebra.Solver.Monoid.html#3757" class="Function">prove′</a>
</pre></body></html>