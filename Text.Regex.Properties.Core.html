<!DOCTYPE HTML>
<html><head><meta charset="utf-8"><title>Text.Regex.Properties.Core</title><link rel="stylesheet" href="Agda.css"></head><body><pre class="Agda"><a id="1" class="Comment">------------------------------------------------------------------------</a>
<a id="74" class="Comment">-- The Agda standard library</a>
<a id="103" class="Comment">--</a>
<a id="106" class="Comment">-- Regular expressions: core properties (only require a Preorder)</a>
<a id="172" class="Comment">------------------------------------------------------------------------</a>

<a id="246" class="Symbol">{-#</a> <a id="250" class="Keyword">OPTIONS</a> <a id="258" class="Pragma">--cubical-compatible</a> <a id="279" class="Pragma">--safe</a> <a id="286" class="Symbol">#-}</a>

<a id="291" class="Keyword">open</a> <a id="296" class="Keyword">import</a> <a id="303" href="Relation.Binary.Bundles.html" class="Module">Relation.Binary.Bundles</a> <a id="327" class="Keyword">using</a> <a id="333" class="Symbol">(</a><a id="334" href="Relation.Binary.Bundles.html#1952" class="Record">Preorder</a><a id="342" class="Symbol">)</a>

<a id="345" class="Keyword">module</a> <a id="352" href="Text.Regex.Properties.Core.html" class="Module">Text.Regex.Properties.Core</a> <a id="379" class="Symbol">{</a><a id="380" href="Text.Regex.Properties.Core.html#380" class="Bound">a</a> <a id="382" href="Text.Regex.Properties.Core.html#382" class="Bound">e</a> <a id="384" href="Text.Regex.Properties.Core.html#384" class="Bound">r</a><a id="385" class="Symbol">}</a> <a id="387" class="Symbol">(</a><a id="388" href="Text.Regex.Properties.Core.html#388" class="Bound">P</a> <a id="390" class="Symbol">:</a> <a id="392" href="Relation.Binary.Bundles.html#1952" class="Record">Preorder</a> <a id="401" href="Text.Regex.Properties.Core.html#380" class="Bound">a</a> <a id="403" href="Text.Regex.Properties.Core.html#382" class="Bound">e</a> <a id="405" href="Text.Regex.Properties.Core.html#384" class="Bound">r</a><a id="406" class="Symbol">)</a> <a id="408" class="Keyword">where</a>

<a id="415" class="Keyword">open</a> <a id="420" class="Keyword">import</a> <a id="427" href="Level.html" class="Module">Level</a> <a id="433" class="Keyword">using</a> <a id="439" class="Symbol">(</a><a id="440" href="Agda.Primitive.html#804" class="Primitive Operator">_⊔_</a><a id="443" class="Symbol">)</a>

<a id="446" class="Keyword">open</a> <a id="451" class="Keyword">import</a> <a id="458" href="Data.Bool.Base.html" class="Module">Data.Bool.Base</a> <a id="473" class="Keyword">using</a> <a id="479" class="Symbol">(</a><a id="480" href="Agda.Builtin.Bool.html#156" class="Datatype">Bool</a><a id="484" class="Symbol">)</a>
<a id="486" class="Keyword">open</a> <a id="491" class="Keyword">import</a> <a id="498" href="Data.List.Base.html" class="Module">Data.List.Base</a> <a id="513" class="Symbol">as</a> <a id="516" class="Module">List</a> <a id="521" class="Keyword">using</a> <a id="527" class="Symbol">(</a><a id="528" href="Agda.Builtin.List.html#130" class="Datatype">List</a><a id="532" class="Symbol">;</a> <a id="534" href="Data.List.Base.html#7375" class="InductiveConstructor">[]</a><a id="536" class="Symbol">;</a> <a id="538" href="Agda.Builtin.List.html#182" class="InductiveConstructor Operator">_∷_</a><a id="541" class="Symbol">;</a> <a id="543" href="Data.List.Base.html#1943" class="Function Operator">_++_</a><a id="547" class="Symbol">)</a>

<a id="550" class="Keyword">open</a> <a id="555" class="Keyword">import</a> <a id="562" href="Data.List.Relation.Unary.Any.html" class="Module">Data.List.Relation.Unary.Any</a> <a id="591" class="Keyword">using</a> <a id="597" class="Symbol">(</a><a id="598" href="Data.List.Relation.Unary.Any.html#1153" class="Datatype">Any</a><a id="601" class="Symbol">;</a> <a id="603" href="Data.List.Relation.Unary.Any.html#1216" class="InductiveConstructor">here</a><a id="607" class="Symbol">;</a> <a id="609" href="Data.List.Relation.Unary.Any.html#1269" class="InductiveConstructor">there</a><a id="614" class="Symbol">)</a>
<a id="616" class="Keyword">open</a> <a id="621" class="Keyword">import</a> <a id="628" href="Data.List.Relation.Unary.All.html" class="Module">Data.List.Relation.Unary.All</a> <a id="657" class="Keyword">using</a> <a id="663" class="Symbol">(</a><a id="664" href="Data.List.Relation.Unary.All.html#1572" class="Datatype">All</a><a id="667" class="Symbol">;</a> <a id="669" href="Data.List.Relation.Unary.All.html#1635" class="InductiveConstructor">[]</a><a id="671" class="Symbol">;</a> <a id="673" href="Data.List.Relation.Unary.All.html#1652" class="InductiveConstructor Operator">_∷_</a><a id="676" class="Symbol">)</a>
<a id="678" class="Keyword">open</a> <a id="683" class="Keyword">import</a> <a id="690" href="Data.Product.html" class="Module">Data.Product</a> <a id="703" class="Keyword">using</a> <a id="709" class="Symbol">(</a><a id="710" href="Data.Product.Base.html#1618" class="Function Operator">_×_</a><a id="713" class="Symbol">;</a> <a id="715" href="Agda.Builtin.Sigma.html#218" class="InductiveConstructor Operator">_,_</a><a id="718" class="Symbol">)</a>
<a id="720" class="Keyword">open</a> <a id="725" class="Keyword">import</a> <a id="732" href="Data.Sum.Base.html" class="Module">Data.Sum.Base</a> <a id="746" class="Keyword">using</a> <a id="752" class="Symbol">(</a><a id="753" href="Data.Sum.Base.html#625" class="Datatype Operator">_⊎_</a><a id="756" class="Symbol">;</a> <a id="758" href="Data.Sum.Base.html#675" class="InductiveConstructor">inj₁</a><a id="762" class="Symbol">;</a> <a id="764" href="Data.Sum.Base.html#700" class="InductiveConstructor">inj₂</a><a id="768" class="Symbol">)</a>

<a id="771" class="Keyword">open</a> <a id="776" class="Keyword">import</a> <a id="783" href="Relation.Nullary.html" class="Module">Relation.Nullary</a> <a id="800" class="Keyword">using</a> <a id="806" class="Symbol">(</a><a id="807" href="Relation.Nullary.Negation.Core.html#824" class="Function Operator">¬_</a><a id="809" class="Symbol">;</a> <a id="811" href="Relation.Nullary.Decidable.Core.html#1490" class="Record">Dec</a><a id="814" class="Symbol">;</a> <a id="816" href="Relation.Nullary.Decidable.Core.html#1627" class="InductiveConstructor">yes</a><a id="819" class="Symbol">;</a> <a id="821" href="Relation.Nullary.Decidable.Core.html#1664" class="InductiveConstructor">no</a><a id="823" class="Symbol">)</a>
<a id="825" class="Keyword">open</a> <a id="830" class="Keyword">import</a> <a id="837" href="Relation.Nullary.Negation.html" class="Module">Relation.Nullary.Negation</a> <a id="863" class="Keyword">using</a> <a id="869" class="Symbol">(</a><a id="870" href="Relation.Nullary.Negation.Core.html#1276" class="Function">contradiction</a><a id="883" class="Symbol">)</a>
<a id="885" class="Keyword">open</a> <a id="890" class="Keyword">import</a> <a id="897" href="Relation.Unary.html" class="Module">Relation.Unary</a> <a id="912" class="Keyword">using</a> <a id="918" class="Symbol">(</a><a id="919" href="Relation.Unary.html#1232" class="Function">Pred</a><a id="923" class="Symbol">)</a>
<a id="925" class="Keyword">open</a> <a id="930" class="Keyword">import</a> <a id="937" href="Relation.Binary.PropositionalEquality.html" class="Module">Relation.Binary.PropositionalEquality</a>

<a id="976" class="Keyword">open</a> <a id="981" href="Relation.Binary.Bundles.html#1952" class="Module">Preorder</a> <a id="990" href="Text.Regex.Properties.Core.html#388" class="Bound">P</a> <a id="992" class="Keyword">using</a> <a id="998" class="Symbol">(</a><a id="999" href="Relation.Binary.Bundles.html#2054" class="Field Operator">_≈_</a><a id="1002" class="Symbol">)</a> <a id="1004" class="Keyword">renaming</a> <a id="1013" class="Symbol">(</a><a id="1014" href="Relation.Binary.Bundles.html#2031" class="Field">Carrier</a> <a id="1022" class="Symbol">to</a> <a id="1025" class="Field">A</a><a id="1026" class="Symbol">;</a> <a id="1028" href="Relation.Binary.Bundles.html#2115" class="Field Operator">_∼_</a> <a id="1032" class="Symbol">to</a> <a id="1035" class="Field Operator">_≤_</a><a id="1038" class="Symbol">)</a>
<a id="1040" class="Keyword">open</a> <a id="1045" class="Keyword">import</a> <a id="1052" href="Text.Regex.Base.html" class="Module">Text.Regex.Base</a> <a id="1068" href="Text.Regex.Properties.Core.html#388" class="Bound">P</a>

<a id="1071" class="Keyword">open</a> <a id="1076" class="Keyword">import</a> <a id="1083" href="Data.List.Relation.Ternary.Appending.Propositional.html" class="Module">Data.List.Relation.Ternary.Appending.Propositional</a> <a id="1134" class="Symbol">{</a><a id="1135" class="Argument">A</a> <a id="1137" class="Symbol">=</a> <a id="1139" href="Text.Regex.Properties.Core.html#1025" class="Field">A</a><a id="1140" class="Symbol">}</a>
<a id="1142" class="Keyword">open</a> <a id="1147" class="Keyword">import</a> <a id="1154" href="Data.List.Relation.Ternary.Appending.Propositional.Properties.html" class="Module">Data.List.Relation.Ternary.Appending.Propositional.Properties</a> <a id="1216" class="Symbol">{</a><a id="1217" class="Argument">A</a> <a id="1219" class="Symbol">=</a> <a id="1221" href="Text.Regex.Properties.Core.html#1025" class="Field">A</a><a id="1222" class="Symbol">}</a>

<a id="1225" class="Comment">------------------------------------------------------------------------</a>
<a id="1298" class="Comment">-- Views</a>

<a id="is-∅"></a><a id="1308" href="Text.Regex.Properties.Core.html#1308" class="Function">is-∅</a> <a id="1313" class="Symbol">:</a> <a id="1315" class="Symbol">∀</a> <a id="1317" class="Symbol">(</a><a id="1318" href="Text.Regex.Properties.Core.html#1318" class="Bound">e</a> <a id="1320" class="Symbol">:</a> <a id="1322" href="Text.Regex.Base.html#1121" class="Datatype">Exp</a><a id="1325" class="Symbol">)</a> <a id="1327" class="Symbol">→</a> <a id="1329" href="Relation.Nullary.Decidable.Core.html#1490" class="Record">Dec</a> <a id="1333" class="Symbol">(</a><a id="1334" href="Text.Regex.Properties.Core.html#1318" class="Bound">e</a> <a id="1336" href="Agda.Builtin.Equality.html#133" class="Datatype Operator">≡</a> <a id="1338" href="Text.Regex.Base.html#1792" class="InductiveConstructor">∅</a><a id="1339" class="Symbol">)</a>
<a id="1341" href="Text.Regex.Properties.Core.html#1308" class="Function">is-∅</a> <a id="1346" href="Text.Regex.Base.html#1141" class="InductiveConstructor">ε</a>          <a id="1357" class="Symbol">=</a> <a id="1359" href="Relation.Nullary.Decidable.Core.html#1664" class="InductiveConstructor">no</a> <a id="1362" class="Symbol">(λ</a> <a id="1365" class="Symbol">())</a>
<a id="1369" href="Text.Regex.Properties.Core.html#1308" class="Function">is-∅</a> <a id="1374" href="Text.Regex.Base.html#1154" class="InductiveConstructor Operator">[</a> <a id="1376" href="Agda.Builtin.List.html#167" class="InductiveConstructor">[]</a> <a id="1379" href="Text.Regex.Base.html#1154" class="InductiveConstructor Operator">]</a>     <a id="1385" class="Symbol">=</a> <a id="1387" href="Relation.Nullary.Decidable.Core.html#1627" class="InductiveConstructor">yes</a> <a id="1391" href="Agda.Builtin.Equality.html#190" class="InductiveConstructor">refl</a>
<a id="1396" href="Text.Regex.Properties.Core.html#1308" class="Function">is-∅</a> <a id="1401" href="Text.Regex.Base.html#1154" class="InductiveConstructor Operator">[</a> <a id="1403" href="Text.Regex.Properties.Core.html#1403" class="Bound">r</a> <a id="1405" href="Agda.Builtin.List.html#182" class="InductiveConstructor Operator">∷</a> <a id="1407" href="Text.Regex.Properties.Core.html#1407" class="Bound">rs</a> <a id="1410" href="Text.Regex.Base.html#1154" class="InductiveConstructor Operator">]</a> <a id="1412" class="Symbol">=</a> <a id="1414" href="Relation.Nullary.Decidable.Core.html#1664" class="InductiveConstructor">no</a> <a id="1417" class="Symbol">(λ</a> <a id="1420" class="Symbol">())</a>
<a id="1424" href="Text.Regex.Properties.Core.html#1308" class="Function">is-∅</a> <a id="1429" href="Text.Regex.Base.html#1187" class="InductiveConstructor Operator">[^</a> <a id="1432" href="Text.Regex.Properties.Core.html#1432" class="Bound">rs</a> <a id="1435" href="Text.Regex.Base.html#1187" class="InductiveConstructor Operator">]</a>    <a id="1440" class="Symbol">=</a> <a id="1442" href="Relation.Nullary.Decidable.Core.html#1664" class="InductiveConstructor">no</a> <a id="1445" class="Symbol">(λ</a> <a id="1448" class="Symbol">())</a>
<a id="1452" href="Text.Regex.Properties.Core.html#1308" class="Function">is-∅</a> <a id="1457" class="Symbol">(</a><a id="1458" href="Text.Regex.Properties.Core.html#1458" class="Bound">e</a> <a id="1460" href="Text.Regex.Base.html#1220" class="InductiveConstructor Operator">∣</a> <a id="1462" href="Text.Regex.Properties.Core.html#1462" class="Bound">f</a><a id="1463" class="Symbol">)</a>    <a id="1468" class="Symbol">=</a> <a id="1470" href="Relation.Nullary.Decidable.Core.html#1664" class="InductiveConstructor">no</a> <a id="1473" class="Symbol">(λ</a> <a id="1476" class="Symbol">())</a>
<a id="1480" href="Text.Regex.Properties.Core.html#1308" class="Function">is-∅</a> <a id="1485" class="Symbol">(</a><a id="1486" href="Text.Regex.Properties.Core.html#1486" class="Bound">e</a> <a id="1488" href="Text.Regex.Base.html#1247" class="InductiveConstructor Operator">∙</a> <a id="1490" href="Text.Regex.Properties.Core.html#1490" class="Bound">f</a><a id="1491" class="Symbol">)</a>    <a id="1496" class="Symbol">=</a> <a id="1498" href="Relation.Nullary.Decidable.Core.html#1664" class="InductiveConstructor">no</a> <a id="1501" class="Symbol">(λ</a> <a id="1504" class="Symbol">())</a>
<a id="1508" href="Text.Regex.Properties.Core.html#1308" class="Function">is-∅</a> <a id="1513" class="Symbol">(</a><a id="1514" href="Text.Regex.Properties.Core.html#1514" class="Bound">e</a> <a id="1516" href="Text.Regex.Base.html#1274" class="InductiveConstructor Operator">⋆</a><a id="1517" class="Symbol">)</a>      <a id="1524" class="Symbol">=</a> <a id="1526" href="Relation.Nullary.Decidable.Core.html#1664" class="InductiveConstructor">no</a> <a id="1529" class="Symbol">(λ</a> <a id="1532" class="Symbol">())</a>

<a id="is-ε"></a><a id="1537" href="Text.Regex.Properties.Core.html#1537" class="Function">is-ε</a> <a id="1542" class="Symbol">:</a> <a id="1544" class="Symbol">∀</a> <a id="1546" class="Symbol">(</a><a id="1547" href="Text.Regex.Properties.Core.html#1547" class="Bound">e</a> <a id="1549" class="Symbol">:</a> <a id="1551" href="Text.Regex.Base.html#1121" class="Datatype">Exp</a><a id="1554" class="Symbol">)</a> <a id="1556" class="Symbol">→</a> <a id="1558" href="Relation.Nullary.Decidable.Core.html#1490" class="Record">Dec</a> <a id="1562" class="Symbol">(</a><a id="1563" href="Text.Regex.Properties.Core.html#1547" class="Bound">e</a> <a id="1565" href="Agda.Builtin.Equality.html#133" class="Datatype Operator">≡</a> <a id="1567" class="InductiveConstructor">ε</a><a id="1568" class="Symbol">)</a>
<a id="1570" href="Text.Regex.Properties.Core.html#1537" class="Function">is-ε</a> <a id="1575" href="Text.Regex.Base.html#1141" class="InductiveConstructor">ε</a>       <a id="1583" class="Symbol">=</a> <a id="1585" href="Relation.Nullary.Decidable.Core.html#1627" class="InductiveConstructor">yes</a> <a id="1589" href="Agda.Builtin.Equality.html#190" class="InductiveConstructor">refl</a>
<a id="1594" href="Text.Regex.Properties.Core.html#1537" class="Function">is-ε</a> <a id="1599" href="Text.Regex.Base.html#1154" class="InductiveConstructor Operator">[</a> <a id="1601" href="Text.Regex.Properties.Core.html#1601" class="Bound">rs</a> <a id="1604" href="Text.Regex.Base.html#1154" class="InductiveConstructor Operator">]</a>  <a id="1607" class="Symbol">=</a> <a id="1609" href="Relation.Nullary.Decidable.Core.html#1664" class="InductiveConstructor">no</a> <a id="1612" class="Symbol">(λ</a> <a id="1615" class="Symbol">())</a>
<a id="1619" href="Text.Regex.Properties.Core.html#1537" class="Function">is-ε</a> <a id="1624" href="Text.Regex.Base.html#1187" class="InductiveConstructor Operator">[^</a> <a id="1627" href="Text.Regex.Properties.Core.html#1627" class="Bound">rs</a> <a id="1630" href="Text.Regex.Base.html#1187" class="InductiveConstructor Operator">]</a> <a id="1632" class="Symbol">=</a> <a id="1634" href="Relation.Nullary.Decidable.Core.html#1664" class="InductiveConstructor">no</a> <a id="1637" class="Symbol">(λ</a> <a id="1640" class="Symbol">())</a>
<a id="1644" href="Text.Regex.Properties.Core.html#1537" class="Function">is-ε</a> <a id="1649" class="Symbol">(</a><a id="1650" href="Text.Regex.Properties.Core.html#1650" class="Bound">e</a> <a id="1652" href="Text.Regex.Base.html#1220" class="InductiveConstructor Operator">∣</a> <a id="1654" href="Text.Regex.Properties.Core.html#1654" class="Bound">f</a><a id="1655" class="Symbol">)</a> <a id="1657" class="Symbol">=</a> <a id="1659" href="Relation.Nullary.Decidable.Core.html#1664" class="InductiveConstructor">no</a> <a id="1662" class="Symbol">(λ</a> <a id="1665" class="Symbol">())</a>
<a id="1669" href="Text.Regex.Properties.Core.html#1537" class="Function">is-ε</a> <a id="1674" class="Symbol">(</a><a id="1675" href="Text.Regex.Properties.Core.html#1675" class="Bound">e</a> <a id="1677" href="Text.Regex.Base.html#1247" class="InductiveConstructor Operator">∙</a> <a id="1679" href="Text.Regex.Properties.Core.html#1679" class="Bound">f</a><a id="1680" class="Symbol">)</a> <a id="1682" class="Symbol">=</a> <a id="1684" href="Relation.Nullary.Decidable.Core.html#1664" class="InductiveConstructor">no</a> <a id="1687" class="Symbol">(λ</a> <a id="1690" class="Symbol">())</a>
<a id="1694" href="Text.Regex.Properties.Core.html#1537" class="Function">is-ε</a> <a id="1699" class="Symbol">(</a><a id="1700" href="Text.Regex.Properties.Core.html#1700" class="Bound">e</a> <a id="1702" href="Text.Regex.Base.html#1274" class="InductiveConstructor Operator">⋆</a><a id="1703" class="Symbol">)</a>   <a id="1707" class="Symbol">=</a> <a id="1709" href="Relation.Nullary.Decidable.Core.html#1664" class="InductiveConstructor">no</a> <a id="1712" class="Symbol">(λ</a> <a id="1715" class="Symbol">())</a>

<a id="1720" class="Comment">------------------------------------------------------------------------</a>
<a id="1793" class="Comment">-- Inversion lemmas</a>

<a id="∉∅"></a><a id="1814" href="Text.Regex.Properties.Core.html#1814" class="Function">∉∅</a> <a id="1817" class="Symbol">:</a> <a id="1819" class="Symbol">∀</a> <a id="1821" class="Symbol">{</a><a id="1822" href="Text.Regex.Properties.Core.html#1822" class="Bound">xs</a><a id="1824" class="Symbol">}</a> <a id="1826" class="Symbol">→</a> <a id="1828" href="Text.Regex.Properties.Core.html#1822" class="Bound">xs</a> <a id="1831" href="Text.Regex.Base.html#2752" class="Function Operator">∉</a> <a id="1833" href="Text.Regex.Base.html#1792" class="InductiveConstructor">∅</a>
<a id="1835" href="Text.Regex.Properties.Core.html#1814" class="Function">∉∅</a> <a id="1838" href="Text.Regex.Base.html#2351" class="InductiveConstructor Operator">[</a> <a id="1840" class="Symbol">()</a> <a id="1843" href="Text.Regex.Base.html#2351" class="InductiveConstructor Operator">]</a>

<a id="∈ε⋆-inv"></a><a id="1846" href="Text.Regex.Properties.Core.html#1846" class="Function">∈ε⋆-inv</a> <a id="1854" class="Symbol">:</a> <a id="1856" class="Symbol">∀</a> <a id="1858" class="Symbol">{</a><a id="1859" href="Text.Regex.Properties.Core.html#1859" class="Bound">w</a><a id="1860" class="Symbol">}</a> <a id="1862" class="Symbol">→</a> <a id="1864" href="Text.Regex.Properties.Core.html#1859" class="Bound">w</a> <a id="1866" href="Text.Regex.Base.html#2231" class="Datatype Operator">∈</a> <a id="1868" class="Symbol">(</a><a id="1869" class="InductiveConstructor">ε</a> <a id="1871" href="Text.Regex.Base.html#1274" class="InductiveConstructor Operator">⋆</a><a id="1872" class="Symbol">)</a> <a id="1874" class="Symbol">→</a> <a id="1876" href="Text.Regex.Properties.Core.html#1859" class="Bound">w</a> <a id="1878" href="Text.Regex.Base.html#2231" class="Datatype Operator">∈</a> <a id="1880" class="InductiveConstructor">ε</a>
<a id="1882" href="Text.Regex.Properties.Core.html#1846" class="Function">∈ε⋆-inv</a> <a id="1890" class="Symbol">(</a><a id="1891" href="Text.Regex.Base.html#2674" class="InductiveConstructor">star</a> <a id="1896" class="Symbol">(</a><a id="1897" href="Text.Regex.Base.html#2512" class="InductiveConstructor">sum</a> <a id="1901" class="Symbol">(</a><a id="1902" href="Data.Sum.Base.html#675" class="InductiveConstructor">inj₁</a> <a id="1907" href="Text.Regex.Base.html#2276" class="InductiveConstructor">ε</a><a id="1908" class="Symbol">)))</a> <a id="1912" class="Symbol">=</a> <a id="1914" href="Text.Regex.Base.html#2276" class="InductiveConstructor">ε</a>
<a id="1916" href="Text.Regex.Properties.Core.html#1846" class="Function">∈ε⋆-inv</a> <a id="1924" class="Symbol">(</a><a id="1925" href="Text.Regex.Base.html#2674" class="InductiveConstructor">star</a> <a id="1930" class="Symbol">(</a><a id="1931" href="Text.Regex.Base.html#2512" class="InductiveConstructor">sum</a> <a id="1935" class="Symbol">(</a><a id="1936" href="Data.Sum.Base.html#700" class="InductiveConstructor">inj₂</a> <a id="1941" class="Symbol">(</a><a id="1942" href="Text.Regex.Base.html#2593" class="InductiveConstructor">prod</a> <a id="1947" href="Text.Regex.Properties.Core.html#1947" class="Bound">eq</a> <a id="1950" href="Text.Regex.Base.html#2276" class="InductiveConstructor">ε</a> <a id="1952" href="Text.Regex.Properties.Core.html#1952" class="Bound">p</a><a id="1953" class="Symbol">))))</a> <a id="1958" class="Keyword">rewrite</a> <a id="1966" href="Data.List.Relation.Ternary.Appending.Propositional.Properties.html#1236" class="Function">[]++⁻¹</a> <a id="1973" href="Text.Regex.Properties.Core.html#1947" class="Bound">eq</a> <a id="1976" class="Symbol">=</a> <a id="1978" href="Text.Regex.Properties.Core.html#1846" class="Function">∈ε⋆-inv</a> <a id="1986" href="Text.Regex.Properties.Core.html#1952" class="Bound">p</a>

<a id="∈∅⋆-inv"></a><a id="1989" href="Text.Regex.Properties.Core.html#1989" class="Function">∈∅⋆-inv</a> <a id="1997" class="Symbol">:</a> <a id="1999" class="Symbol">∀</a> <a id="2001" class="Symbol">{</a><a id="2002" href="Text.Regex.Properties.Core.html#2002" class="Bound">w</a><a id="2003" class="Symbol">}</a> <a id="2005" class="Symbol">→</a> <a id="2007" href="Text.Regex.Properties.Core.html#2002" class="Bound">w</a> <a id="2009" href="Text.Regex.Base.html#2231" class="Datatype Operator">∈</a> <a id="2011" class="Symbol">(</a><a id="2012" href="Text.Regex.Base.html#1792" class="InductiveConstructor">∅</a> <a id="2014" href="Text.Regex.Base.html#1274" class="InductiveConstructor Operator">⋆</a><a id="2015" class="Symbol">)</a> <a id="2017" class="Symbol">→</a> <a id="2019" href="Text.Regex.Properties.Core.html#2002" class="Bound">w</a> <a id="2021" href="Text.Regex.Base.html#2231" class="Datatype Operator">∈</a> <a id="2023" class="InductiveConstructor">ε</a>
<a id="2025" href="Text.Regex.Properties.Core.html#1989" class="Function">∈∅⋆-inv</a> <a id="2033" class="Symbol">(</a><a id="2034" href="Text.Regex.Base.html#2674" class="InductiveConstructor">star</a> <a id="2039" class="Symbol">(</a><a id="2040" href="Text.Regex.Base.html#2512" class="InductiveConstructor">sum</a> <a id="2044" class="Symbol">(</a><a id="2045" href="Data.Sum.Base.html#675" class="InductiveConstructor">inj₁</a> <a id="2050" href="Text.Regex.Base.html#2276" class="InductiveConstructor">ε</a><a id="2051" class="Symbol">)))</a> <a id="2055" class="Symbol">=</a> <a id="2057" href="Text.Regex.Base.html#2276" class="InductiveConstructor">ε</a>
<a id="2059" href="Text.Regex.Properties.Core.html#1989" class="Function">∈∅⋆-inv</a> <a id="2067" class="Symbol">(</a><a id="2068" href="Text.Regex.Base.html#2674" class="InductiveConstructor">star</a> <a id="2073" class="Symbol">(</a><a id="2074" href="Text.Regex.Base.html#2512" class="InductiveConstructor">sum</a> <a id="2078" class="Symbol">(</a><a id="2079" href="Data.Sum.Base.html#700" class="InductiveConstructor">inj₂</a> <a id="2084" class="Symbol">(</a><a id="2085" href="Text.Regex.Base.html#2593" class="InductiveConstructor">prod</a> <a id="2090" href="Text.Regex.Properties.Core.html#2090" class="Bound">eq</a> <a id="2093" href="Text.Regex.Properties.Core.html#2093" class="Bound">p</a> <a id="2095" href="Text.Regex.Properties.Core.html#2095" class="Bound">q</a><a id="2096" class="Symbol">))))</a> <a id="2101" class="Symbol">=</a> <a id="2103" href="Relation.Nullary.Negation.Core.html#1276" class="Function">contradiction</a> <a id="2117" href="Text.Regex.Properties.Core.html#2093" class="Bound">p</a> <a id="2119" href="Text.Regex.Properties.Core.html#1814" class="Function">∉∅</a>

<a id="∈ε∙e-inv"></a><a id="2123" href="Text.Regex.Properties.Core.html#2123" class="Function">∈ε∙e-inv</a> <a id="2132" class="Symbol">:</a> <a id="2134" class="Symbol">∀</a> <a id="2136" class="Symbol">{</a><a id="2137" href="Text.Regex.Properties.Core.html#2137" class="Bound">w</a> <a id="2139" href="Text.Regex.Properties.Core.html#2139" class="Bound">e</a><a id="2140" class="Symbol">}</a> <a id="2142" class="Symbol">→</a> <a id="2144" href="Text.Regex.Properties.Core.html#2137" class="Bound">w</a> <a id="2146" href="Text.Regex.Base.html#2231" class="Datatype Operator">∈</a> <a id="2148" class="Symbol">(</a><a id="2149" class="InductiveConstructor">ε</a> <a id="2151" href="Text.Regex.Base.html#1247" class="InductiveConstructor Operator">∙</a> <a id="2153" href="Text.Regex.Properties.Core.html#2139" class="Bound">e</a><a id="2154" class="Symbol">)</a> <a id="2156" class="Symbol">→</a> <a id="2158" href="Text.Regex.Properties.Core.html#2137" class="Bound">w</a> <a id="2160" href="Text.Regex.Base.html#2231" class="Datatype Operator">∈</a> <a id="2162" href="Text.Regex.Properties.Core.html#2139" class="Bound">e</a>
<a id="2164" href="Text.Regex.Properties.Core.html#2123" class="Function">∈ε∙e-inv</a> <a id="2173" class="Symbol">(</a><a id="2174" href="Text.Regex.Base.html#2593" class="InductiveConstructor">prod</a> <a id="2179" href="Text.Regex.Properties.Core.html#2179" class="Bound">eq</a> <a id="2182" href="Text.Regex.Base.html#2276" class="InductiveConstructor">ε</a> <a id="2184" href="Text.Regex.Properties.Core.html#2184" class="Bound">p</a><a id="2185" class="Symbol">)</a> <a id="2187" class="Keyword">rewrite</a> <a id="2195" href="Data.List.Relation.Ternary.Appending.Propositional.Properties.html#1236" class="Function">[]++⁻¹</a> <a id="2202" href="Text.Regex.Properties.Core.html#2179" class="Bound">eq</a> <a id="2205" class="Symbol">=</a> <a id="2207" href="Text.Regex.Properties.Core.html#2184" class="Bound">p</a>

<a id="∈e∙ε-inv"></a><a id="2210" href="Text.Regex.Properties.Core.html#2210" class="Function">∈e∙ε-inv</a> <a id="2219" class="Symbol">:</a> <a id="2221" class="Symbol">∀</a> <a id="2223" class="Symbol">{</a><a id="2224" href="Text.Regex.Properties.Core.html#2224" class="Bound">w</a> <a id="2226" href="Text.Regex.Properties.Core.html#2226" class="Bound">e</a><a id="2227" class="Symbol">}</a> <a id="2229" class="Symbol">→</a> <a id="2231" href="Text.Regex.Properties.Core.html#2224" class="Bound">w</a> <a id="2233" href="Text.Regex.Base.html#2231" class="Datatype Operator">∈</a> <a id="2235" class="Symbol">(</a><a id="2236" href="Text.Regex.Properties.Core.html#2226" class="Bound">e</a> <a id="2238" href="Text.Regex.Base.html#1247" class="InductiveConstructor Operator">∙</a> <a id="2240" class="InductiveConstructor">ε</a><a id="2241" class="Symbol">)</a> <a id="2243" class="Symbol">→</a> <a id="2245" href="Text.Regex.Properties.Core.html#2224" class="Bound">w</a> <a id="2247" href="Text.Regex.Base.html#2231" class="Datatype Operator">∈</a> <a id="2249" href="Text.Regex.Properties.Core.html#2226" class="Bound">e</a>
<a id="2251" href="Text.Regex.Properties.Core.html#2210" class="Function">∈e∙ε-inv</a> <a id="2260" class="Symbol">(</a><a id="2261" href="Text.Regex.Base.html#2593" class="InductiveConstructor">prod</a> <a id="2266" href="Text.Regex.Properties.Core.html#2266" class="Bound">eq</a> <a id="2269" href="Text.Regex.Properties.Core.html#2269" class="Bound">p</a> <a id="2271" href="Text.Regex.Base.html#2276" class="InductiveConstructor">ε</a><a id="2272" class="Symbol">)</a> <a id="2274" class="Keyword">rewrite</a> <a id="2282" href="Data.List.Relation.Ternary.Appending.Propositional.Properties.html#1322" class="Function">++[]⁻¹</a> <a id="2289" href="Text.Regex.Properties.Core.html#2266" class="Bound">eq</a> <a id="2292" class="Symbol">=</a> <a id="2294" href="Text.Regex.Properties.Core.html#2269" class="Bound">p</a>

<a id="[]∈e∙f-inv"></a><a id="2297" href="Text.Regex.Properties.Core.html#2297" class="Function">[]∈e∙f-inv</a> <a id="2308" class="Symbol">:</a> <a id="2310" class="Symbol">∀</a> <a id="2312" class="Symbol">{</a><a id="2313" href="Text.Regex.Properties.Core.html#2313" class="Bound">e</a> <a id="2315" href="Text.Regex.Properties.Core.html#2315" class="Bound">f</a><a id="2316" class="Symbol">}</a> <a id="2318" class="Symbol">→</a> <a id="2320" class="InductiveConstructor">[]</a> <a id="2323" href="Text.Regex.Base.html#2231" class="Datatype Operator">∈</a> <a id="2325" class="Symbol">(</a><a id="2326" href="Text.Regex.Properties.Core.html#2313" class="Bound">e</a> <a id="2328" href="Text.Regex.Base.html#1247" class="InductiveConstructor Operator">∙</a> <a id="2330" href="Text.Regex.Properties.Core.html#2315" class="Bound">f</a><a id="2331" class="Symbol">)</a> <a id="2333" class="Symbol">→</a> <a id="2335" class="InductiveConstructor">[]</a> <a id="2338" href="Text.Regex.Base.html#2231" class="Datatype Operator">∈</a> <a id="2340" href="Text.Regex.Properties.Core.html#2313" class="Bound">e</a> <a id="2342" href="Data.Product.Base.html#1618" class="Function Operator">×</a> <a id="2344" class="InductiveConstructor">[]</a> <a id="2347" href="Text.Regex.Base.html#2231" class="Datatype Operator">∈</a> <a id="2349" href="Text.Regex.Properties.Core.html#2315" class="Bound">f</a>
<a id="2351" href="Text.Regex.Properties.Core.html#2297" class="Function">[]∈e∙f-inv</a> <a id="2362" class="Symbol">(</a><a id="2363" href="Text.Regex.Base.html#2593" class="InductiveConstructor">prod</a> <a id="2368" href="Text.Regex.Properties.Core.html#2368" class="Bound">eq</a> <a id="2371" href="Text.Regex.Properties.Core.html#2371" class="Bound">p</a> <a id="2373" href="Text.Regex.Properties.Core.html#2373" class="Bound">q</a><a id="2374" class="Symbol">)</a> <a id="2376" class="Keyword">rewrite</a> <a id="2384" href="Data.List.Relation.Ternary.Appending.Properties.html#1792" class="Function">conicalˡ</a> <a id="2393" href="Text.Regex.Properties.Core.html#2368" class="Bound">eq</a> <a id="2396" class="Symbol">|</a> <a id="2398" href="Data.List.Relation.Ternary.Appending.Properties.html#1863" class="Function">conicalʳ</a> <a id="2407" href="Text.Regex.Properties.Core.html#2368" class="Bound">eq</a> <a id="2410" class="Symbol">=</a> <a id="2412" href="Text.Regex.Properties.Core.html#2371" class="Bound">p</a> <a id="2414" href="Agda.Builtin.Sigma.html#218" class="InductiveConstructor Operator">,</a> <a id="2416" href="Text.Regex.Properties.Core.html#2373" class="Bound">q</a>
</pre></body></html>