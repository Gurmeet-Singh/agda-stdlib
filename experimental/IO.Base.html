<!DOCTYPE HTML>
<html><head><meta charset="utf-8"><title>IO.Base</title><link rel="stylesheet" href="Agda.css"></head><body><pre class="Agda"><a id="1" class="Comment">------------------------------------------------------------------------</a>
<a id="74" class="Comment">-- The Agda standard library</a>
<a id="103" class="Comment">--</a>
<a id="106" class="Comment">-- IO: basic types and functions</a>
<a id="139" class="Comment">------------------------------------------------------------------------</a>

<a id="213" class="Symbol">{-#</a> <a id="217" class="Keyword">OPTIONS</a> <a id="225" class="Pragma">--without-K</a> <a id="237" class="Pragma">--guardedness</a> <a id="251" class="Symbol">#-}</a>

<a id="256" class="Keyword">module</a> <a id="263" href="IO.Base.html" class="Module">IO.Base</a> <a id="271" class="Keyword">where</a>

<a id="278" class="Keyword">open</a> <a id="283" class="Keyword">import</a> <a id="290" href="Level.html" class="Module">Level</a>
<a id="296" class="Keyword">open</a> <a id="301" class="Keyword">import</a> <a id="308" href="Codata.Musical.Notation.html" class="Module">Codata.Musical.Notation</a>
<a id="332" class="Keyword">open</a> <a id="337" class="Keyword">import</a> <a id="344" href="Data.Bool.Base.html" class="Module">Data.Bool.Base</a> <a id="359" class="Keyword">using</a> <a id="365" class="Symbol">(</a><a id="366" href="Agda.Builtin.Bool.html#147" class="Datatype">Bool</a><a id="370" class="Symbol">;</a> <a id="372" href="Agda.Builtin.Bool.html#172" class="InductiveConstructor">true</a><a id="376" class="Symbol">;</a> <a id="378" href="Agda.Builtin.Bool.html#166" class="InductiveConstructor">false</a><a id="383" class="Symbol">;</a> <a id="385" href="Data.Bool.Base.html#932" class="Function">not</a><a id="388" class="Symbol">)</a>
<a id="390" class="Keyword">open</a> <a id="395" class="Keyword">import</a> <a id="402" href="Agda.Builtin.Maybe.html" class="Module">Agda.Builtin.Maybe</a> <a id="421" class="Keyword">using</a> <a id="427" class="Symbol">(</a><a id="428" href="Agda.Builtin.Maybe.html#109" class="Datatype">Maybe</a><a id="433" class="Symbol">;</a> <a id="435" href="Agda.Builtin.Maybe.html#168" class="InductiveConstructor">nothing</a><a id="442" class="Symbol">;</a> <a id="444" href="Agda.Builtin.Maybe.html#147" class="InductiveConstructor">just</a><a id="448" class="Symbol">)</a>
<a id="450" class="Keyword">open</a> <a id="455" class="Keyword">import</a> <a id="462" href="Data.Sum.Base.html" class="Module">Data.Sum.Base</a> <a id="476" class="Keyword">using</a> <a id="482" class="Symbol">(</a><a id="483" href="Data.Sum.Base.html#734" class="Datatype Operator">_⊎_</a><a id="486" class="Symbol">;</a> <a id="488" href="Data.Sum.Base.html#784" class="InductiveConstructor">inj₁</a><a id="492" class="Symbol">;</a> <a id="494" href="Data.Sum.Base.html#809" class="InductiveConstructor">inj₂</a><a id="498" class="Symbol">)</a>
<a id="500" class="Keyword">import</a> <a id="507" href="Agda.Builtin.Unit.html" class="Module">Agda.Builtin.Unit</a> <a id="525" class="Symbol">as</a> <a id="528" class="Module">Unit0</a>
<a id="534" class="Keyword">open</a> <a id="539" class="Keyword">import</a> <a id="546" href="Data.Unit.Polymorphic.Base.html" class="Module">Data.Unit.Polymorphic.Base</a>
<a id="573" class="Keyword">open</a> <a id="578" class="Keyword">import</a> <a id="585" href="Function.Base.html" class="Module">Function.Base</a> <a id="599" class="Keyword">using</a> <a id="605" class="Symbol">(</a><a id="606" href="Function.Base.html#3637" class="Function Operator">_∘′_</a><a id="610" class="Symbol">;</a> <a id="612" href="Function.Base.html#716" class="Function">const</a><a id="617" class="Symbol">;</a> <a id="619" href="Function.Base.html#1629" class="Function">flip</a><a id="623" class="Symbol">)</a>
<a id="625" class="Keyword">import</a> <a id="632" href="IO.Primitive.html" class="Module">IO.Primitive</a> <a id="645" class="Symbol">as</a> <a id="648" class="Module">Prim</a>

<a id="654" class="Keyword">private</a>
  <a id="664" class="Keyword">variable</a>
    <a id="677" href="IO.Base.html#677" class="Generalizable">a</a> <a id="679" href="IO.Base.html#679" class="Generalizable">b</a> <a id="681" class="Symbol">:</a> <a id="683" href="Agda.Primitive.html#582" class="Postulate">Level</a>
    <a id="693" href="IO.Base.html#693" class="Generalizable">A</a> <a id="695" class="Symbol">:</a> <a id="697" href="Agda.Primitive.html#311" class="Primitive">Set</a> <a id="701" href="IO.Base.html#677" class="Generalizable">a</a>
    <a id="707" href="IO.Base.html#707" class="Generalizable">B</a> <a id="709" class="Symbol">:</a> <a id="711" href="Agda.Primitive.html#311" class="Primitive">Set</a> <a id="715" href="IO.Base.html#679" class="Generalizable">b</a>

<a id="718" class="Comment">------------------------------------------------------------------------</a>
<a id="791" class="Comment">-- The IO monad</a>

<a id="808" class="Comment">-- One cannot write &quot;infinitely large&quot; computations with the</a>
<a id="869" class="Comment">-- postulated IO monad in IO.Primitive without turning off the</a>
<a id="932" class="Comment">-- termination checker (or going via the FFI, or perhaps abusing</a>
<a id="997" class="Comment">-- something else). The following coinductive deep embedding is</a>
<a id="1061" class="Comment">-- introduced to avoid this problem. Possible non-termination is</a>
<a id="1126" class="Comment">-- isolated to the run function below.</a>

<a id="1166" class="Keyword">data</a> <a id="IO"></a><a id="1171" href="IO.Base.html#1171" class="Datatype">IO</a> <a id="1174" class="Symbol">(</a><a id="1175" href="IO.Base.html#1175" class="Bound">A</a> <a id="1177" class="Symbol">:</a> <a id="1179" href="Agda.Primitive.html#311" class="Primitive">Set</a> <a id="1183" href="IO.Base.html#677" class="Generalizable">a</a><a id="1184" class="Symbol">)</a> <a id="1186" class="Symbol">:</a> <a id="1188" href="Agda.Primitive.html#311" class="Primitive">Set</a> <a id="1192" class="Symbol">(</a><a id="1193" href="Agda.Primitive.html#765" class="Primitive">suc</a> <a id="1197" href="IO.Base.html#1183" class="Bound">a</a><a id="1198" class="Symbol">)</a> <a id="1200" class="Keyword">where</a>
  <a id="IO.lift"></a><a id="1208" href="IO.Base.html#1208" class="InductiveConstructor">lift</a>   <a id="1215" class="Symbol">:</a> <a id="1217" class="Symbol">(</a><a id="1218" href="IO.Base.html#1218" class="Bound">m</a> <a id="1220" class="Symbol">:</a> <a id="1222" href="Agda.Builtin.IO.html#111" class="Postulate">Prim.IO</a> <a id="1230" href="IO.Base.html#1175" class="Bound">A</a><a id="1231" class="Symbol">)</a> <a id="1233" class="Symbol">→</a> <a id="1235" href="IO.Base.html#1171" class="Datatype">IO</a> <a id="1238" href="IO.Base.html#1175" class="Bound">A</a>
  <a id="IO.return"></a><a id="1242" href="IO.Base.html#1242" class="InductiveConstructor">return</a> <a id="1249" class="Symbol">:</a> <a id="1251" class="Symbol">(</a><a id="1252" href="IO.Base.html#1252" class="Bound">x</a> <a id="1254" class="Symbol">:</a> <a id="1256" href="IO.Base.html#1175" class="Bound">A</a><a id="1257" class="Symbol">)</a> <a id="1259" class="Symbol">→</a> <a id="1261" href="IO.Base.html#1171" class="Datatype">IO</a> <a id="1264" href="IO.Base.html#1175" class="Bound">A</a>
  <a id="IO.bind"></a><a id="1268" href="IO.Base.html#1268" class="InductiveConstructor">bind</a>   <a id="1275" class="Symbol">:</a> <a id="1277" class="Symbol">{</a><a id="1278" href="IO.Base.html#1278" class="Bound">B</a> <a id="1280" class="Symbol">:</a> <a id="1282" href="Agda.Primitive.html#311" class="Primitive">Set</a> <a id="1286" href="IO.Base.html#1183" class="Bound">a</a><a id="1287" class="Symbol">}</a> <a id="1289" class="Symbol">(</a><a id="1290" href="IO.Base.html#1290" class="Bound">m</a> <a id="1292" class="Symbol">:</a> <a id="1294" href="Agda.Builtin.Coinduction.html#170" class="Record">∞</a> <a id="1296" class="Symbol">(</a><a id="1297" href="IO.Base.html#1171" class="Datatype">IO</a> <a id="1300" href="IO.Base.html#1278" class="Bound">B</a><a id="1301" class="Symbol">))</a> <a id="1304" class="Symbol">(</a><a id="1305" href="IO.Base.html#1305" class="Bound">f</a> <a id="1307" class="Symbol">:</a> <a id="1309" class="Symbol">(</a><a id="1310" href="IO.Base.html#1310" class="Bound">x</a> <a id="1312" class="Symbol">:</a> <a id="1314" href="IO.Base.html#1278" class="Bound">B</a><a id="1315" class="Symbol">)</a> <a id="1317" class="Symbol">→</a> <a id="1319" href="Agda.Builtin.Coinduction.html#170" class="Record">∞</a> <a id="1321" class="Symbol">(</a><a id="1322" href="IO.Base.html#1171" class="Datatype">IO</a> <a id="1325" href="IO.Base.html#1175" class="Bound">A</a><a id="1326" class="Symbol">))</a> <a id="1329" class="Symbol">→</a> <a id="1331" href="IO.Base.html#1171" class="Datatype">IO</a> <a id="1334" href="IO.Base.html#1175" class="Bound">A</a>
  <a id="IO.seq"></a><a id="1338" href="IO.Base.html#1338" class="InductiveConstructor">seq</a>    <a id="1345" class="Symbol">:</a> <a id="1347" class="Symbol">{</a><a id="1348" href="IO.Base.html#1348" class="Bound">B</a> <a id="1350" class="Symbol">:</a> <a id="1352" href="Agda.Primitive.html#311" class="Primitive">Set</a> <a id="1356" href="IO.Base.html#1183" class="Bound">a</a><a id="1357" class="Symbol">}</a> <a id="1359" class="Symbol">(</a><a id="1360" href="IO.Base.html#1360" class="Bound">m₁</a> <a id="1363" class="Symbol">:</a> <a id="1365" href="Agda.Builtin.Coinduction.html#170" class="Record">∞</a> <a id="1367" class="Symbol">(</a><a id="1368" href="IO.Base.html#1171" class="Datatype">IO</a> <a id="1371" href="IO.Base.html#1348" class="Bound">B</a><a id="1372" class="Symbol">))</a> <a id="1375" class="Symbol">(</a><a id="1376" href="IO.Base.html#1376" class="Bound">m₂</a> <a id="1379" class="Symbol">:</a> <a id="1381" href="Agda.Builtin.Coinduction.html#170" class="Record">∞</a> <a id="1383" class="Symbol">(</a><a id="1384" href="IO.Base.html#1171" class="Datatype">IO</a> <a id="1387" href="IO.Base.html#1175" class="Bound">A</a><a id="1388" class="Symbol">))</a> <a id="1391" class="Symbol">→</a> <a id="1393" href="IO.Base.html#1171" class="Datatype">IO</a> <a id="1396" href="IO.Base.html#1175" class="Bound">A</a>

<a id="pure"></a><a id="1399" href="IO.Base.html#1399" class="Function">pure</a> <a id="1404" class="Symbol">:</a> <a id="1406" href="IO.Base.html#693" class="Generalizable">A</a> <a id="1408" class="Symbol">→</a> <a id="1410" href="IO.Base.html#1171" class="Datatype">IO</a> <a id="1413" href="IO.Base.html#693" class="Generalizable">A</a>
<a id="1415" href="IO.Base.html#1399" class="Function">pure</a> <a id="1420" class="Symbol">=</a> <a id="1422" href="IO.Base.html#1242" class="InductiveConstructor">return</a>

<a id="lift!"></a><a id="1430" href="IO.Base.html#1430" class="Function">lift!</a> <a id="1436" class="Symbol">:</a> <a id="1438" href="IO.Base.html#1171" class="Datatype">IO</a> <a id="1441" href="IO.Base.html#693" class="Generalizable">A</a> <a id="1443" class="Symbol">→</a> <a id="1445" href="IO.Base.html#1171" class="Datatype">IO</a> <a id="1448" class="Symbol">(</a><a id="1449" href="Level.html#400" class="Record">Lift</a> <a id="1454" href="IO.Base.html#679" class="Generalizable">b</a> <a id="1456" href="IO.Base.html#693" class="Generalizable">A</a><a id="1457" class="Symbol">)</a>
<a id="1459" href="IO.Base.html#1430" class="Function">lift!</a>         <a id="1473" class="Symbol">(</a><a id="1474" href="IO.Base.html#1208" class="InductiveConstructor">lift</a> <a id="1479" href="IO.Base.html#1479" class="Bound">io</a><a id="1481" class="Symbol">)</a>   <a id="1485" class="Symbol">=</a> <a id="1487" href="IO.Base.html#1208" class="InductiveConstructor">lift</a> <a id="1492" class="Symbol">(</a><a id="1493" href="IO.Base.html#1479" class="Bound">io</a> <a id="1496" href="IO.Primitive.html#601" class="Postulate Operator">Prim.&gt;&gt;=</a> <a id="1505" class="Symbol">λ</a> <a id="1507" href="IO.Base.html#1507" class="Bound">a</a> <a id="1509" class="Symbol">→</a> <a id="1511" href="IO.Primitive.html#561" class="Postulate">Prim.return</a> <a id="1523" class="Symbol">(</a><a id="1524" href="Level.html#457" class="InductiveConstructor">Level.lift</a> <a id="1535" href="IO.Base.html#1507" class="Bound">a</a><a id="1536" class="Symbol">))</a>
<a id="1539" href="IO.Base.html#1430" class="Function">lift!</a>         <a id="1553" class="Symbol">(</a><a id="1554" href="IO.Base.html#1242" class="InductiveConstructor">return</a> <a id="1561" href="IO.Base.html#1561" class="Bound">a</a><a id="1562" class="Symbol">)</a>  <a id="1565" class="Symbol">=</a> <a id="1567" href="IO.Base.html#1242" class="InductiveConstructor">return</a> <a id="1574" class="Symbol">(</a><a id="1575" href="Level.html#457" class="InductiveConstructor">Level.lift</a> <a id="1586" href="IO.Base.html#1561" class="Bound">a</a><a id="1587" class="Symbol">)</a>
<a id="1589" href="IO.Base.html#1430" class="Function">lift!</a> <a id="1595" class="Symbol">{</a><a id="1596" class="Argument">b</a> <a id="1598" class="Symbol">=</a> <a id="1600" href="IO.Base.html#1600" class="Bound">b</a><a id="1601" class="Symbol">}</a> <a id="1603" class="Symbol">(</a><a id="1604" href="IO.Base.html#1268" class="InductiveConstructor">bind</a> <a id="1609" href="IO.Base.html#1609" class="Bound">m</a> <a id="1611" href="IO.Base.html#1611" class="Bound">f</a><a id="1612" class="Symbol">)</a>  <a id="1615" class="Symbol">=</a> <a id="1617" href="IO.Base.html#1268" class="InductiveConstructor">bind</a> <a id="1622" class="Symbol">(</a><a id="1623" href="Agda.Builtin.Coinduction.html#203" class="CoinductiveConstructor Operator">♯</a> <a id="1625" href="IO.Base.html#1430" class="Function">lift!</a> <a id="1631" class="Symbol">{</a><a id="1632" class="Argument">b</a> <a id="1634" class="Symbol">=</a> <a id="1636" href="IO.Base.html#1600" class="Bound">b</a><a id="1637" class="Symbol">}</a> <a id="1639" class="Symbol">(</a><a id="1640" href="Agda.Builtin.Coinduction.html#238" class="Field">♭</a> <a id="1642" href="IO.Base.html#1609" class="Bound">m</a><a id="1643" class="Symbol">))</a>
                                 <a id="1679" class="Symbol">(λ</a> <a id="1682" href="IO.Base.html#1682" class="Bound">x</a> <a id="1684" class="Symbol">→</a> <a id="1686" href="Agda.Builtin.Coinduction.html#203" class="CoinductiveConstructor Operator">♯</a> <a id="1688" href="IO.Base.html#1430" class="Function">lift!</a> <a id="1694" class="Symbol">(</a><a id="1695" href="Agda.Builtin.Coinduction.html#238" class="Field">♭</a> <a id="1697" class="Symbol">(</a><a id="1698" href="IO.Base.html#1611" class="Bound">f</a> <a id="1700" class="Symbol">(</a><a id="1701" href="Level.html#470" class="Field">lower</a> <a id="1707" href="IO.Base.html#1682" class="Bound">x</a><a id="1708" class="Symbol">))))</a>
<a id="1713" href="IO.Base.html#1430" class="Function">lift!</a> <a id="1719" class="Symbol">{</a><a id="1720" class="Argument">b</a> <a id="1722" class="Symbol">=</a> <a id="1724" href="IO.Base.html#1724" class="Bound">b</a><a id="1725" class="Symbol">}</a> <a id="1727" class="Symbol">(</a><a id="1728" href="IO.Base.html#1338" class="InductiveConstructor">seq</a> <a id="1732" href="IO.Base.html#1732" class="Bound">m₁</a> <a id="1735" href="IO.Base.html#1735" class="Bound">m₂</a><a id="1737" class="Symbol">)</a> <a id="1739" class="Symbol">=</a> <a id="1741" href="IO.Base.html#1338" class="InductiveConstructor">seq</a> <a id="1745" class="Symbol">(</a><a id="1746" href="Agda.Builtin.Coinduction.html#203" class="CoinductiveConstructor Operator">♯</a> <a id="1748" href="IO.Base.html#1430" class="Function">lift!</a> <a id="1754" class="Symbol">{</a><a id="1755" class="Argument">b</a> <a id="1757" class="Symbol">=</a> <a id="1759" href="IO.Base.html#1724" class="Bound">b</a><a id="1760" class="Symbol">}</a> <a id="1762" class="Symbol">(</a><a id="1763" href="Agda.Builtin.Coinduction.html#238" class="Field">♭</a> <a id="1765" href="IO.Base.html#1732" class="Bound">m₁</a><a id="1767" class="Symbol">))</a>
                                <a id="1802" class="Symbol">(</a><a id="1803" href="Agda.Builtin.Coinduction.html#203" class="CoinductiveConstructor Operator">♯</a> <a id="1805" href="IO.Base.html#1430" class="Function">lift!</a> <a id="1811" class="Symbol">(</a><a id="1812" href="Agda.Builtin.Coinduction.html#238" class="Field">♭</a> <a id="1814" href="IO.Base.html#1735" class="Bound">m₂</a><a id="1816" class="Symbol">))</a>

<a id="1820" class="Keyword">module</a> <a id="1827" href="IO.Base.html#1827" class="Module">_</a> <a id="1829" class="Symbol">{</a><a id="1830" href="IO.Base.html#1830" class="Bound">A</a> <a id="1832" href="IO.Base.html#1832" class="Bound">B</a> <a id="1834" class="Symbol">:</a> <a id="1836" href="Agda.Primitive.html#311" class="Primitive">Set</a> <a id="1840" href="IO.Base.html#677" class="Generalizable">a</a><a id="1841" class="Symbol">}</a> <a id="1843" class="Keyword">where</a>

  <a id="1852" class="Keyword">infixl</a> <a id="1859" class="Number">1</a> <a id="1861" href="IO.Base.html#2007" class="Function Operator">_&lt;$&gt;_</a> <a id="1867" href="IO.Base.html#1904" class="Function Operator">_&lt;*&gt;_</a> <a id="1873" href="IO.Base.html#2118" class="Function Operator">_&gt;&gt;=_</a> <a id="1879" href="IO.Base.html#2246" class="Function Operator">_&gt;&gt;_</a>
  <a id="1886" class="Keyword">infixr</a> <a id="1893" class="Number">1</a> <a id="1895" href="IO.Base.html#2189" class="Function Operator">_=&lt;&lt;_</a>

  <a id="1904" href="IO.Base.html#1904" class="Function Operator">_&lt;*&gt;_</a> <a id="1910" class="Symbol">:</a> <a id="1912" href="IO.Base.html#1171" class="Datatype">IO</a> <a id="1915" class="Symbol">(</a><a id="1916" href="IO.Base.html#1830" class="Bound">A</a> <a id="1918" class="Symbol">→</a> <a id="1920" href="IO.Base.html#1832" class="Bound">B</a><a id="1921" class="Symbol">)</a> <a id="1923" class="Symbol">→</a> <a id="1925" href="IO.Base.html#1171" class="Datatype">IO</a> <a id="1928" href="IO.Base.html#1830" class="Bound">A</a> <a id="1930" class="Symbol">→</a> <a id="1932" href="IO.Base.html#1171" class="Datatype">IO</a> <a id="1935" href="IO.Base.html#1832" class="Bound">B</a>
  <a id="1939" href="IO.Base.html#1939" class="Bound">mf</a> <a id="1942" href="IO.Base.html#1904" class="Function Operator">&lt;*&gt;</a> <a id="1946" href="IO.Base.html#1946" class="Bound">mx</a> <a id="1949" class="Symbol">=</a> <a id="1951" href="IO.Base.html#1268" class="InductiveConstructor">bind</a> <a id="1956" class="Symbol">(</a><a id="1957" href="Agda.Builtin.Coinduction.html#203" class="CoinductiveConstructor Operator">♯</a> <a id="1959" href="IO.Base.html#1939" class="Bound">mf</a><a id="1961" class="Symbol">)</a> <a id="1963" class="Symbol">λ</a> <a id="1965" href="IO.Base.html#1965" class="Bound">f</a> <a id="1967" class="Symbol">→</a> <a id="1969" href="Agda.Builtin.Coinduction.html#203" class="CoinductiveConstructor Operator">♯</a> <a id="1971" class="Symbol">(</a><a id="1972" href="IO.Base.html#1268" class="InductiveConstructor">bind</a> <a id="1977" class="Symbol">(</a><a id="1978" href="Agda.Builtin.Coinduction.html#203" class="CoinductiveConstructor Operator">♯</a> <a id="1980" href="IO.Base.html#1946" class="Bound">mx</a><a id="1982" class="Symbol">)</a> <a id="1984" class="Symbol">λ</a> <a id="1986" href="IO.Base.html#1986" class="Bound">x</a> <a id="1988" class="Symbol">→</a> <a id="1990" href="Agda.Builtin.Coinduction.html#203" class="CoinductiveConstructor Operator">♯</a> <a id="1992" href="IO.Base.html#1399" class="Function">pure</a> <a id="1997" class="Symbol">(</a><a id="1998" href="IO.Base.html#1965" class="Bound">f</a> <a id="2000" href="IO.Base.html#1986" class="Bound">x</a><a id="2001" class="Symbol">))</a>

  <a id="2007" href="IO.Base.html#2007" class="Function Operator">_&lt;$&gt;_</a> <a id="2013" class="Symbol">:</a> <a id="2015" class="Symbol">(</a><a id="2016" href="IO.Base.html#1830" class="Bound">A</a> <a id="2018" class="Symbol">→</a> <a id="2020" href="IO.Base.html#1832" class="Bound">B</a><a id="2021" class="Symbol">)</a> <a id="2023" class="Symbol">→</a> <a id="2025" href="IO.Base.html#1171" class="Datatype">IO</a> <a id="2028" href="IO.Base.html#1830" class="Bound">A</a> <a id="2030" class="Symbol">→</a> <a id="2032" href="IO.Base.html#1171" class="Datatype">IO</a> <a id="2035" href="IO.Base.html#1832" class="Bound">B</a>
  <a id="2039" href="IO.Base.html#2039" class="Bound">f</a> <a id="2041" href="IO.Base.html#2007" class="Function Operator">&lt;$&gt;</a> <a id="2045" href="IO.Base.html#2045" class="Bound">m</a> <a id="2047" class="Symbol">=</a> <a id="2049" href="IO.Base.html#1399" class="Function">pure</a> <a id="2054" href="IO.Base.html#2039" class="Bound">f</a> <a id="2056" href="IO.Base.html#1904" class="Function Operator">&lt;*&gt;</a> <a id="2060" href="IO.Base.html#2045" class="Bound">m</a>

  <a id="2065" href="IO.Base.html#2065" class="Function Operator">_&lt;$_</a> <a id="2070" class="Symbol">:</a> <a id="2072" href="IO.Base.html#1832" class="Bound">B</a> <a id="2074" class="Symbol">→</a> <a id="2076" href="IO.Base.html#1171" class="Datatype">IO</a> <a id="2079" href="IO.Base.html#1830" class="Bound">A</a> <a id="2081" class="Symbol">→</a> <a id="2083" href="IO.Base.html#1171" class="Datatype">IO</a> <a id="2086" href="IO.Base.html#1832" class="Bound">B</a>
  <a id="2090" href="IO.Base.html#2090" class="Bound">b</a> <a id="2092" href="IO.Base.html#2065" class="Function Operator">&lt;$</a> <a id="2095" href="IO.Base.html#2095" class="Bound">m</a> <a id="2097" class="Symbol">=</a> <a id="2099" class="Symbol">(</a><a id="2100" href="Function.Base.html#716" class="Function">const</a> <a id="2106" href="IO.Base.html#2090" class="Bound">b</a><a id="2107" class="Symbol">)</a> <a id="2109" href="IO.Base.html#2007" class="Function Operator">&lt;$&gt;</a> <a id="2113" href="IO.Base.html#2095" class="Bound">m</a>

  <a id="2118" href="IO.Base.html#2118" class="Function Operator">_&gt;&gt;=_</a> <a id="2124" class="Symbol">:</a> <a id="2126" href="IO.Base.html#1171" class="Datatype">IO</a> <a id="2129" href="IO.Base.html#1830" class="Bound">A</a> <a id="2131" class="Symbol">→</a> <a id="2133" class="Symbol">(</a><a id="2134" href="IO.Base.html#1830" class="Bound">A</a> <a id="2136" class="Symbol">→</a> <a id="2138" href="IO.Base.html#1171" class="Datatype">IO</a> <a id="2141" href="IO.Base.html#1832" class="Bound">B</a><a id="2142" class="Symbol">)</a> <a id="2144" class="Symbol">→</a> <a id="2146" href="IO.Base.html#1171" class="Datatype">IO</a> <a id="2149" href="IO.Base.html#1832" class="Bound">B</a>
  <a id="2153" href="IO.Base.html#2153" class="Bound">m</a> <a id="2155" href="IO.Base.html#2118" class="Function Operator">&gt;&gt;=</a> <a id="2159" href="IO.Base.html#2159" class="Bound">f</a> <a id="2161" class="Symbol">=</a> <a id="2163" href="IO.Base.html#1268" class="InductiveConstructor">bind</a> <a id="2168" class="Symbol">(</a><a id="2169" href="Agda.Builtin.Coinduction.html#203" class="CoinductiveConstructor Operator">♯</a> <a id="2171" href="IO.Base.html#2153" class="Bound">m</a><a id="2172" class="Symbol">)</a> <a id="2174" class="Symbol">λ</a> <a id="2176" href="IO.Base.html#2176" class="Bound">x</a> <a id="2178" class="Symbol">→</a> <a id="2180" href="Agda.Builtin.Coinduction.html#203" class="CoinductiveConstructor Operator">♯</a> <a id="2182" href="IO.Base.html#2159" class="Bound">f</a> <a id="2184" href="IO.Base.html#2176" class="Bound">x</a>

  <a id="2189" href="IO.Base.html#2189" class="Function Operator">_=&lt;&lt;_</a> <a id="2195" class="Symbol">:</a> <a id="2197" class="Symbol">(</a><a id="2198" href="IO.Base.html#1830" class="Bound">A</a> <a id="2200" class="Symbol">→</a> <a id="2202" href="IO.Base.html#1171" class="Datatype">IO</a> <a id="2205" href="IO.Base.html#1832" class="Bound">B</a><a id="2206" class="Symbol">)</a> <a id="2208" class="Symbol">→</a> <a id="2210" href="IO.Base.html#1171" class="Datatype">IO</a> <a id="2213" href="IO.Base.html#1830" class="Bound">A</a> <a id="2215" class="Symbol">→</a> <a id="2217" href="IO.Base.html#1171" class="Datatype">IO</a> <a id="2220" href="IO.Base.html#1832" class="Bound">B</a>
  <a id="2224" href="IO.Base.html#2189" class="Function Operator">_=&lt;&lt;_</a> <a id="2230" class="Symbol">=</a> <a id="2232" href="Function.Base.html#1629" class="Function">flip</a> <a id="2237" href="IO.Base.html#2118" class="Function Operator">_&gt;&gt;=_</a>

  <a id="2246" href="IO.Base.html#2246" class="Function Operator">_&gt;&gt;_</a> <a id="2251" class="Symbol">:</a> <a id="2253" href="IO.Base.html#1171" class="Datatype">IO</a> <a id="2256" href="IO.Base.html#1830" class="Bound">A</a> <a id="2258" class="Symbol">→</a> <a id="2260" href="IO.Base.html#1171" class="Datatype">IO</a> <a id="2263" href="IO.Base.html#1832" class="Bound">B</a> <a id="2265" class="Symbol">→</a> <a id="2267" href="IO.Base.html#1171" class="Datatype">IO</a> <a id="2270" href="IO.Base.html#1832" class="Bound">B</a>
  <a id="2274" href="IO.Base.html#2274" class="Bound">m₁</a> <a id="2277" href="IO.Base.html#2246" class="Function Operator">&gt;&gt;</a> <a id="2280" href="IO.Base.html#2280" class="Bound">m₂</a> <a id="2283" class="Symbol">=</a> <a id="2285" href="IO.Base.html#1338" class="InductiveConstructor">seq</a> <a id="2289" class="Symbol">(</a><a id="2290" href="Agda.Builtin.Coinduction.html#203" class="CoinductiveConstructor Operator">♯</a> <a id="2292" href="IO.Base.html#2274" class="Bound">m₁</a><a id="2294" class="Symbol">)</a> <a id="2296" class="Symbol">(</a><a id="2297" href="Agda.Builtin.Coinduction.html#203" class="CoinductiveConstructor Operator">♯</a> <a id="2299" href="IO.Base.html#2280" class="Bound">m₂</a><a id="2301" class="Symbol">)</a>

  <a id="2306" href="IO.Base.html#2306" class="Function Operator">_&lt;&lt;_</a> <a id="2311" class="Symbol">:</a> <a id="2313" href="IO.Base.html#1171" class="Datatype">IO</a> <a id="2316" href="IO.Base.html#1832" class="Bound">B</a> <a id="2318" class="Symbol">→</a> <a id="2320" href="IO.Base.html#1171" class="Datatype">IO</a> <a id="2323" href="IO.Base.html#1830" class="Bound">A</a> <a id="2325" class="Symbol">→</a> <a id="2327" href="IO.Base.html#1171" class="Datatype">IO</a> <a id="2330" href="IO.Base.html#1832" class="Bound">B</a>
  <a id="2334" href="IO.Base.html#2306" class="Function Operator">_&lt;&lt;_</a> <a id="2339" class="Symbol">=</a> <a id="2341" href="Function.Base.html#1629" class="Function">flip</a> <a id="2346" href="IO.Base.html#2246" class="Function Operator">_&gt;&gt;_</a>

<a id="2352" class="Comment">------------------------------------------------------------------------</a>
<a id="2425" class="Comment">-- Running programs</a>

<a id="2446" class="Comment">-- A value of type `IO A` is a description of a computation that may</a>
<a id="2515" class="Comment">-- eventually produce an `A`. The `run` function converts this description</a>
<a id="2590" class="Comment">-- of a computation into calls to primitive functions that will actually</a>
<a id="2663" class="Comment">-- perform it.</a>

<a id="2679" class="Symbol">{-#</a> <a id="2683" class="Keyword">NON_TERMINATING</a> <a id="2699" class="Symbol">#-}</a>
<a id="run"></a><a id="2703" href="IO.Base.html#2703" class="Function">run</a> <a id="2707" class="Symbol">:</a> <a id="2709" href="IO.Base.html#1171" class="Datatype">IO</a> <a id="2712" href="IO.Base.html#693" class="Generalizable">A</a> <a id="2714" class="Symbol">→</a> <a id="2716" href="Agda.Builtin.IO.html#111" class="Postulate">Prim.IO</a> <a id="2724" href="IO.Base.html#693" class="Generalizable">A</a>
<a id="2726" href="IO.Base.html#2703" class="Function">run</a> <a id="2730" class="Symbol">(</a><a id="2731" href="IO.Base.html#1208" class="InductiveConstructor">lift</a> <a id="2736" href="IO.Base.html#2736" class="Bound">m</a><a id="2737" class="Symbol">)</a>    <a id="2742" class="Symbol">=</a> <a id="2744" href="IO.Base.html#2736" class="Bound">m</a>
<a id="2746" href="IO.Base.html#2703" class="Function">run</a> <a id="2750" class="Symbol">(</a><a id="2751" href="IO.Base.html#1242" class="InductiveConstructor">return</a> <a id="2758" href="IO.Base.html#2758" class="Bound">x</a><a id="2759" class="Symbol">)</a>  <a id="2762" class="Symbol">=</a> <a id="2764" href="IO.Primitive.html#561" class="Postulate">Prim.return</a> <a id="2776" href="IO.Base.html#2758" class="Bound">x</a>
<a id="2778" href="IO.Base.html#2703" class="Function">run</a> <a id="2782" class="Symbol">(</a><a id="2783" href="IO.Base.html#1268" class="InductiveConstructor">bind</a> <a id="2788" href="IO.Base.html#2788" class="Bound">m</a> <a id="2790" href="IO.Base.html#2790" class="Bound">f</a><a id="2791" class="Symbol">)</a>  <a id="2794" class="Symbol">=</a> <a id="2796" href="IO.Base.html#2703" class="Function">run</a> <a id="2800" class="Symbol">(</a><a id="2801" href="Agda.Builtin.Coinduction.html#238" class="Field">♭</a> <a id="2803" href="IO.Base.html#2788" class="Bound">m</a> <a id="2805" class="Symbol">)</a> <a id="2807" href="IO.Primitive.html#601" class="Postulate Operator">Prim.&gt;&gt;=</a> <a id="2816" class="Symbol">λ</a> <a id="2818" href="IO.Base.html#2818" class="Bound">x</a> <a id="2820" class="Symbol">→</a> <a id="2822" href="IO.Base.html#2703" class="Function">run</a> <a id="2826" class="Symbol">(</a><a id="2827" href="Agda.Builtin.Coinduction.html#238" class="Field">♭</a> <a id="2829" class="Symbol">(</a><a id="2830" href="IO.Base.html#2790" class="Bound">f</a> <a id="2832" href="IO.Base.html#2818" class="Bound">x</a><a id="2833" class="Symbol">))</a>
<a id="2836" href="IO.Base.html#2703" class="Function">run</a> <a id="2840" class="Symbol">(</a><a id="2841" href="IO.Base.html#1338" class="InductiveConstructor">seq</a> <a id="2845" href="IO.Base.html#2845" class="Bound">m₁</a> <a id="2848" href="IO.Base.html#2848" class="Bound">m₂</a><a id="2850" class="Symbol">)</a> <a id="2852" class="Symbol">=</a> <a id="2854" href="IO.Base.html#2703" class="Function">run</a> <a id="2858" class="Symbol">(</a><a id="2859" href="Agda.Builtin.Coinduction.html#238" class="Field">♭</a> <a id="2861" href="IO.Base.html#2845" class="Bound">m₁</a><a id="2863" class="Symbol">)</a> <a id="2865" href="IO.Primitive.html#601" class="Postulate Operator">Prim.&gt;&gt;=</a> <a id="2874" class="Symbol">λ</a> <a id="2876" href="IO.Base.html#2876" class="Bound">_</a> <a id="2878" class="Symbol">→</a> <a id="2880" href="IO.Base.html#2703" class="Function">run</a> <a id="2884" class="Symbol">(</a><a id="2885" href="Agda.Builtin.Coinduction.html#238" class="Field">♭</a> <a id="2887" href="IO.Base.html#2848" class="Bound">m₂</a><a id="2889" class="Symbol">)</a>

<a id="2892" class="Comment">-- The entrypoint of an Agda program will be assigned type `Main` and</a>
<a id="2962" class="Comment">-- implemented using `run` on some `IO ⊤` program.</a>

<a id="Main"></a><a id="3014" href="IO.Base.html#3014" class="Function">Main</a> <a id="3019" class="Symbol">:</a> <a id="3021" href="Agda.Primitive.html#311" class="Primitive">Set</a>
<a id="3025" href="IO.Base.html#3014" class="Function">Main</a> <a id="3030" class="Symbol">=</a> <a id="3032" href="Agda.Builtin.IO.html#111" class="Postulate">Prim.IO</a> <a id="3040" class="Symbol">{</a><a id="3041" href="Level.html#512" class="Function">0ℓ</a><a id="3043" class="Symbol">}</a> <a id="3045" href="Data.Unit.Polymorphic.Base.html#480" class="Function">⊤</a>

<a id="3048" class="Comment">------------------------------------------------------------------------</a>
<a id="3121" class="Comment">-- Utilities</a>

<a id="3135" class="Comment">-- Make a unit-returning primitive level polymorphic</a>
<a id="lift′"></a><a id="3188" href="IO.Base.html#3188" class="Function">lift′</a> <a id="3194" class="Symbol">:</a> <a id="3196" href="Agda.Builtin.IO.html#111" class="Postulate">Prim.IO</a> <a id="3204" href="Agda.Builtin.Unit.html#149" class="Record">Unit0.⊤</a> <a id="3212" class="Symbol">→</a> <a id="3214" href="IO.Base.html#1171" class="Datatype">IO</a> <a id="3217" class="Symbol">{</a><a id="3218" href="IO.Base.html#677" class="Generalizable">a</a><a id="3219" class="Symbol">}</a> <a id="3221" href="Data.Unit.Polymorphic.Base.html#480" class="Function">⊤</a>
<a id="3223" href="IO.Base.html#3188" class="Function">lift′</a> <a id="3229" href="IO.Base.html#3229" class="Bound">io</a> <a id="3232" class="Symbol">=</a> <a id="3234" href="IO.Base.html#1208" class="InductiveConstructor">lift</a> <a id="3239" class="Symbol">(</a><a id="3240" href="IO.Base.html#3229" class="Bound">io</a> <a id="3243" href="IO.Primitive.html#601" class="Postulate Operator">Prim.&gt;&gt;=</a> <a id="3252" class="Symbol">λ</a> <a id="3254" href="IO.Base.html#3254" class="Bound">_</a> <a id="3256" class="Symbol">→</a> <a id="3258" href="IO.Primitive.html#561" class="Postulate">Prim.return</a> <a id="3270" class="Symbol">_)</a>

<a id="3274" class="Comment">-- Throw away the result</a>
<a id="ignore"></a><a id="3299" href="IO.Base.html#3299" class="Function">ignore</a> <a id="3306" class="Symbol">:</a> <a id="3308" href="IO.Base.html#1171" class="Datatype">IO</a> <a id="3311" href="IO.Base.html#693" class="Generalizable">A</a> <a id="3313" class="Symbol">→</a> <a id="3315" href="IO.Base.html#1171" class="Datatype">IO</a> <a id="3318" href="Data.Unit.Polymorphic.Base.html#480" class="Function">⊤</a>
<a id="3320" href="IO.Base.html#3299" class="Function">ignore</a> <a id="3327" href="IO.Base.html#3327" class="Bound">io</a> <a id="3330" class="Symbol">=</a> <a id="3332" href="IO.Base.html#3327" class="Bound">io</a> <a id="3335" href="IO.Base.html#2246" class="Function Operator">&gt;&gt;</a> <a id="3338" href="IO.Base.html#1242" class="InductiveConstructor">return</a> <a id="3345" class="Symbol">_</a>

<a id="3348" class="Comment">-- Conditional executions</a>
<a id="when"></a><a id="3374" href="IO.Base.html#3374" class="Function">when</a> <a id="3379" class="Symbol">:</a> <a id="3381" href="Agda.Builtin.Bool.html#147" class="Datatype">Bool</a> <a id="3386" class="Symbol">→</a> <a id="3388" href="IO.Base.html#1171" class="Datatype">IO</a> <a id="3391" class="Symbol">{</a><a id="3392" href="IO.Base.html#677" class="Generalizable">a</a><a id="3393" class="Symbol">}</a> <a id="3395" href="Data.Unit.Polymorphic.Base.html#480" class="Function">⊤</a> <a id="3397" class="Symbol">→</a> <a id="3399" href="IO.Base.html#1171" class="Datatype">IO</a> <a id="3402" href="Data.Unit.Polymorphic.Base.html#480" class="Function">⊤</a>
<a id="3404" href="IO.Base.html#3374" class="Function">when</a> <a id="3409" href="Agda.Builtin.Bool.html#172" class="InductiveConstructor">true</a> <a id="3414" href="IO.Base.html#3414" class="Bound">m</a> <a id="3416" class="Symbol">=</a> <a id="3418" href="IO.Base.html#3414" class="Bound">m</a>
<a id="3420" href="IO.Base.html#3374" class="Function">when</a> <a id="3425" href="Agda.Builtin.Bool.html#166" class="InductiveConstructor">false</a> <a id="3431" class="Symbol">_</a> <a id="3433" class="Symbol">=</a> <a id="3435" href="IO.Base.html#1399" class="Function">pure</a> <a id="3440" class="Symbol">_</a>

<a id="unless"></a><a id="3443" href="IO.Base.html#3443" class="Function">unless</a> <a id="3450" class="Symbol">:</a> <a id="3452" href="Agda.Builtin.Bool.html#147" class="Datatype">Bool</a> <a id="3457" class="Symbol">→</a> <a id="3459" href="IO.Base.html#1171" class="Datatype">IO</a> <a id="3462" class="Symbol">{</a><a id="3463" href="IO.Base.html#677" class="Generalizable">a</a><a id="3464" class="Symbol">}</a> <a id="3466" href="Data.Unit.Polymorphic.Base.html#480" class="Function">⊤</a> <a id="3468" class="Symbol">→</a> <a id="3470" href="IO.Base.html#1171" class="Datatype">IO</a> <a id="3473" href="Data.Unit.Polymorphic.Base.html#480" class="Function">⊤</a>
<a id="3475" href="IO.Base.html#3443" class="Function">unless</a> <a id="3482" class="Symbol">=</a> <a id="3484" href="IO.Base.html#3374" class="Function">when</a> <a id="3489" href="Function.Base.html#3637" class="Function Operator">∘′</a> <a id="3492" href="Data.Bool.Base.html#932" class="Function">not</a>

<a id="whenJust"></a><a id="3497" href="IO.Base.html#3497" class="Function">whenJust</a> <a id="3506" class="Symbol">:</a> <a id="3508" href="Agda.Builtin.Maybe.html#109" class="Datatype">Maybe</a> <a id="3514" href="IO.Base.html#693" class="Generalizable">A</a> <a id="3516" class="Symbol">→</a> <a id="3518" class="Symbol">(</a><a id="3519" href="IO.Base.html#693" class="Generalizable">A</a> <a id="3521" class="Symbol">→</a> <a id="3523" href="IO.Base.html#1171" class="Datatype">IO</a> <a id="3526" class="Symbol">{</a><a id="3527" href="IO.Base.html#677" class="Generalizable">a</a><a id="3528" class="Symbol">}</a> <a id="3530" href="Data.Unit.Polymorphic.Base.html#480" class="Function">⊤</a><a id="3531" class="Symbol">)</a> <a id="3533" class="Symbol">→</a> <a id="3535" href="IO.Base.html#1171" class="Datatype">IO</a> <a id="3538" href="Data.Unit.Polymorphic.Base.html#480" class="Function">⊤</a>
<a id="3540" href="IO.Base.html#3497" class="Function">whenJust</a> <a id="3549" class="Symbol">(</a><a id="3550" href="Agda.Builtin.Maybe.html#147" class="InductiveConstructor">just</a> <a id="3555" href="IO.Base.html#3555" class="Bound">a</a><a id="3556" class="Symbol">)</a> <a id="3558" href="IO.Base.html#3558" class="Bound">k</a> <a id="3560" class="Symbol">=</a> <a id="3562" href="IO.Base.html#3558" class="Bound">k</a> <a id="3564" href="IO.Base.html#3555" class="Bound">a</a>
<a id="3566" href="IO.Base.html#3497" class="Function">whenJust</a> <a id="3575" href="Agda.Builtin.Maybe.html#168" class="InductiveConstructor">nothing</a>  <a id="3584" class="Symbol">_</a> <a id="3586" class="Symbol">=</a> <a id="3588" href="IO.Base.html#1399" class="Function">pure</a> <a id="3593" class="Symbol">_</a>

<a id="3596" class="Comment">-- Keep running an IO action until we get a value. Convenient when user</a>
<a id="3668" class="Comment">-- input is involved and it may be malformed.</a>
<a id="untilJust"></a><a id="3714" href="IO.Base.html#3714" class="Function">untilJust</a> <a id="3724" class="Symbol">:</a> <a id="3726" href="IO.Base.html#1171" class="Datatype">IO</a> <a id="3729" class="Symbol">(</a><a id="3730" href="Agda.Builtin.Maybe.html#109" class="Datatype">Maybe</a> <a id="3736" href="IO.Base.html#693" class="Generalizable">A</a><a id="3737" class="Symbol">)</a> <a id="3739" class="Symbol">→</a> <a id="3741" href="IO.Base.html#1171" class="Datatype">IO</a> <a id="3744" href="IO.Base.html#693" class="Generalizable">A</a>
<a id="3746" class="Comment">-- Note that here we are forced to use `bind` &amp; the musical notation</a>
<a id="3815" class="Comment">-- explicitly to guarantee that the corecursive call is guarded</a>
<a id="3879" href="IO.Base.html#3714" class="Function">untilJust</a> <a id="3889" href="IO.Base.html#3889" class="Bound">m</a> <a id="3891" class="Symbol">=</a> <a id="3893" href="IO.Base.html#1268" class="InductiveConstructor">bind</a> <a id="3898" class="Symbol">(</a><a id="3899" href="Agda.Builtin.Coinduction.html#203" class="CoinductiveConstructor Operator">♯</a> <a id="3901" href="IO.Base.html#3889" class="Bound">m</a><a id="3902" class="Symbol">)</a> <a id="3904" class="Symbol">λ</a> <a id="3906" class="Keyword">where</a>
  <a id="3914" href="Agda.Builtin.Maybe.html#168" class="InductiveConstructor">nothing</a>  <a id="3923" class="Symbol">→</a> <a id="3925" href="Agda.Builtin.Coinduction.html#203" class="CoinductiveConstructor Operator">♯</a> <a id="3927" href="IO.Base.html#3714" class="Function">untilJust</a> <a id="3937" href="IO.Base.html#3889" class="Bound">m</a>
  <a id="3941" class="Symbol">(</a><a id="3942" href="Agda.Builtin.Maybe.html#147" class="InductiveConstructor">just</a> <a id="3947" href="IO.Base.html#3947" class="Bound">a</a><a id="3948" class="Symbol">)</a> <a id="3950" class="Symbol">→</a> <a id="3952" href="Agda.Builtin.Coinduction.html#203" class="CoinductiveConstructor Operator">♯</a> <a id="3954" href="IO.Base.html#1242" class="InductiveConstructor">return</a> <a id="3961" href="IO.Base.html#3947" class="Bound">a</a>

<a id="untilRight"></a><a id="3964" href="IO.Base.html#3964" class="Function">untilRight</a> <a id="3975" class="Symbol">:</a> <a id="3977" class="Symbol">{</a><a id="3978" href="IO.Base.html#3978" class="Bound">A</a> <a id="3980" href="IO.Base.html#3980" class="Bound">B</a> <a id="3982" class="Symbol">:</a> <a id="3984" href="Agda.Primitive.html#311" class="Primitive">Set</a> <a id="3988" href="IO.Base.html#677" class="Generalizable">a</a><a id="3989" class="Symbol">}</a> <a id="3991" class="Symbol">→</a> <a id="3993" class="Symbol">(</a><a id="3994" href="IO.Base.html#3978" class="Bound">A</a> <a id="3996" class="Symbol">→</a> <a id="3998" href="IO.Base.html#1171" class="Datatype">IO</a> <a id="4001" class="Symbol">(</a><a id="4002" href="IO.Base.html#3978" class="Bound">A</a> <a id="4004" href="Data.Sum.Base.html#734" class="Datatype Operator">⊎</a> <a id="4006" href="IO.Base.html#3980" class="Bound">B</a><a id="4007" class="Symbol">))</a> <a id="4010" class="Symbol">→</a> <a id="4012" href="IO.Base.html#3978" class="Bound">A</a> <a id="4014" class="Symbol">→</a> <a id="4016" href="IO.Base.html#1171" class="Datatype">IO</a> <a id="4019" href="IO.Base.html#3980" class="Bound">B</a>
<a id="4021" href="IO.Base.html#3964" class="Function">untilRight</a> <a id="4032" href="IO.Base.html#4032" class="Bound">f</a> <a id="4034" href="IO.Base.html#4034" class="Bound">x</a> <a id="4036" class="Symbol">=</a> <a id="4038" href="IO.Base.html#1268" class="InductiveConstructor">bind</a> <a id="4043" class="Symbol">(</a><a id="4044" href="Agda.Builtin.Coinduction.html#203" class="CoinductiveConstructor Operator">♯</a> <a id="4046" href="IO.Base.html#4032" class="Bound">f</a> <a id="4048" href="IO.Base.html#4034" class="Bound">x</a><a id="4049" class="Symbol">)</a> <a id="4051" class="Symbol">λ</a> <a id="4053" class="Keyword">where</a>
  <a id="4061" class="Symbol">(</a><a id="4062" href="Data.Sum.Base.html#784" class="InductiveConstructor">inj₁</a> <a id="4067" href="IO.Base.html#4067" class="Bound">x′</a><a id="4069" class="Symbol">)</a> <a id="4071" class="Symbol">→</a> <a id="4073" href="Agda.Builtin.Coinduction.html#203" class="CoinductiveConstructor Operator">♯</a> <a id="4075" href="IO.Base.html#3964" class="Function">untilRight</a> <a id="4086" href="IO.Base.html#4032" class="Bound">f</a> <a id="4088" href="IO.Base.html#4067" class="Bound">x′</a>
  <a id="4093" class="Symbol">(</a><a id="4094" href="Data.Sum.Base.html#809" class="InductiveConstructor">inj₂</a> <a id="4099" href="IO.Base.html#4099" class="Bound">y</a><a id="4100" class="Symbol">)</a> <a id="4102" class="Symbol">→</a> <a id="4104" href="Agda.Builtin.Coinduction.html#203" class="CoinductiveConstructor Operator">♯</a> <a id="4106" href="IO.Base.html#1242" class="InductiveConstructor">return</a> <a id="4113" href="IO.Base.html#4099" class="Bound">y</a>
</pre></body></html>