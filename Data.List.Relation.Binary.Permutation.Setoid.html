<!DOCTYPE HTML>
<html><head><meta charset="utf-8"><title>Data.List.Relation.Binary.Permutation.Setoid</title><link rel="stylesheet" href="Agda.css"></head><body><pre class="Agda"><a id="1" class="Comment">------------------------------------------------------------------------</a>
<a id="74" class="Comment">-- The Agda standard library</a>
<a id="103" class="Comment">--</a>
<a id="106" class="Comment">-- A definition for the permutation relation using setoid equality</a>
<a id="173" class="Comment">------------------------------------------------------------------------</a>

<a id="247" class="Symbol">{-#</a> <a id="251" class="Keyword">OPTIONS</a> <a id="259" class="Pragma">--cubical-compatible</a> <a id="280" class="Pragma">--safe</a> <a id="287" class="Symbol">#-}</a>

<a id="292" class="Keyword">open</a> <a id="297" class="Keyword">import</a> <a id="304" href="Relation.Binary.html" class="Module">Relation.Binary</a>

<a id="321" class="Keyword">module</a> <a id="328" href="Data.List.Relation.Binary.Permutation.Setoid.html" class="Module">Data.List.Relation.Binary.Permutation.Setoid</a>
  <a id="375" class="Symbol">{</a><a id="376" href="Data.List.Relation.Binary.Permutation.Setoid.html#376" class="Bound">a</a> <a id="378" href="Data.List.Relation.Binary.Permutation.Setoid.html#378" class="Bound">ℓ</a><a id="379" class="Symbol">}</a> <a id="381" class="Symbol">(</a><a id="382" href="Data.List.Relation.Binary.Permutation.Setoid.html#382" class="Bound">S</a> <a id="384" class="Symbol">:</a> <a id="386" href="Relation.Binary.Bundles.html#1041" class="Record">Setoid</a> <a id="393" href="Data.List.Relation.Binary.Permutation.Setoid.html#376" class="Bound">a</a> <a id="395" href="Data.List.Relation.Binary.Permutation.Setoid.html#378" class="Bound">ℓ</a><a id="396" class="Symbol">)</a> <a id="398" class="Keyword">where</a>

<a id="405" class="Keyword">open</a> <a id="410" class="Keyword">import</a> <a id="417" href="Data.List.Base.html" class="Module">Data.List.Base</a> <a id="432" class="Keyword">using</a> <a id="438" class="Symbol">(</a><a id="439" href="Agda.Builtin.List.html#130" class="Datatype">List</a><a id="443" class="Symbol">;</a> <a id="445" href="Agda.Builtin.List.html#182" class="InductiveConstructor Operator">_∷_</a><a id="448" class="Symbol">)</a>
<a id="450" class="Keyword">import</a> <a id="457" href="Data.List.Relation.Binary.Permutation.Homogeneous.html" class="Module">Data.List.Relation.Binary.Permutation.Homogeneous</a> <a id="507" class="Symbol">as</a> <a id="510" class="Module">Homogeneous</a>
<a id="522" class="Keyword">import</a> <a id="529" href="Data.List.Relation.Binary.Pointwise.html" class="Module">Data.List.Relation.Binary.Pointwise</a> <a id="565" class="Symbol">as</a> <a id="568" class="Module">Pointwise</a>
<a id="578" class="Keyword">open</a> <a id="583" class="Keyword">import</a> <a id="590" href="Data.List.Relation.Binary.Equality.Setoid.html" class="Module">Data.List.Relation.Binary.Equality.Setoid</a> <a id="632" href="Data.List.Relation.Binary.Permutation.Setoid.html#382" class="Bound">S</a>
<a id="634" class="Keyword">open</a> <a id="639" class="Keyword">import</a> <a id="646" href="Data.Nat.Base.html" class="Module">Data.Nat.Base</a> <a id="660" class="Keyword">using</a> <a id="666" class="Symbol">(</a><a id="667" href="Agda.Builtin.Nat.html#186" class="Datatype">ℕ</a><a id="668" class="Symbol">;</a> <a id="670" href="Agda.Builtin.Nat.html#204" class="InductiveConstructor">zero</a><a id="674" class="Symbol">;</a> <a id="676" href="Agda.Builtin.Nat.html#217" class="InductiveConstructor">suc</a><a id="679" class="Symbol">;</a> <a id="681" href="Agda.Builtin.Nat.html#319" class="Primitive Operator">_+_</a><a id="684" class="Symbol">)</a>
<a id="686" class="Keyword">open</a> <a id="691" class="Keyword">import</a> <a id="698" href="Level.html" class="Module">Level</a> <a id="704" class="Keyword">using</a> <a id="710" class="Symbol">(</a><a id="711" href="Agda.Primitive.html#804" class="Primitive Operator">_⊔_</a><a id="714" class="Symbol">)</a>
<a id="716" class="Keyword">open</a> <a id="721" class="Keyword">import</a> <a id="728" href="Relation.Binary.PropositionalEquality.Core.html" class="Module">Relation.Binary.PropositionalEquality.Core</a> <a id="771" class="Keyword">using</a> <a id="777" class="Symbol">(</a><a id="778" href="Agda.Builtin.Equality.html#133" class="Datatype Operator">_≡_</a><a id="781" class="Symbol">;</a> <a id="783" href="Agda.Builtin.Equality.html#190" class="InductiveConstructor">refl</a><a id="787" class="Symbol">)</a>
<a id="789" class="Keyword">import</a> <a id="796" href="Relation.Binary.Reasoning.Setoid.html" class="Module">Relation.Binary.Reasoning.Setoid</a> <a id="829" class="Symbol">as</a> <a id="832" class="Module">SetoidReasoning</a>

<a id="849" class="Keyword">private</a>
  <a id="859" class="Keyword">module</a> <a id="Eq"></a><a id="866" href="Data.List.Relation.Binary.Permutation.Setoid.html#866" class="Module">Eq</a> <a id="869" class="Symbol">=</a> <a id="871" href="Relation.Binary.Bundles.html#1041" class="Module">Setoid</a> <a id="878" href="Data.List.Relation.Binary.Permutation.Setoid.html#382" class="Bound">S</a>
<a id="880" class="Keyword">open</a> <a id="885" href="Data.List.Relation.Binary.Permutation.Setoid.html#866" class="Module">Eq</a> <a id="888" class="Keyword">using</a> <a id="894" class="Symbol">(</a><a id="895" href="Relation.Binary.Bundles.html#1130" class="Field Operator">_≈_</a><a id="898" class="Symbol">)</a> <a id="900" class="Keyword">renaming</a> <a id="909" class="Symbol">(</a><a id="910" href="Relation.Binary.Bundles.html#1104" class="Field">Carrier</a> <a id="918" class="Symbol">to</a> <a id="921" class="Field">A</a><a id="922" class="Symbol">)</a>

<a id="925" class="Comment">------------------------------------------------------------------------</a>
<a id="998" class="Comment">-- Definition</a>

<a id="1013" class="Keyword">open</a> <a id="1018" href="Data.List.Relation.Binary.Permutation.Homogeneous.html" class="Module">Homogeneous</a> <a id="1030" class="Keyword">public</a>
  <a id="1039" class="Keyword">using</a> <a id="1045" class="Symbol">(</a><a id="1046" href="Data.List.Relation.Binary.Permutation.Homogeneous.html#674" class="InductiveConstructor">refl</a><a id="1050" class="Symbol">;</a> <a id="1052" href="Data.List.Relation.Binary.Permutation.Homogeneous.html#736" class="InductiveConstructor">prep</a><a id="1056" class="Symbol">;</a> <a id="1058" href="Data.List.Relation.Binary.Permutation.Homogeneous.html#829" class="InductiveConstructor">swap</a><a id="1062" class="Symbol">;</a> <a id="1064" href="Data.List.Relation.Binary.Permutation.Homogeneous.html#955" class="InductiveConstructor">trans</a><a id="1069" class="Symbol">)</a>

<a id="1072" class="Keyword">infix</a> <a id="1078" class="Number">3</a> <a id="1080" href="Data.List.Relation.Binary.Permutation.Setoid.html#1085" class="Function Operator">_↭_</a>

<a id="_↭_"></a><a id="1085" href="Data.List.Relation.Binary.Permutation.Setoid.html#1085" class="Function Operator">_↭_</a> <a id="1089" class="Symbol">:</a> <a id="1091" href="Relation.Binary.Core.html#896" class="Function">Rel</a> <a id="1095" class="Symbol">(</a><a id="1096" href="Agda.Builtin.List.html#130" class="Datatype">List</a> <a id="1101" href="Data.List.Relation.Binary.Permutation.Setoid.html#921" class="Field">A</a><a id="1102" class="Symbol">)</a> <a id="1104" class="Symbol">(</a><a id="1105" href="Data.List.Relation.Binary.Permutation.Setoid.html#376" class="Bound">a</a> <a id="1107" href="Agda.Primitive.html#804" class="Primitive Operator">⊔</a> <a id="1109" href="Data.List.Relation.Binary.Permutation.Setoid.html#378" class="Bound">ℓ</a><a id="1110" class="Symbol">)</a>
<a id="1112" href="Data.List.Relation.Binary.Permutation.Setoid.html#1085" class="Function Operator">_↭_</a> <a id="1116" class="Symbol">=</a> <a id="1118" href="Data.List.Relation.Binary.Permutation.Homogeneous.html#605" class="Datatype">Homogeneous.Permutation</a> <a id="1142" href="Relation.Binary.Bundles.html#1130" class="Field Operator">_≈_</a>

<a id="1147" class="Comment">------------------------------------------------------------------------</a>
<a id="1220" class="Comment">-- Constructor aliases</a>

<a id="1244" class="Comment">-- These provide aliases for `swap` and `prep` when the elements being</a>
<a id="1315" class="Comment">-- swapped or prepended are propositionally equal</a>

<a id="↭-prep"></a><a id="1366" href="Data.List.Relation.Binary.Permutation.Setoid.html#1366" class="Function">↭-prep</a> <a id="1373" class="Symbol">:</a> <a id="1375" class="Symbol">∀</a> <a id="1377" href="Data.List.Relation.Binary.Permutation.Setoid.html#1377" class="Bound">x</a> <a id="1379" class="Symbol">{</a><a id="1380" href="Data.List.Relation.Binary.Permutation.Setoid.html#1380" class="Bound">xs</a> <a id="1383" href="Data.List.Relation.Binary.Permutation.Setoid.html#1383" class="Bound">ys</a><a id="1385" class="Symbol">}</a> <a id="1387" class="Symbol">→</a> <a id="1389" href="Data.List.Relation.Binary.Permutation.Setoid.html#1380" class="Bound">xs</a> <a id="1392" href="Data.List.Relation.Binary.Permutation.Setoid.html#1085" class="Function Operator">↭</a> <a id="1394" href="Data.List.Relation.Binary.Permutation.Setoid.html#1383" class="Bound">ys</a> <a id="1397" class="Symbol">→</a> <a id="1399" href="Data.List.Relation.Binary.Permutation.Setoid.html#1377" class="Bound">x</a> <a id="1401" class="InductiveConstructor Operator">∷</a> <a id="1403" href="Data.List.Relation.Binary.Permutation.Setoid.html#1380" class="Bound">xs</a> <a id="1406" href="Data.List.Relation.Binary.Permutation.Setoid.html#1085" class="Function Operator">↭</a> <a id="1408" href="Data.List.Relation.Binary.Permutation.Setoid.html#1377" class="Bound">x</a> <a id="1410" class="InductiveConstructor Operator">∷</a> <a id="1412" href="Data.List.Relation.Binary.Permutation.Setoid.html#1383" class="Bound">ys</a>
<a id="1415" href="Data.List.Relation.Binary.Permutation.Setoid.html#1366" class="Function">↭-prep</a> <a id="1422" href="Data.List.Relation.Binary.Permutation.Setoid.html#1422" class="Bound">x</a> <a id="1424" href="Data.List.Relation.Binary.Permutation.Setoid.html#1424" class="Bound">xs↭ys</a> <a id="1430" class="Symbol">=</a> <a id="1432" href="Data.List.Relation.Binary.Permutation.Homogeneous.html#736" class="InductiveConstructor">prep</a> <a id="1437" href="Relation.Binary.Structures.html#1596" class="Function">Eq.refl</a> <a id="1445" href="Data.List.Relation.Binary.Permutation.Setoid.html#1424" class="Bound">xs↭ys</a>

<a id="↭-swap"></a><a id="1452" href="Data.List.Relation.Binary.Permutation.Setoid.html#1452" class="Function">↭-swap</a> <a id="1459" class="Symbol">:</a> <a id="1461" class="Symbol">∀</a> <a id="1463" href="Data.List.Relation.Binary.Permutation.Setoid.html#1463" class="Bound">x</a> <a id="1465" href="Data.List.Relation.Binary.Permutation.Setoid.html#1465" class="Bound">y</a> <a id="1467" class="Symbol">{</a><a id="1468" href="Data.List.Relation.Binary.Permutation.Setoid.html#1468" class="Bound">xs</a> <a id="1471" href="Data.List.Relation.Binary.Permutation.Setoid.html#1471" class="Bound">ys</a><a id="1473" class="Symbol">}</a> <a id="1475" class="Symbol">→</a> <a id="1477" href="Data.List.Relation.Binary.Permutation.Setoid.html#1468" class="Bound">xs</a> <a id="1480" href="Data.List.Relation.Binary.Permutation.Setoid.html#1085" class="Function Operator">↭</a> <a id="1482" href="Data.List.Relation.Binary.Permutation.Setoid.html#1471" class="Bound">ys</a> <a id="1485" class="Symbol">→</a> <a id="1487" href="Data.List.Relation.Binary.Permutation.Setoid.html#1463" class="Bound">x</a> <a id="1489" class="InductiveConstructor Operator">∷</a> <a id="1491" href="Data.List.Relation.Binary.Permutation.Setoid.html#1465" class="Bound">y</a> <a id="1493" class="InductiveConstructor Operator">∷</a> <a id="1495" href="Data.List.Relation.Binary.Permutation.Setoid.html#1468" class="Bound">xs</a> <a id="1498" href="Data.List.Relation.Binary.Permutation.Setoid.html#1085" class="Function Operator">↭</a> <a id="1500" href="Data.List.Relation.Binary.Permutation.Setoid.html#1465" class="Bound">y</a> <a id="1502" class="InductiveConstructor Operator">∷</a> <a id="1504" href="Data.List.Relation.Binary.Permutation.Setoid.html#1463" class="Bound">x</a> <a id="1506" class="InductiveConstructor Operator">∷</a> <a id="1508" href="Data.List.Relation.Binary.Permutation.Setoid.html#1471" class="Bound">ys</a>
<a id="1511" href="Data.List.Relation.Binary.Permutation.Setoid.html#1452" class="Function">↭-swap</a> <a id="1518" href="Data.List.Relation.Binary.Permutation.Setoid.html#1518" class="Bound">x</a> <a id="1520" href="Data.List.Relation.Binary.Permutation.Setoid.html#1520" class="Bound">y</a> <a id="1522" href="Data.List.Relation.Binary.Permutation.Setoid.html#1522" class="Bound">xs↭ys</a> <a id="1528" class="Symbol">=</a> <a id="1530" href="Data.List.Relation.Binary.Permutation.Homogeneous.html#829" class="InductiveConstructor">swap</a> <a id="1535" href="Relation.Binary.Structures.html#1596" class="Function">Eq.refl</a> <a id="1543" href="Relation.Binary.Structures.html#1596" class="Function">Eq.refl</a> <a id="1551" href="Data.List.Relation.Binary.Permutation.Setoid.html#1522" class="Bound">xs↭ys</a>

<a id="1558" class="Comment">------------------------------------------------------------------------</a>
<a id="1631" class="Comment">-- Functions over permutations</a>

<a id="steps"></a><a id="1663" href="Data.List.Relation.Binary.Permutation.Setoid.html#1663" class="Function">steps</a> <a id="1669" class="Symbol">:</a> <a id="1671" class="Symbol">∀</a> <a id="1673" class="Symbol">{</a><a id="1674" href="Data.List.Relation.Binary.Permutation.Setoid.html#1674" class="Bound">xs</a> <a id="1677" href="Data.List.Relation.Binary.Permutation.Setoid.html#1677" class="Bound">ys</a><a id="1679" class="Symbol">}</a> <a id="1681" class="Symbol">→</a> <a id="1683" href="Data.List.Relation.Binary.Permutation.Setoid.html#1674" class="Bound">xs</a> <a id="1686" href="Data.List.Relation.Binary.Permutation.Setoid.html#1085" class="Function Operator">↭</a> <a id="1688" href="Data.List.Relation.Binary.Permutation.Setoid.html#1677" class="Bound">ys</a> <a id="1691" class="Symbol">→</a> <a id="1693" href="Agda.Builtin.Nat.html#186" class="Datatype">ℕ</a>
<a id="1695" href="Data.List.Relation.Binary.Permutation.Setoid.html#1663" class="Function">steps</a> <a id="1701" class="Symbol">(</a><a id="1702" href="Data.List.Relation.Binary.Permutation.Homogeneous.html#674" class="InductiveConstructor">refl</a> <a id="1707" class="Symbol">_)</a>            <a id="1721" class="Symbol">=</a> <a id="1723" class="Number">1</a>
<a id="1725" href="Data.List.Relation.Binary.Permutation.Setoid.html#1663" class="Function">steps</a> <a id="1731" class="Symbol">(</a><a id="1732" href="Data.List.Relation.Binary.Permutation.Homogeneous.html#736" class="InductiveConstructor">prep</a> <a id="1737" class="Symbol">_</a> <a id="1739" href="Data.List.Relation.Binary.Permutation.Setoid.html#1739" class="Bound">xs↭ys</a><a id="1744" class="Symbol">)</a>      <a id="1751" class="Symbol">=</a> <a id="1753" href="Agda.Builtin.Nat.html#217" class="InductiveConstructor">suc</a> <a id="1757" class="Symbol">(</a><a id="1758" href="Data.List.Relation.Binary.Permutation.Setoid.html#1663" class="Function">steps</a> <a id="1764" href="Data.List.Relation.Binary.Permutation.Setoid.html#1739" class="Bound">xs↭ys</a><a id="1769" class="Symbol">)</a>
<a id="1771" href="Data.List.Relation.Binary.Permutation.Setoid.html#1663" class="Function">steps</a> <a id="1777" class="Symbol">(</a><a id="1778" href="Data.List.Relation.Binary.Permutation.Homogeneous.html#829" class="InductiveConstructor">swap</a> <a id="1783" class="Symbol">_</a> <a id="1785" class="Symbol">_</a> <a id="1787" href="Data.List.Relation.Binary.Permutation.Setoid.html#1787" class="Bound">xs↭ys</a><a id="1792" class="Symbol">)</a>    <a id="1797" class="Symbol">=</a> <a id="1799" href="Agda.Builtin.Nat.html#217" class="InductiveConstructor">suc</a> <a id="1803" class="Symbol">(</a><a id="1804" href="Data.List.Relation.Binary.Permutation.Setoid.html#1663" class="Function">steps</a> <a id="1810" href="Data.List.Relation.Binary.Permutation.Setoid.html#1787" class="Bound">xs↭ys</a><a id="1815" class="Symbol">)</a>
<a id="1817" href="Data.List.Relation.Binary.Permutation.Setoid.html#1663" class="Function">steps</a> <a id="1823" class="Symbol">(</a><a id="1824" href="Data.List.Relation.Binary.Permutation.Homogeneous.html#955" class="InductiveConstructor">trans</a> <a id="1830" href="Data.List.Relation.Binary.Permutation.Setoid.html#1830" class="Bound">xs↭ys</a> <a id="1836" href="Data.List.Relation.Binary.Permutation.Setoid.html#1836" class="Bound">ys↭zs</a><a id="1841" class="Symbol">)</a> <a id="1843" class="Symbol">=</a> <a id="1845" href="Data.List.Relation.Binary.Permutation.Setoid.html#1663" class="Function">steps</a> <a id="1851" href="Data.List.Relation.Binary.Permutation.Setoid.html#1830" class="Bound">xs↭ys</a> <a id="1857" href="Agda.Builtin.Nat.html#319" class="Primitive Operator">+</a> <a id="1859" href="Data.List.Relation.Binary.Permutation.Setoid.html#1663" class="Function">steps</a> <a id="1865" href="Data.List.Relation.Binary.Permutation.Setoid.html#1836" class="Bound">ys↭zs</a>

<a id="1872" class="Comment">------------------------------------------------------------------------</a>
<a id="1945" class="Comment">-- _↭_ is an equivalence</a>

<a id="↭-reflexive"></a><a id="1971" href="Data.List.Relation.Binary.Permutation.Setoid.html#1971" class="Function">↭-reflexive</a> <a id="1983" class="Symbol">:</a> <a id="1985" href="Agda.Builtin.Equality.html#133" class="Datatype Operator">_≡_</a> <a id="1989" href="Relation.Binary.Core.html#1268" class="Function Operator">⇒</a> <a id="1991" href="Data.List.Relation.Binary.Permutation.Setoid.html#1085" class="Function Operator">_↭_</a>
<a id="1995" href="Data.List.Relation.Binary.Permutation.Setoid.html#1971" class="Function">↭-reflexive</a> <a id="2007" href="Agda.Builtin.Equality.html#190" class="InductiveConstructor">refl</a> <a id="2012" class="Symbol">=</a> <a id="2014" href="Data.List.Relation.Binary.Permutation.Homogeneous.html#674" class="InductiveConstructor">refl</a> <a id="2019" class="Symbol">(</a><a id="2020" href="Data.List.Relation.Binary.Pointwise.Properties.html#1074" class="Function">Pointwise.refl</a> <a id="2035" href="Relation.Binary.Structures.html#1596" class="Function">Eq.refl</a><a id="2042" class="Symbol">)</a>

<a id="↭-refl"></a><a id="2045" href="Data.List.Relation.Binary.Permutation.Setoid.html#2045" class="Function">↭-refl</a> <a id="2052" class="Symbol">:</a> <a id="2054" href="Relation.Binary.Definitions.html#1418" class="Function">Reflexive</a> <a id="2064" href="Data.List.Relation.Binary.Permutation.Setoid.html#1085" class="Function Operator">_↭_</a>
<a id="2068" href="Data.List.Relation.Binary.Permutation.Setoid.html#2045" class="Function">↭-refl</a> <a id="2075" class="Symbol">=</a> <a id="2077" href="Data.List.Relation.Binary.Permutation.Setoid.html#1971" class="Function">↭-reflexive</a> <a id="2089" href="Agda.Builtin.Equality.html#190" class="InductiveConstructor">refl</a>

<a id="↭-sym"></a><a id="2095" href="Data.List.Relation.Binary.Permutation.Setoid.html#2095" class="Function">↭-sym</a> <a id="2101" class="Symbol">:</a> <a id="2103" href="Relation.Binary.Definitions.html#1577" class="Function">Symmetric</a> <a id="2113" href="Data.List.Relation.Binary.Permutation.Setoid.html#1085" class="Function Operator">_↭_</a>
<a id="2117" href="Data.List.Relation.Binary.Permutation.Setoid.html#2095" class="Function">↭-sym</a> <a id="2123" class="Symbol">=</a> <a id="2125" href="Data.List.Relation.Binary.Permutation.Homogeneous.html#1196" class="Function">Homogeneous.sym</a> <a id="2141" href="Relation.Binary.Structures.html#1622" class="Function">Eq.sym</a>

<a id="↭-trans"></a><a id="2149" href="Data.List.Relation.Binary.Permutation.Setoid.html#2149" class="Function">↭-trans</a> <a id="2157" class="Symbol">:</a> <a id="2159" href="Relation.Binary.Definitions.html#1945" class="Function">Transitive</a> <a id="2170" href="Data.List.Relation.Binary.Permutation.Setoid.html#1085" class="Function Operator">_↭_</a>
<a id="2174" href="Data.List.Relation.Binary.Permutation.Setoid.html#2149" class="Function">↭-trans</a> <a id="2182" class="Symbol">=</a> <a id="2184" href="Data.List.Relation.Binary.Permutation.Homogeneous.html#955" class="InductiveConstructor">trans</a>

<a id="↭-isEquivalence"></a><a id="2191" href="Data.List.Relation.Binary.Permutation.Setoid.html#2191" class="Function">↭-isEquivalence</a> <a id="2207" class="Symbol">:</a> <a id="2209" href="Relation.Binary.Structures.html#1550" class="Record">IsEquivalence</a> <a id="2223" href="Data.List.Relation.Binary.Permutation.Setoid.html#1085" class="Function Operator">_↭_</a>
<a id="2227" href="Data.List.Relation.Binary.Permutation.Setoid.html#2191" class="Function">↭-isEquivalence</a> <a id="2243" class="Symbol">=</a> <a id="2245" href="Data.List.Relation.Binary.Permutation.Homogeneous.html#1559" class="Function">Homogeneous.isEquivalence</a> <a id="2271" href="Relation.Binary.Structures.html#1596" class="Function">Eq.refl</a> <a id="2279" href="Relation.Binary.Structures.html#1622" class="Function">Eq.sym</a>

<a id="↭-setoid"></a><a id="2287" href="Data.List.Relation.Binary.Permutation.Setoid.html#2287" class="Function">↭-setoid</a> <a id="2296" class="Symbol">:</a> <a id="2298" href="Relation.Binary.Bundles.html#1041" class="Record">Setoid</a> <a id="2305" class="Symbol">_</a> <a id="2307" class="Symbol">_</a>
<a id="2309" href="Data.List.Relation.Binary.Permutation.Setoid.html#2287" class="Function">↭-setoid</a> <a id="2318" class="Symbol">=</a> <a id="2320" href="Data.List.Relation.Binary.Permutation.Homogeneous.html#1767" class="Function">Homogeneous.setoid</a> <a id="2339" class="Symbol">{</a><a id="2340" class="Argument">R</a> <a id="2342" class="Symbol">=</a> <a id="2344" href="Relation.Binary.Bundles.html#1130" class="Field Operator">_≈_</a><a id="2347" class="Symbol">}</a> <a id="2349" href="Relation.Binary.Structures.html#1596" class="Function">Eq.refl</a> <a id="2357" href="Relation.Binary.Structures.html#1622" class="Function">Eq.sym</a>

<a id="2365" class="Comment">------------------------------------------------------------------------</a>
<a id="2438" class="Comment">-- A reasoning API to chain permutation proofs</a>

<a id="2486" class="Keyword">module</a> <a id="PermutationReasoning"></a><a id="2493" href="Data.List.Relation.Binary.Permutation.Setoid.html#2493" class="Module">PermutationReasoning</a> <a id="2514" class="Keyword">where</a>

  <a id="2523" class="Keyword">private</a>
    <a id="2535" class="Keyword">module</a> <a id="PermutationReasoning.Base"></a><a id="2542" href="Data.List.Relation.Binary.Permutation.Setoid.html#2542" class="Module">Base</a> <a id="2547" class="Symbol">=</a> <a id="2549" href="Relation.Binary.Reasoning.Setoid.html" class="Module">SetoidReasoning</a> <a id="2565" href="Data.List.Relation.Binary.Permutation.Setoid.html#2287" class="Function">↭-setoid</a>

  <a id="2577" class="Keyword">open</a> <a id="2582" href="Relation.Binary.Reasoning.Setoid.html" class="Module">SetoidReasoning</a> <a id="2598" href="Data.List.Relation.Binary.Permutation.Setoid.html#2287" class="Function">↭-setoid</a> <a id="2607" class="Keyword">public</a>
    <a id="2618" class="Keyword">hiding</a> <a id="2625" class="Symbol">(</a><a id="2626" href="Relation.Binary.Reasoning.Setoid.html#1061" class="Function">step-≈</a><a id="2632" class="Symbol">;</a> <a id="2634" href="Relation.Binary.Reasoning.Setoid.html#1162" class="Function">step-≈˘</a><a id="2641" class="Symbol">)</a>

  <a id="2646" class="Keyword">infixr</a> <a id="2653" class="Number">2</a> <a id="2655" href="Data.List.Relation.Binary.Permutation.Setoid.html#2709" class="Function">step-↭</a>  <a id="2663" href="Data.List.Relation.Binary.Permutation.Setoid.html#2733" class="Function">step-↭˘</a> <a id="2671" href="Data.List.Relation.Binary.Permutation.Setoid.html#2798" class="Function">step-≋</a> <a id="2678" href="Data.List.Relation.Binary.Permutation.Setoid.html#2969" class="Function">step-≋˘</a> <a id="2686" href="Data.List.Relation.Binary.Permutation.Setoid.html#3360" class="Function">step-swap</a> <a id="2696" href="Data.List.Relation.Binary.Permutation.Setoid.html#3120" class="Function">step-prep</a>

  <a id="PermutationReasoning.step-↭"></a><a id="2709" href="Data.List.Relation.Binary.Permutation.Setoid.html#2709" class="Function">step-↭</a>  <a id="2717" class="Symbol">=</a> <a id="2719" href="Relation.Binary.Reasoning.Setoid.html#1061" class="Function">Base.step-≈</a>
  <a id="PermutationReasoning.step-↭˘"></a><a id="2733" href="Data.List.Relation.Binary.Permutation.Setoid.html#2733" class="Function">step-↭˘</a> <a id="2741" class="Symbol">=</a> <a id="2743" href="Relation.Binary.Reasoning.Setoid.html#1162" class="Function">Base.step-≈˘</a>

  <a id="2759" class="Comment">-- Step with pointwise list equality</a>
  <a id="PermutationReasoning.step-≋"></a><a id="2798" href="Data.List.Relation.Binary.Permutation.Setoid.html#2798" class="Function">step-≋</a> <a id="2805" class="Symbol">:</a> <a id="2807" class="Symbol">∀</a> <a id="2809" href="Data.List.Relation.Binary.Permutation.Setoid.html#2809" class="Bound">x</a> <a id="2811" class="Symbol">{</a><a id="2812" href="Data.List.Relation.Binary.Permutation.Setoid.html#2812" class="Bound">y</a> <a id="2814" href="Data.List.Relation.Binary.Permutation.Setoid.html#2814" class="Bound">z</a><a id="2815" class="Symbol">}</a> <a id="2817" class="Symbol">→</a> <a id="2819" href="Data.List.Relation.Binary.Permutation.Setoid.html#2812" class="Bound">y</a> <a id="2821" href="Relation.Binary.Reasoning.Base.Single.html#1062" class="Datatype Operator">IsRelatedTo</a> <a id="2833" href="Data.List.Relation.Binary.Permutation.Setoid.html#2814" class="Bound">z</a> <a id="2835" class="Symbol">→</a> <a id="2837" href="Data.List.Relation.Binary.Permutation.Setoid.html#2809" class="Bound">x</a> <a id="2839" href="Data.List.Relation.Binary.Equality.Setoid.html#1194" class="Function Operator">≋</a> <a id="2841" href="Data.List.Relation.Binary.Permutation.Setoid.html#2812" class="Bound">y</a> <a id="2843" class="Symbol">→</a> <a id="2845" href="Data.List.Relation.Binary.Permutation.Setoid.html#2809" class="Bound">x</a> <a id="2847" href="Relation.Binary.Reasoning.Base.Single.html#1062" class="Datatype Operator">IsRelatedTo</a> <a id="2859" href="Data.List.Relation.Binary.Permutation.Setoid.html#2814" class="Bound">z</a>
  <a id="2863" href="Data.List.Relation.Binary.Permutation.Setoid.html#2798" class="Function">step-≋</a> <a id="2870" href="Data.List.Relation.Binary.Permutation.Setoid.html#2870" class="Bound">x</a> <a id="2872" class="Symbol">(</a><a id="2873" href="Relation.Binary.Reasoning.Base.Single.html#1102" class="InductiveConstructor">relTo</a> <a id="2879" href="Data.List.Relation.Binary.Permutation.Setoid.html#2879" class="Bound">y↔z</a><a id="2882" class="Symbol">)</a> <a id="2884" href="Data.List.Relation.Binary.Permutation.Setoid.html#2884" class="Bound">x≋y</a> <a id="2888" class="Symbol">=</a> <a id="2890" href="Relation.Binary.Reasoning.Base.Single.html#1102" class="InductiveConstructor">relTo</a> <a id="2896" class="Symbol">(</a><a id="2897" href="Data.List.Relation.Binary.Permutation.Homogeneous.html#955" class="InductiveConstructor">trans</a> <a id="2903" class="Symbol">(</a><a id="2904" href="Data.List.Relation.Binary.Permutation.Homogeneous.html#674" class="InductiveConstructor">refl</a> <a id="2909" href="Data.List.Relation.Binary.Permutation.Setoid.html#2884" class="Bound">x≋y</a><a id="2912" class="Symbol">)</a> <a id="2914" href="Data.List.Relation.Binary.Permutation.Setoid.html#2879" class="Bound">y↔z</a><a id="2917" class="Symbol">)</a>

  <a id="2922" class="Comment">-- Step with flipped pointwise list equality</a>
  <a id="PermutationReasoning.step-≋˘"></a><a id="2969" href="Data.List.Relation.Binary.Permutation.Setoid.html#2969" class="Function">step-≋˘</a> <a id="2977" class="Symbol">:</a> <a id="2979" class="Symbol">∀</a> <a id="2981" href="Data.List.Relation.Binary.Permutation.Setoid.html#2981" class="Bound">x</a> <a id="2983" class="Symbol">{</a><a id="2984" href="Data.List.Relation.Binary.Permutation.Setoid.html#2984" class="Bound">y</a> <a id="2986" href="Data.List.Relation.Binary.Permutation.Setoid.html#2986" class="Bound">z</a><a id="2987" class="Symbol">}</a> <a id="2989" class="Symbol">→</a> <a id="2991" href="Data.List.Relation.Binary.Permutation.Setoid.html#2984" class="Bound">y</a> <a id="2993" href="Relation.Binary.Reasoning.Base.Single.html#1062" class="Datatype Operator">IsRelatedTo</a> <a id="3005" href="Data.List.Relation.Binary.Permutation.Setoid.html#2986" class="Bound">z</a> <a id="3007" class="Symbol">→</a> <a id="3009" href="Data.List.Relation.Binary.Permutation.Setoid.html#2984" class="Bound">y</a> <a id="3011" href="Data.List.Relation.Binary.Equality.Setoid.html#1194" class="Function Operator">≋</a> <a id="3013" href="Data.List.Relation.Binary.Permutation.Setoid.html#2981" class="Bound">x</a> <a id="3015" class="Symbol">→</a> <a id="3017" href="Data.List.Relation.Binary.Permutation.Setoid.html#2981" class="Bound">x</a> <a id="3019" href="Relation.Binary.Reasoning.Base.Single.html#1062" class="Datatype Operator">IsRelatedTo</a> <a id="3031" href="Data.List.Relation.Binary.Permutation.Setoid.html#2986" class="Bound">z</a>
  <a id="3035" href="Data.List.Relation.Binary.Permutation.Setoid.html#2969" class="Function">step-≋˘</a> <a id="3043" href="Data.List.Relation.Binary.Permutation.Setoid.html#3043" class="Bound">x</a> <a id="3045" href="Data.List.Relation.Binary.Permutation.Setoid.html#3045" class="Bound">y↭z</a> <a id="3049" href="Data.List.Relation.Binary.Permutation.Setoid.html#3049" class="Bound">y≋x</a> <a id="3053" class="Symbol">=</a> <a id="3055" href="Data.List.Relation.Binary.Permutation.Setoid.html#3043" class="Bound">x</a> <a id="3057" href="Data.List.Relation.Binary.Permutation.Setoid.html#2798" class="Function">≋⟨</a> <a id="3060" href="Data.List.Relation.Binary.Equality.Setoid.html#1548" class="Function">≋-sym</a> <a id="3066" href="Data.List.Relation.Binary.Permutation.Setoid.html#3049" class="Bound">y≋x</a> <a id="3070" href="Data.List.Relation.Binary.Permutation.Setoid.html#2798" class="Function">⟩</a> <a id="3072" href="Data.List.Relation.Binary.Permutation.Setoid.html#3045" class="Bound">y↭z</a>

  <a id="3079" class="Comment">-- Skip reasoning on the first element</a>
  <a id="PermutationReasoning.step-prep"></a><a id="3120" href="Data.List.Relation.Binary.Permutation.Setoid.html#3120" class="Function">step-prep</a> <a id="3130" class="Symbol">:</a> <a id="3132" class="Symbol">∀</a> <a id="3134" href="Data.List.Relation.Binary.Permutation.Setoid.html#3134" class="Bound">x</a> <a id="3136" href="Data.List.Relation.Binary.Permutation.Setoid.html#3136" class="Bound">xs</a> <a id="3139" class="Symbol">{</a><a id="3140" href="Data.List.Relation.Binary.Permutation.Setoid.html#3140" class="Bound">ys</a> <a id="3143" href="Data.List.Relation.Binary.Permutation.Setoid.html#3143" class="Bound">zs</a> <a id="3146" class="Symbol">:</a> <a id="3148" href="Agda.Builtin.List.html#130" class="Datatype">List</a> <a id="3153" href="Data.List.Relation.Binary.Permutation.Setoid.html#921" class="Field">A</a><a id="3154" class="Symbol">}</a> <a id="3156" class="Symbol">→</a> <a id="3158" class="Symbol">(</a><a id="3159" href="Data.List.Relation.Binary.Permutation.Setoid.html#3134" class="Bound">x</a> <a id="3161" href="Agda.Builtin.List.html#182" class="InductiveConstructor Operator">∷</a> <a id="3163" href="Data.List.Relation.Binary.Permutation.Setoid.html#3140" class="Bound">ys</a><a id="3165" class="Symbol">)</a> <a id="3167" href="Relation.Binary.Reasoning.Base.Single.html#1062" class="Datatype Operator">IsRelatedTo</a> <a id="3179" href="Data.List.Relation.Binary.Permutation.Setoid.html#3143" class="Bound">zs</a> <a id="3182" class="Symbol">→</a>
              <a id="3198" href="Data.List.Relation.Binary.Permutation.Setoid.html#3136" class="Bound">xs</a> <a id="3201" href="Data.List.Relation.Binary.Permutation.Setoid.html#1085" class="Function Operator">↭</a> <a id="3203" href="Data.List.Relation.Binary.Permutation.Setoid.html#3140" class="Bound">ys</a> <a id="3206" class="Symbol">→</a> <a id="3208" class="Symbol">(</a><a id="3209" href="Data.List.Relation.Binary.Permutation.Setoid.html#3134" class="Bound">x</a> <a id="3211" href="Agda.Builtin.List.html#182" class="InductiveConstructor Operator">∷</a> <a id="3213" href="Data.List.Relation.Binary.Permutation.Setoid.html#3136" class="Bound">xs</a><a id="3215" class="Symbol">)</a> <a id="3217" href="Relation.Binary.Reasoning.Base.Single.html#1062" class="Datatype Operator">IsRelatedTo</a> <a id="3229" href="Data.List.Relation.Binary.Permutation.Setoid.html#3143" class="Bound">zs</a>
  <a id="3234" href="Data.List.Relation.Binary.Permutation.Setoid.html#3120" class="Function">step-prep</a> <a id="3244" href="Data.List.Relation.Binary.Permutation.Setoid.html#3244" class="Bound">x</a> <a id="3246" href="Data.List.Relation.Binary.Permutation.Setoid.html#3246" class="Bound">xs</a> <a id="3249" href="Data.List.Relation.Binary.Permutation.Setoid.html#3249" class="Bound">rel</a> <a id="3253" href="Data.List.Relation.Binary.Permutation.Setoid.html#3253" class="Bound">xs↭ys</a> <a id="3259" class="Symbol">=</a> <a id="3261" href="Relation.Binary.Reasoning.Base.Single.html#1102" class="InductiveConstructor">relTo</a> <a id="3267" class="Symbol">(</a><a id="3268" href="Data.List.Relation.Binary.Permutation.Homogeneous.html#955" class="InductiveConstructor">trans</a> <a id="3274" class="Symbol">(</a><a id="3275" href="Data.List.Relation.Binary.Permutation.Homogeneous.html#736" class="InductiveConstructor">prep</a> <a id="3280" href="Relation.Binary.Structures.html#1596" class="Function">Eq.refl</a> <a id="3288" href="Data.List.Relation.Binary.Permutation.Setoid.html#3253" class="Bound">xs↭ys</a><a id="3293" class="Symbol">)</a> <a id="3295" class="Symbol">(</a><a id="3296" href="Relation.Binary.Reasoning.Base.Single.html#1925" class="Function Operator">begin</a> <a id="3302" href="Data.List.Relation.Binary.Permutation.Setoid.html#3249" class="Bound">rel</a><a id="3305" class="Symbol">))</a>

  <a id="3311" class="Comment">-- Skip reasoning about the first two elements</a>
  <a id="PermutationReasoning.step-swap"></a><a id="3360" href="Data.List.Relation.Binary.Permutation.Setoid.html#3360" class="Function">step-swap</a> <a id="3370" class="Symbol">:</a> <a id="3372" class="Symbol">∀</a> <a id="3374" href="Data.List.Relation.Binary.Permutation.Setoid.html#3374" class="Bound">x</a> <a id="3376" href="Data.List.Relation.Binary.Permutation.Setoid.html#3376" class="Bound">y</a> <a id="3378" href="Data.List.Relation.Binary.Permutation.Setoid.html#3378" class="Bound">xs</a> <a id="3381" class="Symbol">{</a><a id="3382" href="Data.List.Relation.Binary.Permutation.Setoid.html#3382" class="Bound">ys</a> <a id="3385" href="Data.List.Relation.Binary.Permutation.Setoid.html#3385" class="Bound">zs</a> <a id="3388" class="Symbol">:</a> <a id="3390" href="Agda.Builtin.List.html#130" class="Datatype">List</a> <a id="3395" href="Data.List.Relation.Binary.Permutation.Setoid.html#921" class="Field">A</a><a id="3396" class="Symbol">}</a> <a id="3398" class="Symbol">→</a> <a id="3400" class="Symbol">(</a><a id="3401" href="Data.List.Relation.Binary.Permutation.Setoid.html#3376" class="Bound">y</a> <a id="3403" href="Agda.Builtin.List.html#182" class="InductiveConstructor Operator">∷</a> <a id="3405" href="Data.List.Relation.Binary.Permutation.Setoid.html#3374" class="Bound">x</a> <a id="3407" href="Agda.Builtin.List.html#182" class="InductiveConstructor Operator">∷</a> <a id="3409" href="Data.List.Relation.Binary.Permutation.Setoid.html#3382" class="Bound">ys</a><a id="3411" class="Symbol">)</a> <a id="3413" href="Relation.Binary.Reasoning.Base.Single.html#1062" class="Datatype Operator">IsRelatedTo</a> <a id="3425" href="Data.List.Relation.Binary.Permutation.Setoid.html#3385" class="Bound">zs</a> <a id="3428" class="Symbol">→</a>
              <a id="3444" href="Data.List.Relation.Binary.Permutation.Setoid.html#3378" class="Bound">xs</a> <a id="3447" href="Data.List.Relation.Binary.Permutation.Setoid.html#1085" class="Function Operator">↭</a> <a id="3449" href="Data.List.Relation.Binary.Permutation.Setoid.html#3382" class="Bound">ys</a> <a id="3452" class="Symbol">→</a> <a id="3454" class="Symbol">(</a><a id="3455" href="Data.List.Relation.Binary.Permutation.Setoid.html#3374" class="Bound">x</a> <a id="3457" href="Agda.Builtin.List.html#182" class="InductiveConstructor Operator">∷</a> <a id="3459" href="Data.List.Relation.Binary.Permutation.Setoid.html#3376" class="Bound">y</a> <a id="3461" href="Agda.Builtin.List.html#182" class="InductiveConstructor Operator">∷</a> <a id="3463" href="Data.List.Relation.Binary.Permutation.Setoid.html#3378" class="Bound">xs</a><a id="3465" class="Symbol">)</a> <a id="3467" href="Relation.Binary.Reasoning.Base.Single.html#1062" class="Datatype Operator">IsRelatedTo</a> <a id="3479" href="Data.List.Relation.Binary.Permutation.Setoid.html#3385" class="Bound">zs</a>
  <a id="3484" href="Data.List.Relation.Binary.Permutation.Setoid.html#3360" class="Function">step-swap</a> <a id="3494" href="Data.List.Relation.Binary.Permutation.Setoid.html#3494" class="Bound">x</a> <a id="3496" href="Data.List.Relation.Binary.Permutation.Setoid.html#3496" class="Bound">y</a> <a id="3498" href="Data.List.Relation.Binary.Permutation.Setoid.html#3498" class="Bound">xs</a> <a id="3501" href="Data.List.Relation.Binary.Permutation.Setoid.html#3501" class="Bound">rel</a> <a id="3505" href="Data.List.Relation.Binary.Permutation.Setoid.html#3505" class="Bound">xs↭ys</a> <a id="3511" class="Symbol">=</a> <a id="3513" href="Relation.Binary.Reasoning.Base.Single.html#1102" class="InductiveConstructor">relTo</a> <a id="3519" class="Symbol">(</a><a id="3520" href="Data.List.Relation.Binary.Permutation.Homogeneous.html#955" class="InductiveConstructor">trans</a> <a id="3526" class="Symbol">(</a><a id="3527" href="Data.List.Relation.Binary.Permutation.Homogeneous.html#829" class="InductiveConstructor">swap</a> <a id="3532" href="Relation.Binary.Structures.html#1596" class="Function">Eq.refl</a> <a id="3540" href="Relation.Binary.Structures.html#1596" class="Function">Eq.refl</a> <a id="3548" href="Data.List.Relation.Binary.Permutation.Setoid.html#3505" class="Bound">xs↭ys</a><a id="3553" class="Symbol">)</a> <a id="3555" class="Symbol">(</a><a id="3556" href="Relation.Binary.Reasoning.Base.Single.html#1925" class="Function Operator">begin</a> <a id="3562" href="Data.List.Relation.Binary.Permutation.Setoid.html#3501" class="Bound">rel</a><a id="3565" class="Symbol">))</a>

  <a id="3571" class="Keyword">syntax</a> <a id="3578" href="Data.List.Relation.Binary.Permutation.Setoid.html#2709" class="Function">step-↭</a>  <a id="3586" class="Bound">x</a> <a id="3588" class="Bound">y↭z</a> <a id="3592" class="Bound">x↭y</a> <a id="3596" class="Symbol">=</a> <a id="3598" class="Bound">x</a> <a id="3600" class="Function">↭⟨</a>  <a id="3604" class="Bound">x↭y</a> <a id="3608" class="Function">⟩</a> <a id="3610" class="Bound">y↭z</a>
  <a id="3616" class="Keyword">syntax</a> <a id="3623" href="Data.List.Relation.Binary.Permutation.Setoid.html#2733" class="Function">step-↭˘</a> <a id="3631" class="Bound">x</a> <a id="3633" class="Bound">y↭z</a> <a id="3637" class="Bound">y↭x</a> <a id="3641" class="Symbol">=</a> <a id="3643" class="Bound">x</a> <a id="3645" class="Function">↭˘⟨</a>  <a id="3650" class="Bound">y↭x</a> <a id="3654" class="Function">⟩</a> <a id="3656" class="Bound">y↭z</a>
  <a id="3662" class="Keyword">syntax</a> <a id="3669" href="Data.List.Relation.Binary.Permutation.Setoid.html#2798" class="Function">step-≋</a>  <a id="3677" class="Bound">x</a> <a id="3679" class="Bound">y↭z</a> <a id="3683" class="Bound">x≋y</a> <a id="3687" class="Symbol">=</a> <a id="3689" class="Bound">x</a> <a id="3691" class="Function">≋⟨</a>  <a id="3695" class="Bound">x≋y</a> <a id="3699" class="Function">⟩</a> <a id="3701" class="Bound">y↭z</a>
  <a id="3707" class="Keyword">syntax</a> <a id="3714" href="Data.List.Relation.Binary.Permutation.Setoid.html#2969" class="Function">step-≋˘</a> <a id="3722" class="Bound">x</a> <a id="3724" class="Bound">y↭z</a> <a id="3728" class="Bound">y≋x</a> <a id="3732" class="Symbol">=</a> <a id="3734" class="Bound">x</a> <a id="3736" class="Function">≋˘⟨</a>  <a id="3741" class="Bound">y≋x</a> <a id="3745" class="Function">⟩</a> <a id="3747" class="Bound">y↭z</a>
  <a id="3753" class="Keyword">syntax</a> <a id="3760" href="Data.List.Relation.Binary.Permutation.Setoid.html#3120" class="Function">step-prep</a> <a id="3770" class="Bound">x</a> <a id="3772" class="Bound">xs</a> <a id="3775" class="Bound">y↭z</a> <a id="3779" class="Bound">x↭y</a> <a id="3783" class="Symbol">=</a> <a id="3785" class="Bound">x</a> <a id="3787" class="Function">∷</a> <a id="3789" class="Bound">xs</a> <a id="3792" class="Function">&lt;⟨</a> <a id="3795" class="Bound">x↭y</a> <a id="3799" class="Function">⟩</a> <a id="3801" class="Bound">y↭z</a>
  <a id="3807" class="Keyword">syntax</a> <a id="3814" href="Data.List.Relation.Binary.Permutation.Setoid.html#3360" class="Function">step-swap</a> <a id="3824" class="Bound">x</a> <a id="3826" class="Bound">y</a> <a id="3828" class="Bound">xs</a> <a id="3831" class="Bound">y↭z</a> <a id="3835" class="Bound">x↭y</a> <a id="3839" class="Symbol">=</a> <a id="3841" class="Bound">x</a> <a id="3843" class="Function">∷</a> <a id="3845" class="Bound">y</a> <a id="3847" class="Function">∷</a> <a id="3849" class="Bound">xs</a> <a id="3852" class="Function">&lt;&lt;⟨</a> <a id="3856" class="Bound">x↭y</a> <a id="3860" class="Function">⟩</a> <a id="3862" class="Bound">y↭z</a>
</pre></body></html>