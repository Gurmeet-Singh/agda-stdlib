<!DOCTYPE HTML>
<html><head><meta charset="utf-8"><title>README.Data.Container.Indexed</title><link rel="stylesheet" href="Agda.css"></head><body><pre class="Agda"><a id="1" class="Comment">------------------------------------------------------------------------</a>
<a id="74" class="Comment">-- The Agda standard library</a>
<a id="103" class="Comment">--</a>
<a id="106" class="Comment">-- Example showing how to define an indexed container</a>
<a id="160" class="Comment">------------------------------------------------------------------------</a>

<a id="234" class="Symbol">{-#</a> <a id="238" class="Keyword">OPTIONS</a> <a id="246" class="Pragma">--with-K</a> <a id="255" class="Pragma">--safe</a> <a id="262" class="Pragma">--guardedness</a> <a id="276" class="Symbol">#-}</a>

<a id="281" class="Keyword">module</a> <a id="288" href="README.Data.Container.Indexed.html" class="Module">README.Data.Container.Indexed</a> <a id="318" class="Keyword">where</a>

<a id="325" class="Keyword">open</a> <a id="330" class="Keyword">import</a> <a id="337" href="Data.Unit.html" class="Module">Data.Unit</a>
<a id="347" class="Keyword">open</a> <a id="352" class="Keyword">import</a> <a id="359" href="Data.Empty.html" class="Module">Data.Empty</a>
<a id="370" class="Keyword">open</a> <a id="375" class="Keyword">import</a> <a id="382" href="Data.Nat.Base.html" class="Module">Data.Nat.Base</a>
<a id="396" class="Keyword">open</a> <a id="401" class="Keyword">import</a> <a id="408" href="Data.Product.html" class="Module">Data.Product</a>
<a id="421" class="Keyword">open</a> <a id="426" class="Keyword">import</a> <a id="433" href="Function.Base.html" class="Module">Function.Base</a> <a id="447" class="Keyword">using</a> <a id="453" class="Symbol">(</a><a id="454" href="Function.Base.html#4503" class="Function Operator">_∋_</a><a id="457" class="Symbol">)</a>
<a id="459" class="Keyword">open</a> <a id="464" class="Keyword">import</a> <a id="471" href="Data.W.Indexed.html" class="Module">Data.W.Indexed</a>
<a id="486" class="Keyword">open</a> <a id="491" class="Keyword">import</a> <a id="498" href="Data.Container.Indexed.html" class="Module">Data.Container.Indexed</a>
<a id="521" class="Keyword">open</a> <a id="526" class="Keyword">import</a> <a id="533" href="Data.Container.Indexed.WithK.html" class="Module">Data.Container.Indexed.WithK</a>

<a id="563" class="Keyword">module</a> <a id="570" href="README.Data.Container.Indexed.html#570" class="Module">_</a> <a id="572" class="Symbol">{</a><a id="573" href="README.Data.Container.Indexed.html#573" class="Bound">a</a><a id="574" class="Symbol">}</a> <a id="576" class="Symbol">(</a><a id="577" href="README.Data.Container.Indexed.html#577" class="Bound">A</a> <a id="579" class="Symbol">:</a> <a id="581" href="Agda.Primitive.html#320" class="Primitive">Set</a> <a id="585" href="README.Data.Container.Indexed.html#573" class="Bound">a</a><a id="586" class="Symbol">)</a> <a id="588" class="Keyword">where</a>

<a id="595" class="Comment">------------------------------------------------------------------------</a>
<a id="668" class="Comment">-- Vector as an indexed container</a>

<a id="703" class="Comment">-- An indexed container is defined by three things:</a>
<a id="755" class="Comment">-- 1. Commands the user can emit</a>
<a id="788" class="Comment">-- 2. Responses the indexed container returns to these commands</a>
<a id="852" class="Comment">-- 3. Update of the index based on the command and the response issued.</a>

<a id="925" class="Comment">-- For a vector, commands are constructors, responses are the number of subvectors</a>
<a id="1008" class="Comment">-- (0 if the vector is empty, 1 otherwise) and the update corresponds to setting the</a>
<a id="1093" class="Comment">-- size of the tail (if it exists). We can formalize these ideas like so:</a>

<a id="1168" class="Comment">-- Depending on the size of the vector, we may have reached the end already (nil)</a>
<a id="1250" class="Comment">-- or we may specify what the head should be (cons). This is the type of commands.</a>

  <a id="1336" class="Keyword">data</a> <a id="1341" href="README.Data.Container.Indexed.html#1341" class="Datatype">VecC</a> <a id="1346" class="Symbol">:</a> <a id="1348" href="Agda.Builtin.Nat.html#186" class="Datatype">ℕ</a> <a id="1350" class="Symbol">→</a> <a id="1352" href="Agda.Primitive.html#320" class="Primitive">Set</a> <a id="1356" href="README.Data.Container.Indexed.html#573" class="Bound">a</a> <a id="1358" class="Keyword">where</a>
    <a id="1368" href="README.Data.Container.Indexed.html#1368" class="InductiveConstructor">nil</a>  <a id="1373" class="Symbol">:</a>           <a id="1385" href="README.Data.Container.Indexed.html#1341" class="Datatype">VecC</a> <a id="1390" href="Agda.Builtin.Nat.html#204" class="InductiveConstructor">zero</a>
    <a id="1399" href="README.Data.Container.Indexed.html#1399" class="InductiveConstructor">cons</a> <a id="1404" class="Symbol">:</a> <a id="1406" class="Symbol">∀</a> <a id="1408" href="README.Data.Container.Indexed.html#1408" class="Bound">n</a> <a id="1410" class="Symbol">→</a> <a id="1412" href="README.Data.Container.Indexed.html#577" class="Bound">A</a> <a id="1414" class="Symbol">→</a> <a id="1416" href="README.Data.Container.Indexed.html#1341" class="Datatype">VecC</a> <a id="1421" class="Symbol">(</a><a id="1422" href="Agda.Builtin.Nat.html#217" class="InductiveConstructor">suc</a> <a id="1426" href="README.Data.Container.Indexed.html#1408" class="Bound">n</a><a id="1427" class="Symbol">)</a>

  <a id="1432" href="README.Data.Container.Indexed.html#1432" class="Function">Vec</a> <a id="1436" class="Symbol">:</a> <a id="1438" href="Data.Container.Indexed.Core.html#387" class="Record">Container</a> <a id="1448" href="Agda.Builtin.Nat.html#186" class="Datatype">ℕ</a> <a id="1450" href="Agda.Builtin.Nat.html#186" class="Datatype">ℕ</a> <a id="1452" href="README.Data.Container.Indexed.html#573" class="Bound">a</a> <a id="1454" class="Symbol">_</a>
  <a id="1458" href="Data.Container.Indexed.Core.html#526" class="Field">Command</a> <a id="1466" href="README.Data.Container.Indexed.html#1432" class="Function">Vec</a> <a id="1470" class="Symbol">=</a> <a id="1472" href="README.Data.Container.Indexed.html#1341" class="Datatype">VecC</a>

<a id="1478" class="Comment">-- We then treat each command independently, specifying both the response and the</a>
<a id="1560" class="Comment">-- next index based on that response.</a>

<a id="1599" class="Comment">-- In the nil case, the response is the empty type: there won&#39;t be any tail. As</a>
<a id="1679" class="Comment">-- a consequence, the next index won&#39;t be needed (and we can rely on the fact the</a>
<a id="1761" class="Comment">-- user will never be able to call it).</a>

  <a id="1804" href="Data.Container.Indexed.Core.html#557" class="Field">Response</a> <a id="1813" href="README.Data.Container.Indexed.html#1432" class="Function">Vec</a> <a id="1817" href="README.Data.Container.Indexed.html#1368" class="InductiveConstructor">nil</a> <a id="1821" class="Symbol">=</a> <a id="1823" href="Data.Empty.html#895" class="Function">⊥</a>
  <a id="1827" href="Data.Container.Indexed.Core.html#598" class="Field">next</a>     <a id="1836" href="README.Data.Container.Indexed.html#1432" class="Function">Vec</a> <a id="1840" href="README.Data.Container.Indexed.html#1368" class="InductiveConstructor">nil</a> <a id="1844" class="Symbol">=</a> <a id="1846" class="Symbol">λ</a> <a id="1848" class="Symbol">()</a>

<a id="1852" class="Comment">-- In the cons case, the response is the unit type: there is exactly one tail. The</a>
<a id="1935" class="Comment">-- next index is the predecessor of the current one. It is handily handed over to</a>
<a id="2017" class="Comment">-- use by `cons`.</a>

  <a id="2038" class="Comment">-- cons</a>
  <a id="2048" href="Data.Container.Indexed.Core.html#557" class="Field">Response</a> <a id="2057" href="README.Data.Container.Indexed.html#1432" class="Function">Vec</a> <a id="2061" class="Symbol">(</a><a id="2062" href="README.Data.Container.Indexed.html#1399" class="InductiveConstructor">cons</a> <a id="2067" href="README.Data.Container.Indexed.html#2067" class="Bound">n</a> <a id="2069" href="README.Data.Container.Indexed.html#2069" class="Bound">a</a><a id="2070" class="Symbol">)</a> <a id="2072" class="Symbol">=</a> <a id="2074" href="Agda.Builtin.Unit.html#158" class="Record">⊤</a>
  <a id="2078" href="Data.Container.Indexed.Core.html#598" class="Field">next</a>     <a id="2087" href="README.Data.Container.Indexed.html#1432" class="Function">Vec</a> <a id="2091" class="Symbol">(</a><a id="2092" href="README.Data.Container.Indexed.html#1399" class="InductiveConstructor">cons</a> <a id="2097" href="README.Data.Container.Indexed.html#2097" class="Bound">n</a> <a id="2099" href="README.Data.Container.Indexed.html#2099" class="Bound">a</a><a id="2100" class="Symbol">)</a> <a id="2102" class="Symbol">=</a> <a id="2104" class="Symbol">λ</a> <a id="2106" href="README.Data.Container.Indexed.html#2106" class="Bound">_</a> <a id="2108" class="Symbol">→</a> <a id="2110" href="README.Data.Container.Indexed.html#2097" class="Bound">n</a>

<a id="2113" class="Comment">-- Finally we can define the type of Vector as the least fixed point of Vec.</a>

  <a id="2193" href="README.Data.Container.Indexed.html#2193" class="Function">Vector</a> <a id="2200" class="Symbol">:</a> <a id="2202" href="Agda.Builtin.Nat.html#186" class="Datatype">ℕ</a> <a id="2204" class="Symbol">→</a> <a id="2206" href="Agda.Primitive.html#320" class="Primitive">Set</a> <a id="2210" href="README.Data.Container.Indexed.html#573" class="Bound">a</a>
  <a id="2214" href="README.Data.Container.Indexed.html#2193" class="Function">Vector</a> <a id="2221" class="Symbol">=</a> <a id="2223" href="Data.Container.Indexed.html#1265" class="Function">μ</a> <a id="2225" href="README.Data.Container.Indexed.html#1432" class="Function">Vec</a>

<a id="2230" class="Keyword">module</a> <a id="2237" href="README.Data.Container.Indexed.html#2237" class="Module">_</a> <a id="2239" class="Symbol">{</a><a id="2240" href="README.Data.Container.Indexed.html#2240" class="Bound">a</a><a id="2241" class="Symbol">}</a> <a id="2243" class="Symbol">{</a><a id="2244" href="README.Data.Container.Indexed.html#2244" class="Bound">A</a> <a id="2246" class="Symbol">:</a> <a id="2248" href="Agda.Primitive.html#320" class="Primitive">Set</a> <a id="2252" href="README.Data.Container.Indexed.html#2240" class="Bound">a</a><a id="2253" class="Symbol">}</a> <a id="2255" class="Keyword">where</a>

<a id="2262" class="Comment">-- We can recover the usual constructors by using `sup` to enter the fixpoint</a>
<a id="2340" class="Comment">-- and then using the appropriate pairing of a command &amp; a handler for the</a>
<a id="2415" class="Comment">-- response.</a>

<a id="2429" class="Comment">-- For [], the response is ⊥ which makes it easy to conclude.</a>

  <a id="2494" href="README.Data.Container.Indexed.html#2494" class="Function">[]</a> <a id="2497" class="Symbol">:</a> <a id="2499" href="README.Data.Container.Indexed.html#2193" class="Function">Vector</a> <a id="2506" href="README.Data.Container.Indexed.html#2244" class="Bound">A</a> <a id="2508" class="Number">0</a>
  <a id="2512" href="README.Data.Container.Indexed.html#2494" class="Function">[]</a> <a id="2515" class="Symbol">=</a> <a id="2517" href="Data.W.Indexed.html#570" class="InductiveConstructor">sup</a> <a id="2521" class="Symbol">(</a><a id="2522" href="README.Data.Container.Indexed.html#1368" class="InductiveConstructor">nil</a> <a id="2526" href="Agda.Builtin.Sigma.html#218" class="InductiveConstructor Operator">,</a> <a id="2528" class="Symbol">λ</a> <a id="2530" class="Symbol">())</a>

<a id="2535" class="Comment">-- For _∷_, the response is ⊤ so we need to pass a tail. We give the one we took</a>
<a id="2616" class="Comment">-- as an argument.</a>

  <a id="2638" class="Keyword">infixr</a> <a id="2645" class="Number">3</a> <a id="2647" href="README.Data.Container.Indexed.html#2653" class="Function Operator">_∷_</a>
  <a id="2653" href="README.Data.Container.Indexed.html#2653" class="Function Operator">_∷_</a> <a id="2657" class="Symbol">:</a> <a id="2659" class="Symbol">∀</a> <a id="2661" class="Symbol">{</a><a id="2662" href="README.Data.Container.Indexed.html#2662" class="Bound">n</a><a id="2663" class="Symbol">}</a> <a id="2665" class="Symbol">→</a> <a id="2667" href="README.Data.Container.Indexed.html#2244" class="Bound">A</a> <a id="2669" class="Symbol">→</a> <a id="2671" href="README.Data.Container.Indexed.html#2193" class="Function">Vector</a> <a id="2678" href="README.Data.Container.Indexed.html#2244" class="Bound">A</a> <a id="2680" href="README.Data.Container.Indexed.html#2662" class="Bound">n</a> <a id="2682" class="Symbol">→</a> <a id="2684" href="README.Data.Container.Indexed.html#2193" class="Function">Vector</a> <a id="2691" href="README.Data.Container.Indexed.html#2244" class="Bound">A</a> <a id="2693" class="Symbol">(</a><a id="2694" href="Agda.Builtin.Nat.html#217" class="InductiveConstructor">suc</a> <a id="2698" href="README.Data.Container.Indexed.html#2662" class="Bound">n</a><a id="2699" class="Symbol">)</a>
  <a id="2703" href="README.Data.Container.Indexed.html#2703" class="Bound">x</a> <a id="2705" href="README.Data.Container.Indexed.html#2653" class="Function Operator">∷</a> <a id="2707" href="README.Data.Container.Indexed.html#2707" class="Bound">xs</a> <a id="2710" class="Symbol">=</a> <a id="2712" href="Data.W.Indexed.html#570" class="InductiveConstructor">sup</a> <a id="2716" class="Symbol">(</a><a id="2717" href="README.Data.Container.Indexed.html#1399" class="InductiveConstructor">cons</a> <a id="2722" class="Symbol">_</a> <a id="2724" href="README.Data.Container.Indexed.html#2703" class="Bound">x</a> <a id="2726" href="Agda.Builtin.Sigma.html#218" class="InductiveConstructor Operator">,</a> <a id="2728" class="Symbol">λ</a> <a id="2730" href="README.Data.Container.Indexed.html#2730" class="Bound">_</a> <a id="2732" class="Symbol">→</a> <a id="2734" href="README.Data.Container.Indexed.html#2707" class="Bound">xs</a><a id="2736" class="Symbol">)</a>

<a id="2739" class="Comment">-- We can now use these constructors to build up vectors:</a>

<a id="1⋯3"></a><a id="2798" href="README.Data.Container.Indexed.html#2798" class="Function">1⋯3</a> <a id="2802" class="Symbol">:</a> <a id="2804" href="README.Data.Container.Indexed.html#2193" class="Function">Vector</a> <a id="2811" href="Agda.Builtin.Nat.html#186" class="Datatype">ℕ</a> <a id="2813" class="Number">3</a>
<a id="2815" href="README.Data.Container.Indexed.html#2798" class="Function">1⋯3</a> <a id="2819" class="Symbol">=</a> <a id="2821" class="Number">1</a> <a id="2823" href="README.Data.Container.Indexed.html#2653" class="Function Operator">∷</a> <a id="2825" class="Number">2</a> <a id="2827" href="README.Data.Container.Indexed.html#2653" class="Function Operator">∷</a> <a id="2829" class="Number">3</a> <a id="2831" href="README.Data.Container.Indexed.html#2653" class="Function Operator">∷</a> <a id="2833" href="README.Data.Container.Indexed.html#2494" class="Function">[]</a>





<a id="2841" class="Comment">-- Horrible thing to check the definition of _∈_ is not buggy.</a>
<a id="2904" class="Comment">-- Not sure whether we can say anything interesting about it in the case of Vector...</a>

<a id="2991" class="Keyword">open</a> <a id="2996" class="Keyword">import</a> <a id="3003" href="Relation.Binary.HeterogeneousEquality.html" class="Module">Relation.Binary.HeterogeneousEquality</a>

<a id="3042" href="README.Data.Container.Indexed.html#3042" class="Function">_</a> <a id="3044" class="Symbol">:</a> <a id="3046" href="Data.Container.Indexed.WithK.html#6691" class="Function Operator">_∈_</a> <a id="3050" class="Symbol">{</a><a id="3051" class="Argument">C</a> <a id="3053" class="Symbol">=</a> <a id="3055" href="README.Data.Container.Indexed.html#1432" class="Function">Vec</a> <a id="3059" href="Agda.Builtin.Nat.html#186" class="Datatype">ℕ</a><a id="3060" class="Symbol">}</a> <a id="3062" class="Symbol">{</a><a id="3063" class="Argument">X</a> <a id="3065" class="Symbol">=</a> <a id="3067" href="README.Data.Container.Indexed.html#2193" class="Function">Vector</a> <a id="3074" href="Agda.Builtin.Nat.html#186" class="Datatype">ℕ</a><a id="3075" class="Symbol">}</a> <a id="3077" href="README.Data.Container.Indexed.html#2798" class="Function">1⋯3</a> <a id="3081" class="Symbol">(</a><a id="3082" href="Data.Container.Indexed.Core.html#706" class="Function Operator">⟦</a> <a id="3084" href="README.Data.Container.Indexed.html#1432" class="Function">Vec</a> <a id="3088" href="Agda.Builtin.Nat.html#186" class="Datatype">ℕ</a> <a id="3090" href="Data.Container.Indexed.Core.html#706" class="Function Operator">⟧</a> <a id="3092" class="Symbol">(</a><a id="3093" href="README.Data.Container.Indexed.html#2193" class="Function">Vector</a> <a id="3100" href="Agda.Builtin.Nat.html#186" class="Datatype">ℕ</a><a id="3101" class="Symbol">)</a> <a id="3103" class="Number">4</a> <a id="3105" href="Function.Base.html#4503" class="Function Operator">∋</a> <a id="3107" href="README.Data.Container.Indexed.html#1399" class="InductiveConstructor">cons</a> <a id="3112" class="Symbol">_</a> <a id="3114" class="Number">0</a> <a id="3116" href="Agda.Builtin.Sigma.html#218" class="InductiveConstructor Operator">,</a> <a id="3118" class="Symbol">λ</a> <a id="3120" href="README.Data.Container.Indexed.html#3120" class="Bound">_</a> <a id="3122" class="Symbol">→</a> <a id="3124" href="README.Data.Container.Indexed.html#2798" class="Function">1⋯3</a><a id="3127" class="Symbol">)</a>
<a id="3129" class="Symbol">_</a> <a id="3131" class="Symbol">=</a> <a id="3133" class="Symbol">_</a> <a id="3135" href="Agda.Builtin.Sigma.html#218" class="InductiveConstructor Operator">,</a> <a id="3137" href="Relation.Binary.HeterogeneousEquality.Core.html#740" class="InductiveConstructor">refl</a>
</pre></body></html>