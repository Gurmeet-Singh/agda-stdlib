<!DOCTYPE HTML>
<html><head><meta charset="utf-8"><title>Data.Tree.AVL.NonEmpty</title><link rel="stylesheet" href="Agda.css"></head><body><pre class="Agda"><a id="1" class="Comment">------------------------------------------------------------------------</a>
<a id="74" class="Comment">-- The Agda standard library</a>
<a id="103" class="Comment">--</a>
<a id="106" class="Comment">-- Non-empty AVL trees</a>
<a id="129" class="Comment">------------------------------------------------------------------------</a>

<a id="203" class="Comment">-- AVL trees are balanced binary search trees.</a>

<a id="251" class="Comment">-- The search tree invariant is specified using the technique</a>
<a id="313" class="Comment">-- described by Conor McBride in his talk &quot;Pivotal pragmatism&quot;.</a>

<a id="378" class="Symbol">{-#</a> <a id="382" class="Keyword">OPTIONS</a> <a id="390" class="Pragma">--cubical-compatible</a> <a id="411" class="Pragma">--safe</a> <a id="418" class="Symbol">#-}</a>

<a id="423" class="Keyword">open</a> <a id="428" class="Keyword">import</a> <a id="435" href="Relation.Binary.html" class="Module">Relation.Binary</a> <a id="451" class="Keyword">using</a> <a id="457" class="Symbol">(</a><a id="458" href="Relation.Binary.Bundles.html#6960" class="Record">StrictTotalOrder</a><a id="474" class="Symbol">)</a>

<a id="477" class="Keyword">module</a> <a id="484" href="Data.Tree.AVL.NonEmpty.html" class="Module">Data.Tree.AVL.NonEmpty</a>
  <a id="509" class="Symbol">{</a><a id="510" href="Data.Tree.AVL.NonEmpty.html#510" class="Bound">a</a> <a id="512" href="Data.Tree.AVL.NonEmpty.html#512" class="Bound">ℓ₁</a> <a id="515" href="Data.Tree.AVL.NonEmpty.html#515" class="Bound">ℓ₂</a><a id="517" class="Symbol">}</a> <a id="519" class="Symbol">(</a><a id="520" href="Data.Tree.AVL.NonEmpty.html#520" class="Bound">strictTotalOrder</a> <a id="537" class="Symbol">:</a> <a id="539" href="Relation.Binary.Bundles.html#6960" class="Record">StrictTotalOrder</a> <a id="556" href="Data.Tree.AVL.NonEmpty.html#510" class="Bound">a</a> <a id="558" href="Data.Tree.AVL.NonEmpty.html#512" class="Bound">ℓ₁</a> <a id="561" href="Data.Tree.AVL.NonEmpty.html#515" class="Bound">ℓ₂</a><a id="563" class="Symbol">)</a> <a id="565" class="Keyword">where</a>

<a id="572" class="Keyword">open</a> <a id="577" class="Keyword">import</a> <a id="584" href="Data.Bool.Base.html" class="Module">Data.Bool.Base</a> <a id="599" class="Keyword">using</a> <a id="605" class="Symbol">(</a><a id="606" href="Agda.Builtin.Bool.html#156" class="Datatype">Bool</a><a id="610" class="Symbol">)</a>
<a id="612" class="Keyword">open</a> <a id="617" class="Keyword">import</a> <a id="624" href="Data.Empty.html" class="Module">Data.Empty</a>
<a id="635" class="Keyword">open</a> <a id="640" class="Keyword">import</a> <a id="647" href="Data.List.NonEmpty.html" class="Module">Data.List.NonEmpty</a> <a id="666" class="Symbol">as</a> <a id="669" class="Module">List⁺</a> <a id="675" class="Keyword">using</a> <a id="681" class="Symbol">(</a><a id="682" href="Data.List.NonEmpty.Base.html#1281" class="Record">List⁺</a><a id="687" class="Symbol">;</a> <a id="689" href="Data.List.NonEmpty.Base.html#1327" class="InductiveConstructor Operator">_∷_</a><a id="692" class="Symbol">;</a> <a id="694" href="Data.List.NonEmpty.Base.html#3563" class="Function Operator">_++⁺_</a><a id="699" class="Symbol">)</a>
<a id="701" class="Keyword">open</a> <a id="706" class="Keyword">import</a> <a id="713" href="Data.Maybe.Base.html" class="Module">Data.Maybe.Base</a> <a id="729" class="Keyword">hiding</a> <a id="736" class="Symbol">(</a><a id="737" href="Data.Maybe.Base.html#2049" class="Function">map</a><a id="740" class="Symbol">)</a>
<a id="742" class="Keyword">open</a> <a id="747" class="Keyword">import</a> <a id="754" href="Data.Nat.Base.html" class="Module">Data.Nat.Base</a> <a id="768" class="Keyword">hiding</a> <a id="775" class="Symbol">(</a><a id="776" href="Data.Nat.Base.html#1654" class="Function Operator">_&lt;_</a><a id="779" class="Symbol">;</a> <a id="781" href="Data.Nat.Base.html#3493" class="Function Operator">_⊔_</a><a id="784" class="Symbol">;</a> <a id="786" href="Data.Nat.Base.html#7112" class="Function">compare</a><a id="793" class="Symbol">)</a>
<a id="795" class="Keyword">open</a> <a id="800" class="Keyword">import</a> <a id="807" href="Data.Product.html" class="Module">Data.Product</a> <a id="820" class="Keyword">hiding</a> <a id="827" class="Symbol">(</a><a id="828" href="Data.Product.Base.html#2173" class="Function">map</a><a id="831" class="Symbol">)</a>
<a id="833" class="Keyword">open</a> <a id="838" class="Keyword">import</a> <a id="845" href="Data.Unit.html" class="Module">Data.Unit</a>
<a id="855" class="Keyword">open</a> <a id="860" class="Keyword">import</a> <a id="867" href="Function.Base.html" class="Module">Function.Base</a> <a id="881" class="Keyword">using</a> <a id="887" class="Symbol">(</a><a id="888" href="Function.Base.html#2003" class="Function Operator">_$_</a><a id="891" class="Symbol">;</a> <a id="893" href="Function.Base.html#3646" class="Function Operator">_∘′_</a><a id="897" class="Symbol">)</a>
<a id="899" class="Keyword">open</a> <a id="904" class="Keyword">import</a> <a id="911" href="Level.html" class="Module">Level</a> <a id="917" class="Keyword">using</a> <a id="923" class="Symbol">(</a><a id="924" href="Agda.Primitive.html#804" class="Primitive Operator">_⊔_</a><a id="927" class="Symbol">;</a> <a id="929" href="Level.html#409" class="Record">Lift</a><a id="933" class="Symbol">;</a> <a id="935" href="Level.html#466" class="InductiveConstructor">lift</a><a id="939" class="Symbol">)</a>
<a id="941" class="Keyword">open</a> <a id="946" class="Keyword">import</a> <a id="953" href="Relation.Unary.html" class="Module">Relation.Unary</a>

<a id="969" class="Keyword">open</a> <a id="974" href="Relation.Binary.Bundles.html#6960" class="Module">StrictTotalOrder</a> <a id="991" href="Data.Tree.AVL.NonEmpty.html#520" class="Bound">strictTotalOrder</a> <a id="1008" class="Keyword">renaming</a> <a id="1017" class="Symbol">(</a><a id="1018" href="Relation.Binary.Bundles.html#7047" class="Field">Carrier</a> <a id="1026" class="Symbol">to</a> <a id="1029" class="Field">Key</a><a id="1032" class="Symbol">)</a>
<a id="1034" class="Keyword">open</a> <a id="1039" class="Keyword">import</a> <a id="1046" href="Data.Tree.AVL.Value.html" class="Module">Data.Tree.AVL.Value</a> <a id="1066" href="Relation.Binary.Bundles.html#4501" class="Function">Eq.setoid</a>
<a id="1076" class="Keyword">import</a> <a id="1083" href="Data.Tree.AVL.Indexed.html" class="Module">Data.Tree.AVL.Indexed</a> <a id="1105" href="Data.Tree.AVL.NonEmpty.html#520" class="Bound">strictTotalOrder</a> as <a id="Indexed"></a><a id="1125" href="Data.Tree.AVL.NonEmpty.html#1125" class="Module">Indexed</a>
<a id="1133" class="Keyword">open</a> <a id="1138" href="Data.Tree.AVL.NonEmpty.html#1125" class="Module">Indexed</a> <a id="1146" class="Keyword">using</a> <a id="1152" class="Symbol">(</a><a id="1153" href="Data.Tree.AVL.Key.html#1234" class="InductiveConstructor">⊥⁺</a><a id="1155" class="Symbol">;</a> <a id="1157" href="Data.Tree.AVL.Key.html#1247" class="InductiveConstructor">⊤⁺</a><a id="1159" class="Symbol">;</a> <a id="1161" href="Data.Tree.AVL.Key.html#1712" class="InductiveConstructor">⊥⁺&lt;⊤⁺</a><a id="1166" class="Symbol">;</a> <a id="1168" href="Data.Tree.AVL.Key.html#1909" class="Function Operator">⊥⁺&lt;[_]&lt;⊤⁺</a><a id="1177" class="Symbol">;</a> <a id="1179" href="Data.Tree.AVL.Key.html#1753" class="InductiveConstructor Operator">⊥⁺&lt;[_]</a><a id="1185" class="Symbol">;</a> <a id="1187" href="Data.Tree.AVL.Key.html#1732" class="InductiveConstructor Operator">[_]&lt;⊤⁺</a><a id="1193" class="Symbol">;</a> <a id="1195" href="Data.Tree.AVL.Indexed.html#1702" class="InductiveConstructor">node</a><a id="1199" class="Symbol">;</a> <a id="1201" href="Data.Tree.AVL.Indexed.html#10880" class="Function">toList</a><a id="1207" class="Symbol">)</a>

<a id="1210" class="Comment">------------------------------------------------------------------------</a>
<a id="1283" class="Comment">-- Types and functions with hidden indices</a>

<a id="1327" class="Comment">-- NB: the height is non-zero thus guaranteeing that the AVL tree</a>
<a id="1393" class="Comment">-- contains at least one value.</a>

<a id="1426" class="Keyword">data</a> <a id="Tree⁺"></a><a id="1431" href="Data.Tree.AVL.NonEmpty.html#1431" class="Datatype">Tree⁺</a> <a id="1437" class="Symbol">{</a><a id="1438" href="Data.Tree.AVL.NonEmpty.html#1438" class="Bound">v</a><a id="1439" class="Symbol">}</a> <a id="1441" class="Symbol">(</a><a id="1442" href="Data.Tree.AVL.NonEmpty.html#1442" class="Bound">V</a> <a id="1444" class="Symbol">:</a> <a id="1446" href="Data.Tree.AVL.Value.html#654" class="Record">Value</a> <a id="1452" href="Data.Tree.AVL.NonEmpty.html#1438" class="Bound">v</a><a id="1453" class="Symbol">)</a> <a id="1455" class="Symbol">:</a> <a id="1457" href="Agda.Primitive.html#320" class="Primitive">Set</a> <a id="1461" class="Symbol">(</a><a id="1462" href="Data.Tree.AVL.NonEmpty.html#510" class="Bound">a</a> <a id="1464" href="Agda.Primitive.html#804" class="Primitive Operator">⊔</a> <a id="1466" href="Data.Tree.AVL.NonEmpty.html#1438" class="Bound">v</a> <a id="1468" href="Agda.Primitive.html#804" class="Primitive Operator">⊔</a> <a id="1470" href="Data.Tree.AVL.NonEmpty.html#515" class="Bound">ℓ₂</a><a id="1472" class="Symbol">)</a> <a id="1474" class="Keyword">where</a>
  <a id="Tree⁺.tree"></a><a id="1482" href="Data.Tree.AVL.NonEmpty.html#1482" class="InductiveConstructor">tree</a> <a id="1487" class="Symbol">:</a> <a id="1489" class="Symbol">∀</a> <a id="1491" class="Symbol">{</a><a id="1492" href="Data.Tree.AVL.NonEmpty.html#1492" class="Bound">h</a><a id="1493" class="Symbol">}</a> <a id="1495" class="Symbol">→</a> <a id="1497" href="Data.Tree.AVL.Indexed.html#1596" class="Datatype">Indexed.Tree</a> <a id="1510" href="Data.Tree.AVL.NonEmpty.html#1442" class="Bound">V</a> <a id="1512" href="Data.Tree.AVL.Key.html#1234" class="InductiveConstructor">⊥⁺</a> <a id="1515" href="Data.Tree.AVL.Key.html#1247" class="InductiveConstructor">⊤⁺</a> <a id="1518" class="Symbol">(</a><a id="1519" href="Agda.Builtin.Nat.html#217" class="InductiveConstructor">suc</a> <a id="1523" href="Data.Tree.AVL.NonEmpty.html#1492" class="Bound">h</a><a id="1524" class="Symbol">)</a> <a id="1526" class="Symbol">→</a> <a id="1528" href="Data.Tree.AVL.NonEmpty.html#1431" class="Datatype">Tree⁺</a> <a id="1534" href="Data.Tree.AVL.NonEmpty.html#1442" class="Bound">V</a>

<a id="1537" class="Keyword">module</a> <a id="1544" href="Data.Tree.AVL.NonEmpty.html#1544" class="Module">_</a> <a id="1546" class="Symbol">{</a><a id="1547" href="Data.Tree.AVL.NonEmpty.html#1547" class="Bound">v</a><a id="1548" class="Symbol">}</a> <a id="1550" class="Symbol">{</a><a id="1551" href="Data.Tree.AVL.NonEmpty.html#1551" class="Bound">V</a> <a id="1553" class="Symbol">:</a> <a id="1555" href="Data.Tree.AVL.Value.html#654" class="Record">Value</a> <a id="1561" href="Data.Tree.AVL.NonEmpty.html#1547" class="Bound">v</a><a id="1562" class="Symbol">}</a> <a id="1564" class="Keyword">where</a>

  <a id="1573" class="Keyword">private</a>
    <a id="1585" href="Data.Tree.AVL.NonEmpty.html#1585" class="Function">Val</a> <a id="1589" class="Symbol">=</a> <a id="1591" href="Data.Tree.AVL.Value.html#724" class="Field">Value.family</a> <a id="1604" href="Data.Tree.AVL.NonEmpty.html#1551" class="Bound">V</a>

  <a id="1609" href="Data.Tree.AVL.NonEmpty.html#1609" class="Function">singleton</a> <a id="1619" class="Symbol">:</a> <a id="1621" class="Symbol">(</a><a id="1622" href="Data.Tree.AVL.NonEmpty.html#1622" class="Bound">k</a> <a id="1624" class="Symbol">:</a> <a id="1626" href="Data.Tree.AVL.NonEmpty.html#1029" class="Field">Key</a><a id="1629" class="Symbol">)</a> <a id="1631" class="Symbol">→</a> <a id="1633" href="Data.Tree.AVL.NonEmpty.html#1585" class="Function">Val</a> <a id="1637" href="Data.Tree.AVL.NonEmpty.html#1622" class="Bound">k</a> <a id="1639" class="Symbol">→</a> <a id="1641" href="Data.Tree.AVL.NonEmpty.html#1431" class="Datatype">Tree⁺</a> <a id="1647" href="Data.Tree.AVL.NonEmpty.html#1551" class="Bound">V</a>
  <a id="1651" href="Data.Tree.AVL.NonEmpty.html#1609" class="Function">singleton</a> <a id="1661" href="Data.Tree.AVL.NonEmpty.html#1661" class="Bound">k</a> <a id="1663" href="Data.Tree.AVL.NonEmpty.html#1663" class="Bound">v</a> <a id="1665" class="Symbol">=</a> <a id="1667" href="Data.Tree.AVL.NonEmpty.html#1482" class="InductiveConstructor">tree</a> <a id="1672" class="Symbol">(</a><a id="1673" href="Data.Tree.AVL.Indexed.html#8188" class="Function">Indexed.singleton</a> <a id="1691" href="Data.Tree.AVL.NonEmpty.html#1661" class="Bound">k</a> <a id="1693" href="Data.Tree.AVL.NonEmpty.html#1663" class="Bound">v</a> <a id="1695" href="Data.Tree.AVL.Key.html#1909" class="Function Operator">⊥⁺&lt;[</a> <a id="1700" href="Data.Tree.AVL.NonEmpty.html#1661" class="Bound">k</a> <a id="1702" href="Data.Tree.AVL.Key.html#1909" class="Function Operator">]&lt;⊤⁺</a><a id="1706" class="Symbol">)</a>

  <a id="1711" href="Data.Tree.AVL.NonEmpty.html#1711" class="Function">insert</a> <a id="1718" class="Symbol">:</a> <a id="1720" class="Symbol">(</a><a id="1721" href="Data.Tree.AVL.NonEmpty.html#1721" class="Bound">k</a> <a id="1723" class="Symbol">:</a> <a id="1725" href="Data.Tree.AVL.NonEmpty.html#1029" class="Field">Key</a><a id="1728" class="Symbol">)</a> <a id="1730" class="Symbol">→</a> <a id="1732" href="Data.Tree.AVL.NonEmpty.html#1585" class="Function">Val</a> <a id="1736" href="Data.Tree.AVL.NonEmpty.html#1721" class="Bound">k</a> <a id="1738" class="Symbol">→</a> <a id="1740" href="Data.Tree.AVL.NonEmpty.html#1431" class="Datatype">Tree⁺</a> <a id="1746" href="Data.Tree.AVL.NonEmpty.html#1551" class="Bound">V</a> <a id="1748" class="Symbol">→</a> <a id="1750" href="Data.Tree.AVL.NonEmpty.html#1431" class="Datatype">Tree⁺</a> <a id="1756" href="Data.Tree.AVL.NonEmpty.html#1551" class="Bound">V</a>
  <a id="1760" href="Data.Tree.AVL.NonEmpty.html#1711" class="Function">insert</a> <a id="1767" href="Data.Tree.AVL.NonEmpty.html#1767" class="Bound">k</a> <a id="1769" href="Data.Tree.AVL.NonEmpty.html#1769" class="Bound">v</a> <a id="1771" class="Symbol">(</a><a id="1772" href="Data.Tree.AVL.NonEmpty.html#1482" class="InductiveConstructor">tree</a> <a id="1777" href="Data.Tree.AVL.NonEmpty.html#1777" class="Bound">t</a><a id="1778" class="Symbol">)</a> <a id="1780" class="Keyword">with</a> <a id="1785" href="Data.Tree.AVL.Indexed.html#9306" class="Function">Indexed.insert</a> <a id="1800" href="Data.Tree.AVL.NonEmpty.html#1767" class="Bound">k</a> <a id="1802" href="Data.Tree.AVL.NonEmpty.html#1769" class="Bound">v</a> <a id="1804" href="Data.Tree.AVL.NonEmpty.html#1777" class="Bound">t</a> <a id="1806" href="Data.Tree.AVL.Key.html#1909" class="Function Operator">⊥⁺&lt;[</a> <a id="1811" href="Data.Tree.AVL.NonEmpty.html#1767" class="Bound">k</a> <a id="1813" href="Data.Tree.AVL.Key.html#1909" class="Function Operator">]&lt;⊤⁺</a>
  <a id="1820" class="Symbol">...</a> <a id="1824" class="Symbol">|</a> <a id="1826" href="Data.Tree.AVL.Height.html#444" class="InductiveConstructor">Indexed.0#</a> <a id="1837" href="Agda.Builtin.Sigma.html#218" class="InductiveConstructor Operator">,</a> <a id="1839" href="Data.Tree.AVL.NonEmpty.html#1839" class="Bound">t′</a> <a id="1842" class="Symbol">=</a> <a id="1844" href="Data.Tree.AVL.NonEmpty.html#1482" class="InductiveConstructor">tree</a> <a id="1849" href="Data.Tree.AVL.NonEmpty.html#1839" class="Bound">t′</a>
  <a id="1854" class="Symbol">...</a> <a id="1858" class="Symbol">|</a> <a id="1860" href="Data.Tree.AVL.Height.html#462" class="InductiveConstructor">Indexed.1#</a> <a id="1871" href="Agda.Builtin.Sigma.html#218" class="InductiveConstructor Operator">,</a> <a id="1873" href="Data.Tree.AVL.NonEmpty.html#1873" class="Bound">t′</a> <a id="1876" class="Symbol">=</a> <a id="1878" href="Data.Tree.AVL.NonEmpty.html#1482" class="InductiveConstructor">tree</a> <a id="1883" href="Data.Tree.AVL.NonEmpty.html#1873" class="Bound">t′</a>

  <a id="1889" href="Data.Tree.AVL.NonEmpty.html#1889" class="Function">insertWith</a> <a id="1900" class="Symbol">:</a> <a id="1902" class="Symbol">(</a><a id="1903" href="Data.Tree.AVL.NonEmpty.html#1903" class="Bound">k</a> <a id="1905" class="Symbol">:</a> <a id="1907" href="Data.Tree.AVL.NonEmpty.html#1029" class="Field">Key</a><a id="1910" class="Symbol">)</a> <a id="1912" class="Symbol">→</a> <a id="1914" class="Symbol">(</a><a id="1915" href="Agda.Builtin.Maybe.html#118" class="Datatype">Maybe</a> <a id="1921" class="Symbol">(</a><a id="1922" href="Data.Tree.AVL.NonEmpty.html#1585" class="Function">Val</a> <a id="1926" href="Data.Tree.AVL.NonEmpty.html#1903" class="Bound">k</a><a id="1927" class="Symbol">)</a> <a id="1929" class="Symbol">→</a> <a id="1931" href="Data.Tree.AVL.NonEmpty.html#1585" class="Function">Val</a> <a id="1935" href="Data.Tree.AVL.NonEmpty.html#1903" class="Bound">k</a><a id="1936" class="Symbol">)</a> <a id="1938" class="Symbol">→</a> <a id="1940" href="Data.Tree.AVL.NonEmpty.html#1431" class="Datatype">Tree⁺</a> <a id="1946" href="Data.Tree.AVL.NonEmpty.html#1551" class="Bound">V</a> <a id="1948" class="Symbol">→</a> <a id="1950" href="Data.Tree.AVL.NonEmpty.html#1431" class="Datatype">Tree⁺</a> <a id="1956" href="Data.Tree.AVL.NonEmpty.html#1551" class="Bound">V</a>
  <a id="1960" href="Data.Tree.AVL.NonEmpty.html#1889" class="Function">insertWith</a> <a id="1971" href="Data.Tree.AVL.NonEmpty.html#1971" class="Bound">k</a> <a id="1973" href="Data.Tree.AVL.NonEmpty.html#1973" class="Bound">f</a> <a id="1975" class="Symbol">(</a><a id="1976" href="Data.Tree.AVL.NonEmpty.html#1482" class="InductiveConstructor">tree</a> <a id="1981" href="Data.Tree.AVL.NonEmpty.html#1981" class="Bound">t</a><a id="1982" class="Symbol">)</a> <a id="1984" class="Keyword">with</a> <a id="1989" href="Data.Tree.AVL.Indexed.html#8554" class="Function">Indexed.insertWith</a> <a id="2008" href="Data.Tree.AVL.NonEmpty.html#1971" class="Bound">k</a> <a id="2010" href="Data.Tree.AVL.NonEmpty.html#1973" class="Bound">f</a> <a id="2012" href="Data.Tree.AVL.NonEmpty.html#1981" class="Bound">t</a> <a id="2014" href="Data.Tree.AVL.Key.html#1909" class="Function Operator">⊥⁺&lt;[</a> <a id="2019" href="Data.Tree.AVL.NonEmpty.html#1971" class="Bound">k</a> <a id="2021" href="Data.Tree.AVL.Key.html#1909" class="Function Operator">]&lt;⊤⁺</a>
  <a id="2028" class="Symbol">...</a> <a id="2032" class="Symbol">|</a> <a id="2034" href="Data.Tree.AVL.Height.html#444" class="InductiveConstructor">Indexed.0#</a> <a id="2045" href="Agda.Builtin.Sigma.html#218" class="InductiveConstructor Operator">,</a> <a id="2047" href="Data.Tree.AVL.NonEmpty.html#2047" class="Bound">t′</a> <a id="2050" class="Symbol">=</a> <a id="2052" href="Data.Tree.AVL.NonEmpty.html#1482" class="InductiveConstructor">tree</a> <a id="2057" href="Data.Tree.AVL.NonEmpty.html#2047" class="Bound">t′</a>
  <a id="2062" class="Symbol">...</a> <a id="2066" class="Symbol">|</a> <a id="2068" href="Data.Tree.AVL.Height.html#462" class="InductiveConstructor">Indexed.1#</a> <a id="2079" href="Agda.Builtin.Sigma.html#218" class="InductiveConstructor Operator">,</a> <a id="2081" href="Data.Tree.AVL.NonEmpty.html#2081" class="Bound">t′</a> <a id="2084" class="Symbol">=</a> <a id="2086" href="Data.Tree.AVL.NonEmpty.html#1482" class="InductiveConstructor">tree</a> <a id="2091" href="Data.Tree.AVL.NonEmpty.html#2081" class="Bound">t′</a>

  <a id="2097" href="Data.Tree.AVL.NonEmpty.html#2097" class="Function">delete</a> <a id="2104" class="Symbol">:</a> <a id="2106" href="Data.Tree.AVL.NonEmpty.html#1029" class="Field">Key</a> <a id="2110" class="Symbol">→</a> <a id="2112" href="Data.Tree.AVL.NonEmpty.html#1431" class="Datatype">Tree⁺</a> <a id="2118" href="Data.Tree.AVL.NonEmpty.html#1551" class="Bound">V</a> <a id="2120" class="Symbol">→</a> <a id="2122" href="Agda.Builtin.Maybe.html#118" class="Datatype">Maybe</a> <a id="2128" class="Symbol">(</a><a id="2129" href="Data.Tree.AVL.NonEmpty.html#1431" class="Datatype">Tree⁺</a> <a id="2135" href="Data.Tree.AVL.NonEmpty.html#1551" class="Bound">V</a><a id="2136" class="Symbol">)</a>
  <a id="2140" href="Data.Tree.AVL.NonEmpty.html#2097" class="Function">delete</a> <a id="2147" href="Data.Tree.AVL.NonEmpty.html#2147" class="Bound">k</a> <a id="2149" class="Symbol">(</a><a id="2150" href="Data.Tree.AVL.NonEmpty.html#1482" class="InductiveConstructor">tree</a> <a id="2155" class="Symbol">{</a><a id="2156" href="Data.Tree.AVL.NonEmpty.html#2156" class="Bound">h</a><a id="2157" class="Symbol">}</a>     <a id="2163" href="Data.Tree.AVL.NonEmpty.html#2163" class="Bound">t</a><a id="2164" class="Symbol">)</a> <a id="2166" class="Keyword">with</a> <a id="2171" href="Data.Tree.AVL.Indexed.html#9606" class="Function">Indexed.delete</a> <a id="2186" href="Data.Tree.AVL.NonEmpty.html#2147" class="Bound">k</a> <a id="2188" href="Data.Tree.AVL.NonEmpty.html#2163" class="Bound">t</a> <a id="2190" href="Data.Tree.AVL.Key.html#1909" class="Function Operator">⊥⁺&lt;[</a> <a id="2195" href="Data.Tree.AVL.NonEmpty.html#2147" class="Bound">k</a> <a id="2197" href="Data.Tree.AVL.Key.html#1909" class="Function Operator">]&lt;⊤⁺</a>
  <a id="2204" href="Data.Tree.AVL.NonEmpty.html#2097" class="Function">delete</a> <a id="2211" href="Data.Tree.AVL.NonEmpty.html#2211" class="Bound">k</a> <a id="2213" class="Symbol">(</a><a id="2214" href="Data.Tree.AVL.NonEmpty.html#1482" class="InductiveConstructor">tree</a> <a id="2219" class="Symbol">{</a><a id="2220" href="Data.Tree.AVL.NonEmpty.html#2220" class="Bound">h</a><a id="2221" class="Symbol">}</a>     <a id="2227" href="Data.Tree.AVL.NonEmpty.html#2227" class="Bound">t</a><a id="2228" class="Symbol">)</a> <a id="2230" class="Symbol">|</a> <a id="2232" href="Data.Tree.AVL.Height.html#462" class="InductiveConstructor">Indexed.1#</a> <a id="2243" href="Agda.Builtin.Sigma.html#218" class="InductiveConstructor Operator">,</a> <a id="2245" href="Data.Tree.AVL.NonEmpty.html#2245" class="Bound">t′</a> <a id="2248" class="Symbol">=</a> <a id="2250" href="Agda.Builtin.Maybe.html#156" class="InductiveConstructor">just</a> <a id="2255" class="Symbol">(</a><a id="2256" href="Data.Tree.AVL.NonEmpty.html#1482" class="InductiveConstructor">tree</a> <a id="2261" href="Data.Tree.AVL.NonEmpty.html#2245" class="Bound">t′</a><a id="2263" class="Symbol">)</a>
  <a id="2267" href="Data.Tree.AVL.NonEmpty.html#2097" class="Function">delete</a> <a id="2274" href="Data.Tree.AVL.NonEmpty.html#2274" class="Bound">k</a> <a id="2276" class="Symbol">(</a><a id="2277" href="Data.Tree.AVL.NonEmpty.html#1482" class="InductiveConstructor">tree</a> <a id="2282" class="Symbol">{</a><a id="2283" class="Number">0</a><a id="2284" class="Symbol">}</a>     <a id="2290" href="Data.Tree.AVL.NonEmpty.html#2290" class="Bound">t</a><a id="2291" class="Symbol">)</a> <a id="2293" class="Symbol">|</a> <a id="2295" href="Data.Tree.AVL.Height.html#444" class="InductiveConstructor">Indexed.0#</a> <a id="2306" href="Agda.Builtin.Sigma.html#218" class="InductiveConstructor Operator">,</a> <a id="2308" href="Data.Tree.AVL.NonEmpty.html#2308" class="Bound">t′</a> <a id="2311" class="Symbol">=</a> <a id="2313" href="Agda.Builtin.Maybe.html#177" class="InductiveConstructor">nothing</a>
  <a id="2323" href="Data.Tree.AVL.NonEmpty.html#2097" class="Function">delete</a> <a id="2330" href="Data.Tree.AVL.NonEmpty.html#2330" class="Bound">k</a> <a id="2332" class="Symbol">(</a><a id="2333" href="Data.Tree.AVL.NonEmpty.html#1482" class="InductiveConstructor">tree</a> <a id="2338" class="Symbol">{</a><a id="2339" href="Agda.Builtin.Nat.html#217" class="InductiveConstructor">suc</a> <a id="2343" href="Data.Tree.AVL.NonEmpty.html#2343" class="Bound">h</a><a id="2344" class="Symbol">}</a> <a id="2346" href="Data.Tree.AVL.NonEmpty.html#2346" class="Bound">t</a><a id="2347" class="Symbol">)</a> <a id="2349" class="Symbol">|</a> <a id="2351" href="Data.Tree.AVL.Height.html#444" class="InductiveConstructor">Indexed.0#</a> <a id="2362" href="Agda.Builtin.Sigma.html#218" class="InductiveConstructor Operator">,</a> <a id="2364" href="Data.Tree.AVL.NonEmpty.html#2364" class="Bound">t′</a> <a id="2367" class="Symbol">=</a> <a id="2369" href="Agda.Builtin.Maybe.html#156" class="InductiveConstructor">just</a> <a id="2374" class="Symbol">(</a><a id="2375" href="Data.Tree.AVL.NonEmpty.html#1482" class="InductiveConstructor">tree</a> <a id="2380" href="Data.Tree.AVL.NonEmpty.html#2364" class="Bound">t′</a><a id="2382" class="Symbol">)</a>

  <a id="2387" href="Data.Tree.AVL.NonEmpty.html#2387" class="Function">lookup</a> <a id="2394" class="Symbol">:</a> <a id="2396" href="Data.Tree.AVL.NonEmpty.html#1431" class="Datatype">Tree⁺</a> <a id="2402" href="Data.Tree.AVL.NonEmpty.html#1551" class="Bound">V</a> <a id="2404" class="Symbol">→</a> <a id="2406" class="Symbol">(</a><a id="2407" href="Data.Tree.AVL.NonEmpty.html#2407" class="Bound">k</a> <a id="2409" class="Symbol">:</a> <a id="2411" href="Data.Tree.AVL.NonEmpty.html#1029" class="Field">Key</a><a id="2414" class="Symbol">)</a> <a id="2416" class="Symbol">→</a> <a id="2418" href="Agda.Builtin.Maybe.html#118" class="Datatype">Maybe</a> <a id="2424" class="Symbol">(</a><a id="2425" href="Data.Tree.AVL.NonEmpty.html#1585" class="Function">Val</a> <a id="2429" href="Data.Tree.AVL.NonEmpty.html#2407" class="Bound">k</a><a id="2430" class="Symbol">)</a>
  <a id="2434" href="Data.Tree.AVL.NonEmpty.html#2387" class="Function">lookup</a> <a id="2441" class="Symbol">(</a><a id="2442" href="Data.Tree.AVL.NonEmpty.html#1482" class="InductiveConstructor">tree</a> <a id="2447" href="Data.Tree.AVL.NonEmpty.html#2447" class="Bound">t</a><a id="2448" class="Symbol">)</a> <a id="2450" href="Data.Tree.AVL.NonEmpty.html#2450" class="Bound">k</a> <a id="2452" class="Symbol">=</a> <a id="2454" href="Data.Tree.AVL.Indexed.html#10114" class="Function">Indexed.lookup</a> <a id="2469" href="Data.Tree.AVL.NonEmpty.html#2447" class="Bound">t</a> <a id="2471" href="Data.Tree.AVL.NonEmpty.html#2450" class="Bound">k</a> <a id="2473" href="Data.Tree.AVL.Key.html#1909" class="Function Operator">⊥⁺&lt;[</a> <a id="2478" href="Data.Tree.AVL.NonEmpty.html#2450" class="Bound">k</a> <a id="2480" href="Data.Tree.AVL.Key.html#1909" class="Function Operator">]&lt;⊤⁺</a>

<a id="2486" class="Keyword">module</a> <a id="2493" href="Data.Tree.AVL.NonEmpty.html#2493" class="Module">_</a> <a id="2495" class="Symbol">{</a><a id="2496" href="Data.Tree.AVL.NonEmpty.html#2496" class="Bound">v</a> <a id="2498" href="Data.Tree.AVL.NonEmpty.html#2498" class="Bound">w</a><a id="2499" class="Symbol">}</a> <a id="2501" class="Symbol">{</a><a id="2502" href="Data.Tree.AVL.NonEmpty.html#2502" class="Bound">V</a> <a id="2504" class="Symbol">:</a> <a id="2506" href="Data.Tree.AVL.Value.html#654" class="Record">Value</a> <a id="2512" href="Data.Tree.AVL.NonEmpty.html#2496" class="Bound">v</a><a id="2513" class="Symbol">}</a> <a id="2515" class="Symbol">{</a><a id="2516" href="Data.Tree.AVL.NonEmpty.html#2516" class="Bound">W</a> <a id="2518" class="Symbol">:</a> <a id="2520" href="Data.Tree.AVL.Value.html#654" class="Record">Value</a> <a id="2526" href="Data.Tree.AVL.NonEmpty.html#2498" class="Bound">w</a><a id="2527" class="Symbol">}</a> <a id="2529" class="Keyword">where</a>

  <a id="2538" class="Keyword">private</a>
    <a id="2550" href="Data.Tree.AVL.NonEmpty.html#2550" class="Function">Val</a> <a id="2554" class="Symbol">=</a> <a id="2556" href="Data.Tree.AVL.Value.html#724" class="Field">Value.family</a> <a id="2569" href="Data.Tree.AVL.NonEmpty.html#2502" class="Bound">V</a>
    <a id="2575" href="Data.Tree.AVL.NonEmpty.html#2575" class="Function">Wal</a> <a id="2579" class="Symbol">=</a> <a id="2581" href="Data.Tree.AVL.Value.html#724" class="Field">Value.family</a> <a id="2594" href="Data.Tree.AVL.NonEmpty.html#2516" class="Bound">W</a>

  <a id="2599" href="Data.Tree.AVL.NonEmpty.html#2599" class="Function">map</a> <a id="2603" class="Symbol">:</a> <a id="2605" href="Relation.Unary.html#3629" class="Function">∀[</a> <a id="2608" href="Data.Tree.AVL.NonEmpty.html#2550" class="Function">Val</a> <a id="2612" href="Relation.Unary.html#4621" class="Function Operator">⇒</a> <a id="2614" href="Data.Tree.AVL.NonEmpty.html#2575" class="Function">Wal</a> <a id="2618" href="Relation.Unary.html#3629" class="Function">]</a> <a id="2620" class="Symbol">→</a> <a id="2622" href="Data.Tree.AVL.NonEmpty.html#1431" class="Datatype">Tree⁺</a> <a id="2628" href="Data.Tree.AVL.NonEmpty.html#2502" class="Bound">V</a> <a id="2630" class="Symbol">→</a> <a id="2632" href="Data.Tree.AVL.NonEmpty.html#1431" class="Datatype">Tree⁺</a> <a id="2638" href="Data.Tree.AVL.NonEmpty.html#2516" class="Bound">W</a>
  <a id="2642" href="Data.Tree.AVL.NonEmpty.html#2599" class="Function">map</a> <a id="2646" href="Data.Tree.AVL.NonEmpty.html#2646" class="Bound">f</a> <a id="2648" class="Symbol">(</a><a id="2649" href="Data.Tree.AVL.NonEmpty.html#1482" class="InductiveConstructor">tree</a> <a id="2654" href="Data.Tree.AVL.NonEmpty.html#2654" class="Bound">t</a><a id="2655" class="Symbol">)</a> <a id="2657" class="Symbol">=</a> <a id="2659" href="Data.Tree.AVL.NonEmpty.html#1482" class="InductiveConstructor">tree</a> <a id="2664" href="Function.Base.html#2003" class="Function Operator">$</a> <a id="2666" href="Data.Tree.AVL.Indexed.html#11197" class="Function">Indexed.map</a> <a id="2678" href="Data.Tree.AVL.NonEmpty.html#2646" class="Bound">f</a> <a id="2680" href="Data.Tree.AVL.NonEmpty.html#2654" class="Bound">t</a>

<a id="2683" class="Keyword">module</a> <a id="2690" href="Data.Tree.AVL.NonEmpty.html#2690" class="Module">_</a> <a id="2692" class="Symbol">{</a><a id="2693" href="Data.Tree.AVL.NonEmpty.html#2693" class="Bound">v</a><a id="2694" class="Symbol">}</a> <a id="2696" class="Symbol">{</a><a id="2697" href="Data.Tree.AVL.NonEmpty.html#2697" class="Bound">V</a> <a id="2699" class="Symbol">:</a> <a id="2701" href="Data.Tree.AVL.Value.html#654" class="Record">Value</a> <a id="2707" href="Data.Tree.AVL.NonEmpty.html#2693" class="Bound">v</a><a id="2708" class="Symbol">}</a> <a id="2710" class="Keyword">where</a>

  <a id="2719" class="Comment">-- The input does not need to be ordered.</a>

  <a id="2764" href="Data.Tree.AVL.NonEmpty.html#2764" class="Function">fromList⁺</a> <a id="2774" class="Symbol">:</a> <a id="2776" href="Data.List.NonEmpty.Base.html#1281" class="Record">List⁺</a> <a id="2782" class="Symbol">(</a><a id="2783" href="Data.Tree.AVL.Value.html#897" class="Record Operator">K&amp;</a> <a id="2786" href="Data.Tree.AVL.NonEmpty.html#2697" class="Bound">V</a><a id="2787" class="Symbol">)</a> <a id="2789" class="Symbol">→</a> <a id="2791" href="Data.Tree.AVL.NonEmpty.html#1431" class="Datatype">Tree⁺</a> <a id="2797" href="Data.Tree.AVL.NonEmpty.html#2697" class="Bound">V</a>
  <a id="2801" href="Data.Tree.AVL.NonEmpty.html#2764" class="Function">fromList⁺</a> <a id="2811" class="Symbol">=</a> <a id="2813" href="Data.List.NonEmpty.Base.html#2820" class="Function">List⁺.foldr</a> <a id="2825" class="Symbol">(</a><a id="2826" href="Data.Product.Base.html#3109" class="Function">uncurry</a> <a id="2834" href="Data.Tree.AVL.NonEmpty.html#1711" class="Function">insert</a> <a id="2841" href="Function.Base.html#3646" class="Function Operator">∘′</a> <a id="2844" href="Data.Tree.AVL.Value.html#1065" class="Function">toPair</a><a id="2850" class="Symbol">)</a> <a id="2852" class="Symbol">(</a><a id="2853" href="Data.Product.Base.html#3109" class="Function">uncurry</a> <a id="2861" href="Data.Tree.AVL.NonEmpty.html#1609" class="Function">singleton</a> <a id="2871" href="Function.Base.html#3646" class="Function Operator">∘′</a> <a id="2874" href="Data.Tree.AVL.Value.html#1065" class="Function">toPair</a><a id="2880" class="Symbol">)</a>

  <a id="2885" class="Comment">-- The output is ordered</a>

  <a id="2913" href="Data.Tree.AVL.NonEmpty.html#2913" class="Function">toList⁺</a> <a id="2921" class="Symbol">:</a> <a id="2923" href="Data.Tree.AVL.NonEmpty.html#1431" class="Datatype">Tree⁺</a> <a id="2929" href="Data.Tree.AVL.NonEmpty.html#2697" class="Bound">V</a> <a id="2931" class="Symbol">→</a> <a id="2933" href="Data.List.NonEmpty.Base.html#1281" class="Record">List⁺</a> <a id="2939" class="Symbol">(</a><a id="2940" href="Data.Tree.AVL.Value.html#897" class="Record Operator">K&amp;</a> <a id="2943" href="Data.Tree.AVL.NonEmpty.html#2697" class="Bound">V</a><a id="2944" class="Symbol">)</a>
  <a id="2948" href="Data.Tree.AVL.NonEmpty.html#2913" class="Function">toList⁺</a> <a id="2956" class="Symbol">(</a><a id="2957" href="Data.Tree.AVL.NonEmpty.html#1482" class="InductiveConstructor">tree</a> <a id="2962" class="Symbol">(</a><a id="2963" href="Data.Tree.AVL.Indexed.html#1702" class="InductiveConstructor">node</a> <a id="2968" href="Data.Tree.AVL.NonEmpty.html#2968" class="Bound">k&amp;v</a> <a id="2972" href="Data.Tree.AVL.NonEmpty.html#2972" class="Bound">l</a> <a id="2974" href="Data.Tree.AVL.NonEmpty.html#2974" class="Bound">r</a> <a id="2976" href="Data.Tree.AVL.NonEmpty.html#2976" class="Bound">bal</a><a id="2979" class="Symbol">))</a> <a id="2982" class="Symbol">=</a> <a id="2984" href="Data.Tree.AVL.Indexed.html#10880" class="Function">toList</a> <a id="2991" href="Data.Tree.AVL.NonEmpty.html#2972" class="Bound">l</a> <a id="2993" href="Data.List.NonEmpty.Base.html#3563" class="Function Operator">++⁺</a> <a id="2997" href="Data.Tree.AVL.NonEmpty.html#2968" class="Bound">k&amp;v</a> <a id="3001" href="Data.List.NonEmpty.Base.html#1327" class="InductiveConstructor Operator">∷</a> <a id="3003" href="Data.Tree.AVL.Indexed.html#10880" class="Function">toList</a> <a id="3010" href="Data.Tree.AVL.NonEmpty.html#2974" class="Bound">r</a>
</pre></body></html>