<!DOCTYPE HTML>
<html><head><meta charset="utf-8"><title>Data.Tree.AVL.NonEmpty</title><link rel="stylesheet" href="Agda.css"></head><body><pre class="Agda"><a id="1" class="Comment">------------------------------------------------------------------------</a>
<a id="74" class="Comment">-- The Agda standard library</a>
<a id="103" class="Comment">--</a>
<a id="106" class="Comment">-- Non-empty AVL trees</a>
<a id="129" class="Comment">------------------------------------------------------------------------</a>

<a id="203" class="Comment">-- AVL trees are balanced binary search trees.</a>

<a id="251" class="Comment">-- The search tree invariant is specified using the technique</a>
<a id="313" class="Comment">-- described by Conor McBride in his talk &quot;Pivotal pragmatism&quot;.</a>

<a id="378" class="Symbol">{-#</a> <a id="382" class="Keyword">OPTIONS</a> <a id="390" class="Pragma">--cubical-compatible</a> <a id="411" class="Pragma">--safe</a> <a id="418" class="Symbol">#-}</a>

<a id="423" class="Keyword">open</a> <a id="428" class="Keyword">import</a> <a id="435" href="Relation.Binary.html" class="Module">Relation.Binary</a> <a id="451" class="Keyword">using</a> <a id="457" class="Symbol">(</a><a id="458" href="Relation.Binary.Bundles.html#6960" class="Record">StrictTotalOrder</a><a id="474" class="Symbol">)</a>

<a id="477" class="Keyword">module</a> <a id="484" href="Data.Tree.AVL.NonEmpty.html" class="Module">Data.Tree.AVL.NonEmpty</a>
  <a id="509" class="Symbol">{</a><a id="510" href="Data.Tree.AVL.NonEmpty.html#510" class="Bound">a</a> <a id="512" href="Data.Tree.AVL.NonEmpty.html#512" class="Bound">ℓ₁</a> <a id="515" href="Data.Tree.AVL.NonEmpty.html#515" class="Bound">ℓ₂</a><a id="517" class="Symbol">}</a> <a id="519" class="Symbol">(</a><a id="520" href="Data.Tree.AVL.NonEmpty.html#520" class="Bound">strictTotalOrder</a> <a id="537" class="Symbol">:</a> <a id="539" href="Relation.Binary.Bundles.html#6960" class="Record">StrictTotalOrder</a> <a id="556" href="Data.Tree.AVL.NonEmpty.html#510" class="Bound">a</a> <a id="558" href="Data.Tree.AVL.NonEmpty.html#512" class="Bound">ℓ₁</a> <a id="561" href="Data.Tree.AVL.NonEmpty.html#515" class="Bound">ℓ₂</a><a id="563" class="Symbol">)</a> <a id="565" class="Keyword">where</a>

<a id="572" class="Keyword">open</a> <a id="577" class="Keyword">import</a> <a id="584" href="Data.Bool.Base.html" class="Module">Data.Bool.Base</a> <a id="599" class="Keyword">using</a> <a id="605" class="Symbol">(</a><a id="606" href="Agda.Builtin.Bool.html#156" class="Datatype">Bool</a><a id="610" class="Symbol">)</a>
<a id="612" class="Keyword">open</a> <a id="617" class="Keyword">import</a> <a id="624" href="Data.Empty.html" class="Module">Data.Empty</a>
<a id="635" class="Keyword">open</a> <a id="640" class="Keyword">import</a> <a id="647" href="Data.List.NonEmpty.html" class="Module">Data.List.NonEmpty</a> <a id="666" class="Symbol">as</a> <a id="669" class="Module">List⁺</a> <a id="675" class="Keyword">using</a> <a id="681" class="Symbol">(</a><a id="682" href="Data.List.NonEmpty.Base.html#1281" class="Record">List⁺</a><a id="687" class="Symbol">;</a> <a id="689" href="Data.List.NonEmpty.Base.html#1327" class="InductiveConstructor Operator">_∷_</a><a id="692" class="Symbol">;</a> <a id="694" href="Data.List.NonEmpty.Base.html#3563" class="Function Operator">_++⁺_</a><a id="699" class="Symbol">)</a>
<a id="701" class="Keyword">open</a> <a id="706" class="Keyword">import</a> <a id="713" href="Data.Maybe.Base.html" class="Module">Data.Maybe.Base</a> <a id="729" class="Keyword">hiding</a> <a id="736" class="Symbol">(</a><a id="737" href="Data.Maybe.Base.html#2049" class="Function">map</a><a id="740" class="Symbol">)</a>
<a id="742" class="Keyword">open</a> <a id="747" class="Keyword">import</a> <a id="754" href="Data.Nat.Base.html" class="Module">Data.Nat.Base</a> <a id="768" class="Keyword">hiding</a> <a id="775" class="Symbol">(</a><a id="776" href="Data.Nat.Base.html#1654" class="Function Operator">_&lt;_</a><a id="779" class="Symbol">;</a> <a id="781" href="Data.Nat.Base.html#3493" class="Function Operator">_⊔_</a><a id="784" class="Symbol">;</a> <a id="786" href="Data.Nat.Base.html#7112" class="Function">compare</a><a id="793" class="Symbol">)</a>
<a id="795" class="Keyword">open</a> <a id="800" class="Keyword">import</a> <a id="807" href="Data.Product.Base.html" class="Module">Data.Product.Base</a> <a id="825" class="Keyword">hiding</a> <a id="832" class="Symbol">(</a><a id="833" href="Data.Product.Base.html#2173" class="Function">map</a><a id="836" class="Symbol">)</a>
<a id="838" class="Keyword">open</a> <a id="843" class="Keyword">import</a> <a id="850" href="Data.Unit.html" class="Module">Data.Unit</a>
<a id="860" class="Keyword">open</a> <a id="865" class="Keyword">import</a> <a id="872" href="Function.Base.html" class="Module">Function.Base</a> <a id="886" class="Keyword">using</a> <a id="892" class="Symbol">(</a><a id="893" href="Function.Base.html#2003" class="Function Operator">_$_</a><a id="896" class="Symbol">;</a> <a id="898" href="Function.Base.html#3646" class="Function Operator">_∘′_</a><a id="902" class="Symbol">)</a>
<a id="904" class="Keyword">open</a> <a id="909" class="Keyword">import</a> <a id="916" href="Level.html" class="Module">Level</a> <a id="922" class="Keyword">using</a> <a id="928" class="Symbol">(</a><a id="929" href="Agda.Primitive.html#804" class="Primitive Operator">_⊔_</a><a id="932" class="Symbol">;</a> <a id="934" href="Level.html#409" class="Record">Lift</a><a id="938" class="Symbol">;</a> <a id="940" href="Level.html#466" class="InductiveConstructor">lift</a><a id="944" class="Symbol">)</a>
<a id="946" class="Keyword">open</a> <a id="951" class="Keyword">import</a> <a id="958" href="Relation.Unary.html" class="Module">Relation.Unary</a>

<a id="974" class="Keyword">open</a> <a id="979" href="Relation.Binary.Bundles.html#6960" class="Module">StrictTotalOrder</a> <a id="996" href="Data.Tree.AVL.NonEmpty.html#520" class="Bound">strictTotalOrder</a> <a id="1013" class="Keyword">renaming</a> <a id="1022" class="Symbol">(</a><a id="1023" href="Relation.Binary.Bundles.html#7047" class="Field">Carrier</a> <a id="1031" class="Symbol">to</a> <a id="1034" class="Field">Key</a><a id="1037" class="Symbol">)</a>
<a id="1039" class="Keyword">open</a> <a id="1044" class="Keyword">import</a> <a id="1051" href="Data.Tree.AVL.Value.html" class="Module">Data.Tree.AVL.Value</a> <a id="1071" href="Relation.Binary.Bundles.html#4501" class="Function">Eq.setoid</a>
<a id="1081" class="Keyword">import</a> <a id="1088" href="Data.Tree.AVL.Indexed.html" class="Module">Data.Tree.AVL.Indexed</a> <a id="1110" href="Data.Tree.AVL.NonEmpty.html#520" class="Bound">strictTotalOrder</a> as <a id="Indexed"></a><a id="1130" href="Data.Tree.AVL.NonEmpty.html#1130" class="Module">Indexed</a>
<a id="1138" class="Keyword">open</a> <a id="1143" href="Data.Tree.AVL.NonEmpty.html#1130" class="Module">Indexed</a> <a id="1151" class="Keyword">using</a> <a id="1157" class="Symbol">(</a><a id="1158" href="Data.Tree.AVL.Key.html#1234" class="InductiveConstructor">⊥⁺</a><a id="1160" class="Symbol">;</a> <a id="1162" href="Data.Tree.AVL.Key.html#1247" class="InductiveConstructor">⊤⁺</a><a id="1164" class="Symbol">;</a> <a id="1166" href="Data.Tree.AVL.Key.html#1712" class="InductiveConstructor">⊥⁺&lt;⊤⁺</a><a id="1171" class="Symbol">;</a> <a id="1173" href="Data.Tree.AVL.Key.html#1909" class="Function Operator">⊥⁺&lt;[_]&lt;⊤⁺</a><a id="1182" class="Symbol">;</a> <a id="1184" href="Data.Tree.AVL.Key.html#1753" class="InductiveConstructor Operator">⊥⁺&lt;[_]</a><a id="1190" class="Symbol">;</a> <a id="1192" href="Data.Tree.AVL.Key.html#1732" class="InductiveConstructor Operator">[_]&lt;⊤⁺</a><a id="1198" class="Symbol">;</a> <a id="1200" href="Data.Tree.AVL.Indexed.html#1702" class="InductiveConstructor">node</a><a id="1204" class="Symbol">;</a> <a id="1206" href="Data.Tree.AVL.Indexed.html#10880" class="Function">toList</a><a id="1212" class="Symbol">)</a>

<a id="1215" class="Comment">------------------------------------------------------------------------</a>
<a id="1288" class="Comment">-- Types and functions with hidden indices</a>

<a id="1332" class="Comment">-- NB: the height is non-zero thus guaranteeing that the AVL tree</a>
<a id="1398" class="Comment">-- contains at least one value.</a>

<a id="1431" class="Keyword">data</a> <a id="Tree⁺"></a><a id="1436" href="Data.Tree.AVL.NonEmpty.html#1436" class="Datatype">Tree⁺</a> <a id="1442" class="Symbol">{</a><a id="1443" href="Data.Tree.AVL.NonEmpty.html#1443" class="Bound">v</a><a id="1444" class="Symbol">}</a> <a id="1446" class="Symbol">(</a><a id="1447" href="Data.Tree.AVL.NonEmpty.html#1447" class="Bound">V</a> <a id="1449" class="Symbol">:</a> <a id="1451" href="Data.Tree.AVL.Value.html#659" class="Record">Value</a> <a id="1457" href="Data.Tree.AVL.NonEmpty.html#1443" class="Bound">v</a><a id="1458" class="Symbol">)</a> <a id="1460" class="Symbol">:</a> <a id="1462" href="Agda.Primitive.html#320" class="Primitive">Set</a> <a id="1466" class="Symbol">(</a><a id="1467" href="Data.Tree.AVL.NonEmpty.html#510" class="Bound">a</a> <a id="1469" href="Agda.Primitive.html#804" class="Primitive Operator">⊔</a> <a id="1471" href="Data.Tree.AVL.NonEmpty.html#1443" class="Bound">v</a> <a id="1473" href="Agda.Primitive.html#804" class="Primitive Operator">⊔</a> <a id="1475" href="Data.Tree.AVL.NonEmpty.html#515" class="Bound">ℓ₂</a><a id="1477" class="Symbol">)</a> <a id="1479" class="Keyword">where</a>
  <a id="Tree⁺.tree"></a><a id="1487" href="Data.Tree.AVL.NonEmpty.html#1487" class="InductiveConstructor">tree</a> <a id="1492" class="Symbol">:</a> <a id="1494" class="Symbol">∀</a> <a id="1496" class="Symbol">{</a><a id="1497" href="Data.Tree.AVL.NonEmpty.html#1497" class="Bound">h</a><a id="1498" class="Symbol">}</a> <a id="1500" class="Symbol">→</a> <a id="1502" href="Data.Tree.AVL.Indexed.html#1596" class="Datatype">Indexed.Tree</a> <a id="1515" href="Data.Tree.AVL.NonEmpty.html#1447" class="Bound">V</a> <a id="1517" href="Data.Tree.AVL.Key.html#1234" class="InductiveConstructor">⊥⁺</a> <a id="1520" href="Data.Tree.AVL.Key.html#1247" class="InductiveConstructor">⊤⁺</a> <a id="1523" class="Symbol">(</a><a id="1524" href="Agda.Builtin.Nat.html#217" class="InductiveConstructor">suc</a> <a id="1528" href="Data.Tree.AVL.NonEmpty.html#1497" class="Bound">h</a><a id="1529" class="Symbol">)</a> <a id="1531" class="Symbol">→</a> <a id="1533" href="Data.Tree.AVL.NonEmpty.html#1436" class="Datatype">Tree⁺</a> <a id="1539" href="Data.Tree.AVL.NonEmpty.html#1447" class="Bound">V</a>

<a id="1542" class="Keyword">module</a> <a id="1549" href="Data.Tree.AVL.NonEmpty.html#1549" class="Module">_</a> <a id="1551" class="Symbol">{</a><a id="1552" href="Data.Tree.AVL.NonEmpty.html#1552" class="Bound">v</a><a id="1553" class="Symbol">}</a> <a id="1555" class="Symbol">{</a><a id="1556" href="Data.Tree.AVL.NonEmpty.html#1556" class="Bound">V</a> <a id="1558" class="Symbol">:</a> <a id="1560" href="Data.Tree.AVL.Value.html#659" class="Record">Value</a> <a id="1566" href="Data.Tree.AVL.NonEmpty.html#1552" class="Bound">v</a><a id="1567" class="Symbol">}</a> <a id="1569" class="Keyword">where</a>

  <a id="1578" class="Keyword">private</a>
    <a id="1590" href="Data.Tree.AVL.NonEmpty.html#1590" class="Function">Val</a> <a id="1594" class="Symbol">=</a> <a id="1596" href="Data.Tree.AVL.Value.html#729" class="Field">Value.family</a> <a id="1609" href="Data.Tree.AVL.NonEmpty.html#1556" class="Bound">V</a>

  <a id="1614" href="Data.Tree.AVL.NonEmpty.html#1614" class="Function">singleton</a> <a id="1624" class="Symbol">:</a> <a id="1626" class="Symbol">(</a><a id="1627" href="Data.Tree.AVL.NonEmpty.html#1627" class="Bound">k</a> <a id="1629" class="Symbol">:</a> <a id="1631" href="Data.Tree.AVL.NonEmpty.html#1034" class="Field">Key</a><a id="1634" class="Symbol">)</a> <a id="1636" class="Symbol">→</a> <a id="1638" href="Data.Tree.AVL.NonEmpty.html#1590" class="Function">Val</a> <a id="1642" href="Data.Tree.AVL.NonEmpty.html#1627" class="Bound">k</a> <a id="1644" class="Symbol">→</a> <a id="1646" href="Data.Tree.AVL.NonEmpty.html#1436" class="Datatype">Tree⁺</a> <a id="1652" href="Data.Tree.AVL.NonEmpty.html#1556" class="Bound">V</a>
  <a id="1656" href="Data.Tree.AVL.NonEmpty.html#1614" class="Function">singleton</a> <a id="1666" href="Data.Tree.AVL.NonEmpty.html#1666" class="Bound">k</a> <a id="1668" href="Data.Tree.AVL.NonEmpty.html#1668" class="Bound">v</a> <a id="1670" class="Symbol">=</a> <a id="1672" href="Data.Tree.AVL.NonEmpty.html#1487" class="InductiveConstructor">tree</a> <a id="1677" class="Symbol">(</a><a id="1678" href="Data.Tree.AVL.Indexed.html#8188" class="Function">Indexed.singleton</a> <a id="1696" href="Data.Tree.AVL.NonEmpty.html#1666" class="Bound">k</a> <a id="1698" href="Data.Tree.AVL.NonEmpty.html#1668" class="Bound">v</a> <a id="1700" href="Data.Tree.AVL.Key.html#1909" class="Function Operator">⊥⁺&lt;[</a> <a id="1705" href="Data.Tree.AVL.NonEmpty.html#1666" class="Bound">k</a> <a id="1707" href="Data.Tree.AVL.Key.html#1909" class="Function Operator">]&lt;⊤⁺</a><a id="1711" class="Symbol">)</a>

  <a id="1716" href="Data.Tree.AVL.NonEmpty.html#1716" class="Function">insert</a> <a id="1723" class="Symbol">:</a> <a id="1725" class="Symbol">(</a><a id="1726" href="Data.Tree.AVL.NonEmpty.html#1726" class="Bound">k</a> <a id="1728" class="Symbol">:</a> <a id="1730" href="Data.Tree.AVL.NonEmpty.html#1034" class="Field">Key</a><a id="1733" class="Symbol">)</a> <a id="1735" class="Symbol">→</a> <a id="1737" href="Data.Tree.AVL.NonEmpty.html#1590" class="Function">Val</a> <a id="1741" href="Data.Tree.AVL.NonEmpty.html#1726" class="Bound">k</a> <a id="1743" class="Symbol">→</a> <a id="1745" href="Data.Tree.AVL.NonEmpty.html#1436" class="Datatype">Tree⁺</a> <a id="1751" href="Data.Tree.AVL.NonEmpty.html#1556" class="Bound">V</a> <a id="1753" class="Symbol">→</a> <a id="1755" href="Data.Tree.AVL.NonEmpty.html#1436" class="Datatype">Tree⁺</a> <a id="1761" href="Data.Tree.AVL.NonEmpty.html#1556" class="Bound">V</a>
  <a id="1765" href="Data.Tree.AVL.NonEmpty.html#1716" class="Function">insert</a> <a id="1772" href="Data.Tree.AVL.NonEmpty.html#1772" class="Bound">k</a> <a id="1774" href="Data.Tree.AVL.NonEmpty.html#1774" class="Bound">v</a> <a id="1776" class="Symbol">(</a><a id="1777" href="Data.Tree.AVL.NonEmpty.html#1487" class="InductiveConstructor">tree</a> <a id="1782" href="Data.Tree.AVL.NonEmpty.html#1782" class="Bound">t</a><a id="1783" class="Symbol">)</a> <a id="1785" class="Keyword">with</a> <a id="1790" href="Data.Tree.AVL.Indexed.html#9306" class="Function">Indexed.insert</a> <a id="1805" href="Data.Tree.AVL.NonEmpty.html#1772" class="Bound">k</a> <a id="1807" href="Data.Tree.AVL.NonEmpty.html#1774" class="Bound">v</a> <a id="1809" href="Data.Tree.AVL.NonEmpty.html#1782" class="Bound">t</a> <a id="1811" href="Data.Tree.AVL.Key.html#1909" class="Function Operator">⊥⁺&lt;[</a> <a id="1816" href="Data.Tree.AVL.NonEmpty.html#1772" class="Bound">k</a> <a id="1818" href="Data.Tree.AVL.Key.html#1909" class="Function Operator">]&lt;⊤⁺</a>
  <a id="1825" class="Symbol">...</a> <a id="1829" class="Symbol">|</a> <a id="1831" href="Data.Tree.AVL.Height.html#444" class="InductiveConstructor">Indexed.0#</a> <a id="1842" href="Agda.Builtin.Sigma.html#218" class="InductiveConstructor Operator">,</a> <a id="1844" href="Data.Tree.AVL.NonEmpty.html#1844" class="Bound">t′</a> <a id="1847" class="Symbol">=</a> <a id="1849" href="Data.Tree.AVL.NonEmpty.html#1487" class="InductiveConstructor">tree</a> <a id="1854" href="Data.Tree.AVL.NonEmpty.html#1844" class="Bound">t′</a>
  <a id="1859" class="Symbol">...</a> <a id="1863" class="Symbol">|</a> <a id="1865" href="Data.Tree.AVL.Height.html#462" class="InductiveConstructor">Indexed.1#</a> <a id="1876" href="Agda.Builtin.Sigma.html#218" class="InductiveConstructor Operator">,</a> <a id="1878" href="Data.Tree.AVL.NonEmpty.html#1878" class="Bound">t′</a> <a id="1881" class="Symbol">=</a> <a id="1883" href="Data.Tree.AVL.NonEmpty.html#1487" class="InductiveConstructor">tree</a> <a id="1888" href="Data.Tree.AVL.NonEmpty.html#1878" class="Bound">t′</a>

  <a id="1894" href="Data.Tree.AVL.NonEmpty.html#1894" class="Function">insertWith</a> <a id="1905" class="Symbol">:</a> <a id="1907" class="Symbol">(</a><a id="1908" href="Data.Tree.AVL.NonEmpty.html#1908" class="Bound">k</a> <a id="1910" class="Symbol">:</a> <a id="1912" href="Data.Tree.AVL.NonEmpty.html#1034" class="Field">Key</a><a id="1915" class="Symbol">)</a> <a id="1917" class="Symbol">→</a> <a id="1919" class="Symbol">(</a><a id="1920" href="Agda.Builtin.Maybe.html#118" class="Datatype">Maybe</a> <a id="1926" class="Symbol">(</a><a id="1927" href="Data.Tree.AVL.NonEmpty.html#1590" class="Function">Val</a> <a id="1931" href="Data.Tree.AVL.NonEmpty.html#1908" class="Bound">k</a><a id="1932" class="Symbol">)</a> <a id="1934" class="Symbol">→</a> <a id="1936" href="Data.Tree.AVL.NonEmpty.html#1590" class="Function">Val</a> <a id="1940" href="Data.Tree.AVL.NonEmpty.html#1908" class="Bound">k</a><a id="1941" class="Symbol">)</a> <a id="1943" class="Symbol">→</a> <a id="1945" href="Data.Tree.AVL.NonEmpty.html#1436" class="Datatype">Tree⁺</a> <a id="1951" href="Data.Tree.AVL.NonEmpty.html#1556" class="Bound">V</a> <a id="1953" class="Symbol">→</a> <a id="1955" href="Data.Tree.AVL.NonEmpty.html#1436" class="Datatype">Tree⁺</a> <a id="1961" href="Data.Tree.AVL.NonEmpty.html#1556" class="Bound">V</a>
  <a id="1965" href="Data.Tree.AVL.NonEmpty.html#1894" class="Function">insertWith</a> <a id="1976" href="Data.Tree.AVL.NonEmpty.html#1976" class="Bound">k</a> <a id="1978" href="Data.Tree.AVL.NonEmpty.html#1978" class="Bound">f</a> <a id="1980" class="Symbol">(</a><a id="1981" href="Data.Tree.AVL.NonEmpty.html#1487" class="InductiveConstructor">tree</a> <a id="1986" href="Data.Tree.AVL.NonEmpty.html#1986" class="Bound">t</a><a id="1987" class="Symbol">)</a> <a id="1989" class="Keyword">with</a> <a id="1994" href="Data.Tree.AVL.Indexed.html#8554" class="Function">Indexed.insertWith</a> <a id="2013" href="Data.Tree.AVL.NonEmpty.html#1976" class="Bound">k</a> <a id="2015" href="Data.Tree.AVL.NonEmpty.html#1978" class="Bound">f</a> <a id="2017" href="Data.Tree.AVL.NonEmpty.html#1986" class="Bound">t</a> <a id="2019" href="Data.Tree.AVL.Key.html#1909" class="Function Operator">⊥⁺&lt;[</a> <a id="2024" href="Data.Tree.AVL.NonEmpty.html#1976" class="Bound">k</a> <a id="2026" href="Data.Tree.AVL.Key.html#1909" class="Function Operator">]&lt;⊤⁺</a>
  <a id="2033" class="Symbol">...</a> <a id="2037" class="Symbol">|</a> <a id="2039" href="Data.Tree.AVL.Height.html#444" class="InductiveConstructor">Indexed.0#</a> <a id="2050" href="Agda.Builtin.Sigma.html#218" class="InductiveConstructor Operator">,</a> <a id="2052" href="Data.Tree.AVL.NonEmpty.html#2052" class="Bound">t′</a> <a id="2055" class="Symbol">=</a> <a id="2057" href="Data.Tree.AVL.NonEmpty.html#1487" class="InductiveConstructor">tree</a> <a id="2062" href="Data.Tree.AVL.NonEmpty.html#2052" class="Bound">t′</a>
  <a id="2067" class="Symbol">...</a> <a id="2071" class="Symbol">|</a> <a id="2073" href="Data.Tree.AVL.Height.html#462" class="InductiveConstructor">Indexed.1#</a> <a id="2084" href="Agda.Builtin.Sigma.html#218" class="InductiveConstructor Operator">,</a> <a id="2086" href="Data.Tree.AVL.NonEmpty.html#2086" class="Bound">t′</a> <a id="2089" class="Symbol">=</a> <a id="2091" href="Data.Tree.AVL.NonEmpty.html#1487" class="InductiveConstructor">tree</a> <a id="2096" href="Data.Tree.AVL.NonEmpty.html#2086" class="Bound">t′</a>

  <a id="2102" href="Data.Tree.AVL.NonEmpty.html#2102" class="Function">delete</a> <a id="2109" class="Symbol">:</a> <a id="2111" href="Data.Tree.AVL.NonEmpty.html#1034" class="Field">Key</a> <a id="2115" class="Symbol">→</a> <a id="2117" href="Data.Tree.AVL.NonEmpty.html#1436" class="Datatype">Tree⁺</a> <a id="2123" href="Data.Tree.AVL.NonEmpty.html#1556" class="Bound">V</a> <a id="2125" class="Symbol">→</a> <a id="2127" href="Agda.Builtin.Maybe.html#118" class="Datatype">Maybe</a> <a id="2133" class="Symbol">(</a><a id="2134" href="Data.Tree.AVL.NonEmpty.html#1436" class="Datatype">Tree⁺</a> <a id="2140" href="Data.Tree.AVL.NonEmpty.html#1556" class="Bound">V</a><a id="2141" class="Symbol">)</a>
  <a id="2145" href="Data.Tree.AVL.NonEmpty.html#2102" class="Function">delete</a> <a id="2152" href="Data.Tree.AVL.NonEmpty.html#2152" class="Bound">k</a> <a id="2154" class="Symbol">(</a><a id="2155" href="Data.Tree.AVL.NonEmpty.html#1487" class="InductiveConstructor">tree</a> <a id="2160" class="Symbol">{</a><a id="2161" href="Data.Tree.AVL.NonEmpty.html#2161" class="Bound">h</a><a id="2162" class="Symbol">}</a>     <a id="2168" href="Data.Tree.AVL.NonEmpty.html#2168" class="Bound">t</a><a id="2169" class="Symbol">)</a> <a id="2171" class="Keyword">with</a> <a id="2176" href="Data.Tree.AVL.Indexed.html#9606" class="Function">Indexed.delete</a> <a id="2191" href="Data.Tree.AVL.NonEmpty.html#2152" class="Bound">k</a> <a id="2193" href="Data.Tree.AVL.NonEmpty.html#2168" class="Bound">t</a> <a id="2195" href="Data.Tree.AVL.Key.html#1909" class="Function Operator">⊥⁺&lt;[</a> <a id="2200" href="Data.Tree.AVL.NonEmpty.html#2152" class="Bound">k</a> <a id="2202" href="Data.Tree.AVL.Key.html#1909" class="Function Operator">]&lt;⊤⁺</a>
  <a id="2209" href="Data.Tree.AVL.NonEmpty.html#2102" class="Function">delete</a> <a id="2216" href="Data.Tree.AVL.NonEmpty.html#2216" class="Bound">k</a> <a id="2218" class="Symbol">(</a><a id="2219" href="Data.Tree.AVL.NonEmpty.html#1487" class="InductiveConstructor">tree</a> <a id="2224" class="Symbol">{</a><a id="2225" href="Data.Tree.AVL.NonEmpty.html#2225" class="Bound">h</a><a id="2226" class="Symbol">}</a>     <a id="2232" href="Data.Tree.AVL.NonEmpty.html#2232" class="Bound">t</a><a id="2233" class="Symbol">)</a> <a id="2235" class="Symbol">|</a> <a id="2237" href="Data.Tree.AVL.Height.html#462" class="InductiveConstructor">Indexed.1#</a> <a id="2248" href="Agda.Builtin.Sigma.html#218" class="InductiveConstructor Operator">,</a> <a id="2250" href="Data.Tree.AVL.NonEmpty.html#2250" class="Bound">t′</a> <a id="2253" class="Symbol">=</a> <a id="2255" href="Agda.Builtin.Maybe.html#156" class="InductiveConstructor">just</a> <a id="2260" class="Symbol">(</a><a id="2261" href="Data.Tree.AVL.NonEmpty.html#1487" class="InductiveConstructor">tree</a> <a id="2266" href="Data.Tree.AVL.NonEmpty.html#2250" class="Bound">t′</a><a id="2268" class="Symbol">)</a>
  <a id="2272" href="Data.Tree.AVL.NonEmpty.html#2102" class="Function">delete</a> <a id="2279" href="Data.Tree.AVL.NonEmpty.html#2279" class="Bound">k</a> <a id="2281" class="Symbol">(</a><a id="2282" href="Data.Tree.AVL.NonEmpty.html#1487" class="InductiveConstructor">tree</a> <a id="2287" class="Symbol">{</a><a id="2288" class="Number">0</a><a id="2289" class="Symbol">}</a>     <a id="2295" href="Data.Tree.AVL.NonEmpty.html#2295" class="Bound">t</a><a id="2296" class="Symbol">)</a> <a id="2298" class="Symbol">|</a> <a id="2300" href="Data.Tree.AVL.Height.html#444" class="InductiveConstructor">Indexed.0#</a> <a id="2311" href="Agda.Builtin.Sigma.html#218" class="InductiveConstructor Operator">,</a> <a id="2313" href="Data.Tree.AVL.NonEmpty.html#2313" class="Bound">t′</a> <a id="2316" class="Symbol">=</a> <a id="2318" href="Agda.Builtin.Maybe.html#177" class="InductiveConstructor">nothing</a>
  <a id="2328" href="Data.Tree.AVL.NonEmpty.html#2102" class="Function">delete</a> <a id="2335" href="Data.Tree.AVL.NonEmpty.html#2335" class="Bound">k</a> <a id="2337" class="Symbol">(</a><a id="2338" href="Data.Tree.AVL.NonEmpty.html#1487" class="InductiveConstructor">tree</a> <a id="2343" class="Symbol">{</a><a id="2344" href="Agda.Builtin.Nat.html#217" class="InductiveConstructor">suc</a> <a id="2348" href="Data.Tree.AVL.NonEmpty.html#2348" class="Bound">h</a><a id="2349" class="Symbol">}</a> <a id="2351" href="Data.Tree.AVL.NonEmpty.html#2351" class="Bound">t</a><a id="2352" class="Symbol">)</a> <a id="2354" class="Symbol">|</a> <a id="2356" href="Data.Tree.AVL.Height.html#444" class="InductiveConstructor">Indexed.0#</a> <a id="2367" href="Agda.Builtin.Sigma.html#218" class="InductiveConstructor Operator">,</a> <a id="2369" href="Data.Tree.AVL.NonEmpty.html#2369" class="Bound">t′</a> <a id="2372" class="Symbol">=</a> <a id="2374" href="Agda.Builtin.Maybe.html#156" class="InductiveConstructor">just</a> <a id="2379" class="Symbol">(</a><a id="2380" href="Data.Tree.AVL.NonEmpty.html#1487" class="InductiveConstructor">tree</a> <a id="2385" href="Data.Tree.AVL.NonEmpty.html#2369" class="Bound">t′</a><a id="2387" class="Symbol">)</a>

  <a id="2392" href="Data.Tree.AVL.NonEmpty.html#2392" class="Function">lookup</a> <a id="2399" class="Symbol">:</a> <a id="2401" href="Data.Tree.AVL.NonEmpty.html#1436" class="Datatype">Tree⁺</a> <a id="2407" href="Data.Tree.AVL.NonEmpty.html#1556" class="Bound">V</a> <a id="2409" class="Symbol">→</a> <a id="2411" class="Symbol">(</a><a id="2412" href="Data.Tree.AVL.NonEmpty.html#2412" class="Bound">k</a> <a id="2414" class="Symbol">:</a> <a id="2416" href="Data.Tree.AVL.NonEmpty.html#1034" class="Field">Key</a><a id="2419" class="Symbol">)</a> <a id="2421" class="Symbol">→</a> <a id="2423" href="Agda.Builtin.Maybe.html#118" class="Datatype">Maybe</a> <a id="2429" class="Symbol">(</a><a id="2430" href="Data.Tree.AVL.NonEmpty.html#1590" class="Function">Val</a> <a id="2434" href="Data.Tree.AVL.NonEmpty.html#2412" class="Bound">k</a><a id="2435" class="Symbol">)</a>
  <a id="2439" href="Data.Tree.AVL.NonEmpty.html#2392" class="Function">lookup</a> <a id="2446" class="Symbol">(</a><a id="2447" href="Data.Tree.AVL.NonEmpty.html#1487" class="InductiveConstructor">tree</a> <a id="2452" href="Data.Tree.AVL.NonEmpty.html#2452" class="Bound">t</a><a id="2453" class="Symbol">)</a> <a id="2455" href="Data.Tree.AVL.NonEmpty.html#2455" class="Bound">k</a> <a id="2457" class="Symbol">=</a> <a id="2459" href="Data.Tree.AVL.Indexed.html#10114" class="Function">Indexed.lookup</a> <a id="2474" href="Data.Tree.AVL.NonEmpty.html#2452" class="Bound">t</a> <a id="2476" href="Data.Tree.AVL.NonEmpty.html#2455" class="Bound">k</a> <a id="2478" href="Data.Tree.AVL.Key.html#1909" class="Function Operator">⊥⁺&lt;[</a> <a id="2483" href="Data.Tree.AVL.NonEmpty.html#2455" class="Bound">k</a> <a id="2485" href="Data.Tree.AVL.Key.html#1909" class="Function Operator">]&lt;⊤⁺</a>

<a id="2491" class="Keyword">module</a> <a id="2498" href="Data.Tree.AVL.NonEmpty.html#2498" class="Module">_</a> <a id="2500" class="Symbol">{</a><a id="2501" href="Data.Tree.AVL.NonEmpty.html#2501" class="Bound">v</a> <a id="2503" href="Data.Tree.AVL.NonEmpty.html#2503" class="Bound">w</a><a id="2504" class="Symbol">}</a> <a id="2506" class="Symbol">{</a><a id="2507" href="Data.Tree.AVL.NonEmpty.html#2507" class="Bound">V</a> <a id="2509" class="Symbol">:</a> <a id="2511" href="Data.Tree.AVL.Value.html#659" class="Record">Value</a> <a id="2517" href="Data.Tree.AVL.NonEmpty.html#2501" class="Bound">v</a><a id="2518" class="Symbol">}</a> <a id="2520" class="Symbol">{</a><a id="2521" href="Data.Tree.AVL.NonEmpty.html#2521" class="Bound">W</a> <a id="2523" class="Symbol">:</a> <a id="2525" href="Data.Tree.AVL.Value.html#659" class="Record">Value</a> <a id="2531" href="Data.Tree.AVL.NonEmpty.html#2503" class="Bound">w</a><a id="2532" class="Symbol">}</a> <a id="2534" class="Keyword">where</a>

  <a id="2543" class="Keyword">private</a>
    <a id="2555" href="Data.Tree.AVL.NonEmpty.html#2555" class="Function">Val</a> <a id="2559" class="Symbol">=</a> <a id="2561" href="Data.Tree.AVL.Value.html#729" class="Field">Value.family</a> <a id="2574" href="Data.Tree.AVL.NonEmpty.html#2507" class="Bound">V</a>
    <a id="2580" href="Data.Tree.AVL.NonEmpty.html#2580" class="Function">Wal</a> <a id="2584" class="Symbol">=</a> <a id="2586" href="Data.Tree.AVL.Value.html#729" class="Field">Value.family</a> <a id="2599" href="Data.Tree.AVL.NonEmpty.html#2521" class="Bound">W</a>

  <a id="2604" href="Data.Tree.AVL.NonEmpty.html#2604" class="Function">map</a> <a id="2608" class="Symbol">:</a> <a id="2610" href="Relation.Unary.html#3629" class="Function">∀[</a> <a id="2613" href="Data.Tree.AVL.NonEmpty.html#2555" class="Function">Val</a> <a id="2617" href="Relation.Unary.html#4621" class="Function Operator">⇒</a> <a id="2619" href="Data.Tree.AVL.NonEmpty.html#2580" class="Function">Wal</a> <a id="2623" href="Relation.Unary.html#3629" class="Function">]</a> <a id="2625" class="Symbol">→</a> <a id="2627" href="Data.Tree.AVL.NonEmpty.html#1436" class="Datatype">Tree⁺</a> <a id="2633" href="Data.Tree.AVL.NonEmpty.html#2507" class="Bound">V</a> <a id="2635" class="Symbol">→</a> <a id="2637" href="Data.Tree.AVL.NonEmpty.html#1436" class="Datatype">Tree⁺</a> <a id="2643" href="Data.Tree.AVL.NonEmpty.html#2521" class="Bound">W</a>
  <a id="2647" href="Data.Tree.AVL.NonEmpty.html#2604" class="Function">map</a> <a id="2651" href="Data.Tree.AVL.NonEmpty.html#2651" class="Bound">f</a> <a id="2653" class="Symbol">(</a><a id="2654" href="Data.Tree.AVL.NonEmpty.html#1487" class="InductiveConstructor">tree</a> <a id="2659" href="Data.Tree.AVL.NonEmpty.html#2659" class="Bound">t</a><a id="2660" class="Symbol">)</a> <a id="2662" class="Symbol">=</a> <a id="2664" href="Data.Tree.AVL.NonEmpty.html#1487" class="InductiveConstructor">tree</a> <a id="2669" href="Function.Base.html#2003" class="Function Operator">$</a> <a id="2671" href="Data.Tree.AVL.Indexed.html#11197" class="Function">Indexed.map</a> <a id="2683" href="Data.Tree.AVL.NonEmpty.html#2651" class="Bound">f</a> <a id="2685" href="Data.Tree.AVL.NonEmpty.html#2659" class="Bound">t</a>

<a id="2688" class="Keyword">module</a> <a id="2695" href="Data.Tree.AVL.NonEmpty.html#2695" class="Module">_</a> <a id="2697" class="Symbol">{</a><a id="2698" href="Data.Tree.AVL.NonEmpty.html#2698" class="Bound">v</a><a id="2699" class="Symbol">}</a> <a id="2701" class="Symbol">{</a><a id="2702" href="Data.Tree.AVL.NonEmpty.html#2702" class="Bound">V</a> <a id="2704" class="Symbol">:</a> <a id="2706" href="Data.Tree.AVL.Value.html#659" class="Record">Value</a> <a id="2712" href="Data.Tree.AVL.NonEmpty.html#2698" class="Bound">v</a><a id="2713" class="Symbol">}</a> <a id="2715" class="Keyword">where</a>

  <a id="2724" class="Comment">-- The input does not need to be ordered.</a>

  <a id="2769" href="Data.Tree.AVL.NonEmpty.html#2769" class="Function">fromList⁺</a> <a id="2779" class="Symbol">:</a> <a id="2781" href="Data.List.NonEmpty.Base.html#1281" class="Record">List⁺</a> <a id="2787" class="Symbol">(</a><a id="2788" href="Data.Tree.AVL.Value.html#902" class="Record Operator">K&amp;</a> <a id="2791" href="Data.Tree.AVL.NonEmpty.html#2702" class="Bound">V</a><a id="2792" class="Symbol">)</a> <a id="2794" class="Symbol">→</a> <a id="2796" href="Data.Tree.AVL.NonEmpty.html#1436" class="Datatype">Tree⁺</a> <a id="2802" href="Data.Tree.AVL.NonEmpty.html#2702" class="Bound">V</a>
  <a id="2806" href="Data.Tree.AVL.NonEmpty.html#2769" class="Function">fromList⁺</a> <a id="2816" class="Symbol">=</a> <a id="2818" href="Data.List.NonEmpty.Base.html#2820" class="Function">List⁺.foldr</a> <a id="2830" class="Symbol">(</a><a id="2831" href="Data.Product.Base.html#3109" class="Function">uncurry</a> <a id="2839" href="Data.Tree.AVL.NonEmpty.html#1716" class="Function">insert</a> <a id="2846" href="Function.Base.html#3646" class="Function Operator">∘′</a> <a id="2849" href="Data.Tree.AVL.Value.html#1070" class="Function">toPair</a><a id="2855" class="Symbol">)</a> <a id="2857" class="Symbol">(</a><a id="2858" href="Data.Product.Base.html#3109" class="Function">uncurry</a> <a id="2866" href="Data.Tree.AVL.NonEmpty.html#1614" class="Function">singleton</a> <a id="2876" href="Function.Base.html#3646" class="Function Operator">∘′</a> <a id="2879" href="Data.Tree.AVL.Value.html#1070" class="Function">toPair</a><a id="2885" class="Symbol">)</a>

  <a id="2890" class="Comment">-- The output is ordered</a>

  <a id="2918" href="Data.Tree.AVL.NonEmpty.html#2918" class="Function">toList⁺</a> <a id="2926" class="Symbol">:</a> <a id="2928" href="Data.Tree.AVL.NonEmpty.html#1436" class="Datatype">Tree⁺</a> <a id="2934" href="Data.Tree.AVL.NonEmpty.html#2702" class="Bound">V</a> <a id="2936" class="Symbol">→</a> <a id="2938" href="Data.List.NonEmpty.Base.html#1281" class="Record">List⁺</a> <a id="2944" class="Symbol">(</a><a id="2945" href="Data.Tree.AVL.Value.html#902" class="Record Operator">K&amp;</a> <a id="2948" href="Data.Tree.AVL.NonEmpty.html#2702" class="Bound">V</a><a id="2949" class="Symbol">)</a>
  <a id="2953" href="Data.Tree.AVL.NonEmpty.html#2918" class="Function">toList⁺</a> <a id="2961" class="Symbol">(</a><a id="2962" href="Data.Tree.AVL.NonEmpty.html#1487" class="InductiveConstructor">tree</a> <a id="2967" class="Symbol">(</a><a id="2968" href="Data.Tree.AVL.Indexed.html#1702" class="InductiveConstructor">node</a> <a id="2973" href="Data.Tree.AVL.NonEmpty.html#2973" class="Bound">k&amp;v</a> <a id="2977" href="Data.Tree.AVL.NonEmpty.html#2977" class="Bound">l</a> <a id="2979" href="Data.Tree.AVL.NonEmpty.html#2979" class="Bound">r</a> <a id="2981" href="Data.Tree.AVL.NonEmpty.html#2981" class="Bound">bal</a><a id="2984" class="Symbol">))</a> <a id="2987" class="Symbol">=</a> <a id="2989" href="Data.Tree.AVL.Indexed.html#10880" class="Function">toList</a> <a id="2996" href="Data.Tree.AVL.NonEmpty.html#2977" class="Bound">l</a> <a id="2998" href="Data.List.NonEmpty.Base.html#3563" class="Function Operator">++⁺</a> <a id="3002" href="Data.Tree.AVL.NonEmpty.html#2973" class="Bound">k&amp;v</a> <a id="3006" href="Data.List.NonEmpty.Base.html#1327" class="InductiveConstructor Operator">∷</a> <a id="3008" href="Data.Tree.AVL.Indexed.html#10880" class="Function">toList</a> <a id="3015" href="Data.Tree.AVL.NonEmpty.html#2979" class="Bound">r</a>
</pre></body></html>